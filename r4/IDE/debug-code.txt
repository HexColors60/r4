| debug code
| PHREDA 2015
|--------------------
^r4/lib/gui.txt
^r4/lib/btn.txt
^r4/lib/input.txt

^r4/lib/parse.txt

^r4/lib/fontm.txt
^inc/fntm/droidsans13.txt

^r4/system/mem-ed.txt

^r4/ide/r4-token.txt
^r4/ide/r4-tokenrun.txt
^r4/ide/r4-info.txt
^r4/ide/r4-tokenprint.txt

^r4/lib/codecolor.txt

^r4/lib/trace.txt

#pantaini>	| comienzo de pantalla
#pantafin>	| fin de pantalla
#prilinea	| primera linea visible
#cntlinea

#fuente  	| fuente editable
#fuente> 	| cursor
#$fuente	| fin de texto

|---------------------------------
:<<13 | a -- a
	( fuente >=? )( dup c@
		13 =? ( drop ; )
		drop 1- ) ;

:>>13 | a -- a
	( $fuente <? )( dup c@
		13 =? ( drop 1- ; ) | quitar el 1-
		drop 1+ )
	drop $fuente 2 - ;

:scrollup | 'fuente -- 'fuente
	pantaini> 1- <<13 1- <<13  1+ 'pantaini> !
	prilinea 1? ( 1- ) 'prilinea !
	;

:scrolldw
	pantaini> >>13 2 + 'pantaini> !
	pantafin> >>13 2 + 'pantafin> !
	1 'prilinea +! ;


:drawcur | com -- com
	blink 1? ( drop ; ) drop
	fuente> >? ( ; )
	dup	( fuente> <? )( c@+ 13 =? ( 2drop ; ) gemit )
	$ffffff ink
	printcur drop ;

:barv
	$333333 ink
	0 0 op 0 sh pline
	ccw 2 << 0 op ccw 2 << sh pline
	poli ;

:viewscreen
	xfb> show 'exit dup <f5> >esc< ;

|------------- DRAWCODE
:gotosrc
	<<ip 0? ( drop ; )
	tok>src 'fuente> ! ;

:drawcode
	barv

	0 1 gotoxy
	pantaini>
	0 ( cntlinea <? )(
		gris
		dup prilinea + "%d" print
       	ccw 2 << dup 'tx1 ! 'ccx !
		swap
		drawcur lf
		>>lineacolor0
		0 'tx1 !
		0? ( 2drop cntlinea $fuente )( cr )
		swap 1+ ) drop
	$fuente <? ( 1- ) 'pantafin> !
	fuente>
	( pantafin> >? )( scrolldw )
	( pantaini> <? )( scrollup )
	drop

	showvstack
	[ stepvm gotosrc ; ] <f2>
	[ stepvmn gotosrc ; ] <f3>
	[ resetvm gotosrc ; ] <f4>
	[ viewscreen ; ] <f5>
	;

|--------------------------------------------------------------------
:wordcntok | adr -- adr token cnt
	dup 4+ @ over 12 + @ 20 >> $fff and ;

:drawline
	tokencolor
	dup $ff and
	5 =? ( drop sp tokenstr print cr ; )
	drop
	sp
	tokenstr print allowcr ;

:drawword | nro --
	4 << 'indicepal +
	wordcntok
	( 1? )( 1- swap @+ drawline swap ) 3drop ;

|--------------------------------------------------------------------
#lista )( 8192
#lista> 'lista
#cntlista
#inipal
#inipal>
#curpal

|--------------------------------------------------------------------
:showcode | nro
	curpal 2 << 'lista + @ |4 << 'indicepal +
|	dup 16 + @ 1- swap @ 1-
	@ 1-
	0 ( cntlinea <? )(
		swap 1? ( >>lineacolor0 ) cr
		swap 1+ ) 2drop
|	( over <? )( >>lineacolor0 cr ) 2drop
	;

:makelist
	'lista >r
	indicepal< ( indicepal> <? )(
		dup 8 + @ 1 nand? ( drop dup r!+ )( drop ) | solo palabras
|		dup r!+
		16 + ) drop
	r> dup
	'lista - 2 >> 'cntlista !
	'lista> !

	cntlista cntlinea - 0 max 'inipal !
	cntlista 1- 'curpal !
	;

:drawlista
	'lista inipal 2 << +
	0 ( cntlinea <? )(
		curpal inipal - =? ( "*" print )
		cntlista >=? ( 2drop ; ) -? ( 2drop ; )
		swap
		2 col
		dup @
		@+ "%w" print
		4+ | @+ "%h " print
		14 col
		@+ printinfword |"%h " print
		16 col
		@ printmovword |"%h " print
		cr
		4+ swap 1+ ) 2drop ;

|------------- INFOCODE
:infocode

	0 1 gotoxy
	drawlista

	[ curpal 1- 0 max 'curpal ! ; ] <up>
	[ curpal 1+ cntlista 1- min 'curpal ! ; ] <dn>
	[ curpal cntlinea - 0 max 'curpal ! ; ] <pgup>
	[ curpal cntlinea +  cntlista 1- min 'curpal ! ; ] <pgdn>

	curpal
	inipal <? ( dup 'inipal ! )
	inipal cntlinea + >=? ( dup cntlinea - 1+ 'inipal ! )
	drop

|	25 1 gotoxy chome!
|	curpal drawword
|	showcode
	;

|------------------------------------------------
:drawstatus
	0 0 gotoxy
	$6600 ink 1 linesfill blanco
	'ed.nombre " %s " print
	cyan
	indiceinc> 'indiceinc - 3 >> "| %d Includes | " print
|	cntwords "%d Words | " print
	cntuwords "%d Used Words | " print
	cntuvars  "%d Used Vars" print


	0 rows 1- gotoxy
	$2200 ink 1 linesfill blanco
|	blanco "> " print 'pad 255 inputcr 'compila&run <enter> cr
|	dumpvstack cr
	verde dup ":R%d" print
	blanco "dEBUG " printx

|	<<ip 1? ( @ ) "%h " print

	" |F1-info|F2-step|F3-stepn|F4-reset " print
	amarillo
	"|PILA D |PILA R  " printr
	;


#modoscreen 'drawcode

:debugcode
	resetvm
	'fontdroidsans13 fontm
	clrscr
	rows 2 - 'cntlinea !
	makelist
	4
	show clrscr

		modoscreen exec

		[ modoscreen 'infocode =? ( 'drawcode )( 'infocode ) 'modoscreen ! drop ; ] <f1>

    	drawstatus
		'exit >esc<
		cminiflecha ;

|-----------------------------
:ram
	mark
	here	| --- RAM
	dup 'fuente !
	dup 'fuente> !
	dup '$fuente !
	$3ffff +			| 256kb texto
	'here  ! | -- FREE
	;

:loadtxt | -- cargar texto
	ed.load
	mark
	here 'ed.nombre load 0 swap c!

	|-- queda solo cr al fin de linea
	fuente dup 'pantaini> !
	here ( c@+ 1? )(
		13 =? ( over c@ 10 =? ( drop swap 1+ swap )( drop ) )
		10 =? ( drop c@+ 13 <>? ( drop 1- 13 ) )
		$3A =? ( pick2 'fuente> ! )
		rot c!+ swap ) 2drop
	0 swap c!+ '$fuente ! ;

:errortok
	cls
	errormsg print
	show 'exit >esc<
	;

:main
	0 paper
	ram
	loadtxt
	fuente tokeniza 1? ( errortok ; ) drop
	tokenpostusa
	tokenmem
	debugcode
	;

: main ;
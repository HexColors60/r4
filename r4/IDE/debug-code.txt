| debug code
| PHREDA 2015
|--------------------
^r4/lib/gui.txt
^r4/lib/btn.txt
^r4/lib/input.txt
^r4/lib/xfb.txt
^r4/lib/system.txt
^r4/lib/parse.txt

|^r4/lib/fontm.txt
|^inc/fntm/droidsans13.txt

^r4/system/mem-ed.txt

^r4/compiler/r4-token.txt

^r4/ide/r4-tokenrun.txt
^r4/ide/r4-stackana.txt

^r4/compiler/r4-tokenprint.txt

^r4/lib/codecolor.txt

^r4/lib/trace.txt

#pantaini>	| comienzo de pantalla
#pantafin>	| fin de pantalla
#prilinea	| primera linea visible
#cntlinea

#fuente  	| fuente editable
#fuente> 	| cursor
#$fuente	| fin de texto

#wlist
#wnow

#initok
#initok>
#nowtok>
#cnttok

#varlist )( $fff
#varlistc
#varows

#memstart
#memtype
#rowmem
#colmem

#mode 0

|--------------------------------------------------------------------
:<<13 | a -- a
	( fuente >=? )( dup c@
		13 =? ( drop ; )
		drop 1- ) ;

:>>13 | a -- a
	( $fuente <? )( dup c@
		13 =? ( drop 1- ; ) | quitar el 1-
		drop 1+ )
	drop $fuente 2 - ;

:scrollup | 'fuente -- 'fuente
	pantaini> 1- <<13 1- <<13  1+ 'pantaini> !
	prilinea 1? ( 1- ) 'prilinea !
	;

:scrolldw
	pantaini> >>13 2 + 'pantaini> !
	pantafin> >>13 2 + 'pantafin> !
	1 'prilinea +! ;

:drawcur | com -- com
	blink 1? ( drop ; ) drop
	fuente> >? ( ; )
	dup	( fuente> <? )( c@+ 13 =? ( 2drop ; ) gemit )
	$ffffff ink
	printcur drop ;

:viewscreen
	xfb> show 'exit dup <f5> >esc< ;

:karriba
	fuente> fuente =? ( drop ; )
	dup 1- <<13 swap over - swap
	dup 1- <<13 swap over - rot min + fuente max
	'fuente> ! ;

:kabajo
	fuente> $fuente >=? ( drop ; )
	dup 1- <<13	over swap - swap >>13 1+
	dup 1+ >>13 1+ over - rot min +
	'fuente> ! ;

:kder fuente> $fuente <? ( 1+ 'fuente> ! ; ) drop ;
:kizq fuente> fuente >? ( 1- 'fuente> ! ; ) drop ;
:kpgup  20 ( 1? )( 1- karriba ) drop ;
:kpgdn	20 ( 1? )( 1- kabajo ) drop	;
:khome	fuente> 1- <<13 1+ 'fuente> ! ;
:kend	fuente> >>13  1+ 'fuente> ! ;


|------------- DRAWCODE
:gotosrc
	<<ip 0? ( drop ; )
	tok>src 'fuente> ! ;

|------------ +info
:nocr | adr adr· -- 1/0
	over @ ( over <?  )(
		c@+ 13 =? ( 3drop 0 ; ) drop
		) drop ;

:cadalinea
	( " " ,s dup ,printinfoword " " ,s
		dup 16 + @ nocr 1? )( drop 16 + ) drop
	0 ,c
	;

:buildinfo
	here 'wlist !
	indicepal<
	( indicepal> <? )(	| adr ind
		dup
		@ ,				| graba posicion de palabra
		cadalinea
		16 + ) drop
	0 ,
	;

:inwlist | adr -- adr
	wlist ( @+ 1? )(
		pick2 >? ( drop 4 - 'wnow ! ; ) drop
        >>0 ) 2drop
	wlist 'wnow ! ;

:wline
	wnow 4+
	dup
	blanco fillpr negro printr
	>>0
	'wnow ! ;

|---------------------------------------------
:prevars
	'varlist >a
	indicepal< ( indicepal> <? )(
		dup 8 + @ 1 and? ( over 'indicepal - 4 >> a!+ ) drop
		16 + ) drop
	a> 'varlist - 2 >> 'varlistc ! ;

:showvars
	0 ( varows <? )(
		varlistc >=? ( drop ; )
		dup 2 << 'varlist + @
		dup dic>str @ "%w = " violeta print
		nro>mem @ "%d" blanco print
		cr
		1+ ) drop ;

:panelvar
	sw ccw 5 << - 'tx1 !
	sw 'tx2 !
	0 1 gotoxy
	showvars
	;

:teclavar
	;

|---------------------------------------------
:barv
	$333333 ink
	0 cch op 0 cntlinea 1+ cch * pline
	ccw 2 << cch op ccw 2 << cntlinea 1+ cch * pline
	poli ;

:panelcode
	barv
	0 1 gotoxy
	sw ccw 1+ 5 << - 'tx2 !
	pantaini>
	inwlist
	0 ( cntlinea <? )(
		gris dup prilinea + "%d" print
       	ccw 2 << dup 'tx1 ! 'ccx !
		swap
		drawcur lf
		>>lineacolor0
		wnow @ >? ( wline )
		0 'tx1 !
		0? ( 2drop cntlinea $fuente )( cr )
		swap 1+ ) drop
	$fuente <? ( 1- ) 'pantafin> !
	fuente>
	( pantafin> >? )( scrolldw )
	( pantaini> <? )( scrollup )
	drop
	;

:teclacode
	[ resetvm gotosrc ; ] dup <f12> "F12-reset" $fff37a flink sp
	'karriba <up>	'kabajo <dn>
	'kder	<ri>	'kizq	<le>
	'kpgup	<pgup>	'kpgdn	<pgdn>
	'khome	<home>	'kend	<end>
	;

|---------------------------------------------
:panelstack
	scr
	showvstack
	;

:teclastack
	'vtos.dec dup <f9> "F9-Dec" $fff37a flink sp
	'vtos.hex dup <f10> "F10-Hex" $fff37a flink sp
	'vtos.fix dup <f11> "F11-Fix" $fff37a flink sp
	;

|------------------------------------------------
:em4 $f and "%h" print ;

:emit4byte | dword --
	blanco dup 4 >> em4 dup em4
	gris dup 12 >> em4 dup 8 >> em4
	blanco dup 20 >> em4 dup 16 >> em4
	gris dup 28 >> em4 24 >> em4
	;

:bytemem
	memstart >b
	rowmem ( 1? )(
		cyan b> "%h: " print
		8 ( 1? )( 1- b@+ emit4byte ) drop
		cr 1- ) drop ;

:emit2word
	blanco dup 12 >> em4 dup 8 >> em4 dup 4 >> em4 dup em4
	gris dup 28 >> em4 dup 24 >> em4 dup 20 >> em4 16 >> em4 ;

:wordmem
	memstart >b
	rowmem ( 1? )(
		cyan b> "%h: " print
		8 ( 1? )( 1- b@+ emit2word ) drop
		cr 1- ) drop ;

:emitdword
	b> $4 and? ( blanco )( gris ) drop
	dup 28 >> em4 dup 24 >> em4 dup 20 >> em4 dup 16 >> em4
	dup 12 >> em4 dup 8 >> em4 dup 4 >> em4 em4 ;

:dwordmem
	memstart >b
	rowmem ( 1? )(
		cyan b> "%h: " print
		8 ( 1? )( 1- b@+ emitdword ) drop
		cr 1- ) drop ;

:ch8
	$ff and 32 <? ( 46 nip ) "%k" print ;

:emitchar
	blanco
	dup ch8 dup 8 >> ch8 dup 16 >> ch8 24 >> ch8 ;

:stringmem
	memstart >b
	rowmem ( 1? )(
		cyan b> "%h: " print
		16 ( 1? )( 1- b@+ emitchar ) drop
		cr 1- ) drop ;

:fixmem
	memstart >b
	rowmem ( 1? )(
		cyan b> "%h: " print
		blanco
		8 ( 1? )( 1- b@+ "%f " print ) drop
		cr 1- ) drop ;

:bitpix
	$80000000 ( 1? )(
		2dup and 1? ( $ffffff nip )
		a!+ 1 0>> ) 2drop ;
:bitmem
	memstart >b
	ccx ccy setxy
	rowmem ( 1? )(
		16 ( 1? )( b@+ bitpix 1- ) drop
		sw 16 3 << - 2 << a+
		1- ) drop ;

:32mem
	memstart >b
	ccx ccy setxy
	rowmem 12 * ( 1? )(
		16 3 << ( 1? )( b@+ a!+ 1- ) drop
		sw 16 3 << - 2 << a+
		1- ) drop ;

#memt bytemem wordmem dwordmem stringmem fixmem bitmem 32mem 32mem

:paneldump
	memstart 0? ( drop ; ) drop
	blanco 0 rows 9 - gotoxy
	memtype 2 << 'memt + @ exec ;

:teclamem
	[ d.TOS 'memstart ! ; ] dup <f10> "f10-stk>MEM" $fff37a flink sp
	[ memtype 1+ $7 and 'memtype ! ; ] dup <f11> "f11-TYPE" $fff37a flink sp
	;

#vecmode 'teclacode 'teclavar 'teclamem 'teclastack
#strmode "COD" "VAR" "MEM" "STK"
:teclado
	mode 2 << 'strmode + sp " %s " $0f0 fprint sp
	[ fuente> breakpoint playvm gotosrc ; ] dup <f1> "F1-play2c" $fff37a flink sp
	[ stepvm gotosrc ; ] dup <f2> "F2-step" $fff37a flink sp
	[ stepvmn gotosrc ; ] dup <f3> "F3-stepn" $fff37a flink sp
|	[ fuente> breakpoint ; ] dup <f4> "F4-Break" $fff37a flink sp
	[ viewscreen ; ] dup <f6> "F6-view" $fff37a flink sp
	[ mode 1+ $3 and 'mode ! ; ] <tab>
	'exit >esc<
	mode $3 and 2 << 'vecmode + @ exec
	;

|------------------------------------------------
:drawstatus
	home
	$666666 ink 1 linesfill blanco
|	blanco "> " print 'pad 255 inputcr 'compila&run <enter> cr
|	dumpvstack cr
	verde dup ":R%d" print
	blanco "dEBUG | " printx
	'ed.nombre printx

	indiceinc> 'indiceinc - 3 >> " | %d Inc | " print
|	cntwords "%d Words | " print
	cntuwords "%d Used W | " print
	cntuvars  "%d Used V" print

|	<<ip 1? ( @ ) "%h " print

	0 cntlinea 1+ gotoxy
	$666666 ink 1 linesfill blanco


	0 rows 1- gotoxy
	$666666 ink 1 linesfill blanco
	teclado
	amarillo
	"|      D       |      R         " printr
	;


|------------------------------------------------
| code | var
| mem  | stack
:debugcode
	resetvm
	prevars

|	'fontdroidsans13 fontm
	fonti

	clrscr
	rows 11 - 'cntlinea !
	rows 11 - 'varows !

	freemem 'memstart !
	0 'memtype !
	8 'rowmem !
	cols 20 - 'colmem !

	0 'mode !
	4
|	gotosrc
	show clrscr
		panelcode
        paneldump
		panelvar
		panelstack
    	drawstatus
		cminiflecha ;

|-----------------------------
:ram
	mark
	iniXFB
	here	| --- RAM
	dup 'fuente !
	dup 'fuente> !
	dup '$fuente !
	$3ffff +			| 256kb texto
	'here  ! | -- FREE
	;

:ajpri
	0
	fuente ( pantaini> <? )(
		c@+ 13 =? ( rot 1+ rot rot )
		drop ) drop
	'prilinea ! ;

:loadtxt | -- cargar texto
	ed.load
	mark
	here 'ed.nombre
	getpath
	load 0 swap c!

	|-- queda solo cr al fin de linea
	fuente dup 'pantaini> !
	here ( c@+ 1? )(
		13 =? ( over c@ 10 =? ( drop swap 1+ swap )( drop ) )
		10 =? ( drop c@+ 13 <>? ( drop 1- 13 ) )
		rot c!+ swap ) 2drop
	0 swap c!+ '$fuente !

	ed.ncar 'fuente> +!
	ed.ipan 'pantaini> +!
	ajpri
	;


:errortok
	mark
	'ed.nombre ,s
	errorlincar "|%d|%d|" ,print
	errormsg ,s
	nroerror 3 <? ( poserror " '%w'" ,print ) drop
	"debug.err" savemem
	trace
	;

:main
	0 'paper !
	ram
	loadtxt
	fuente tokeniza 1? ( errortok ; ) drop
	tokenpostusa
	buildinfo
	tokenmem
	debugcode
	fuente> fuente - 'ed.ncar !
	pantaini> fuente - 'ed.ipan !
	ed.save
	;

: main ;
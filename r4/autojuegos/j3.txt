| Adivina la palabra
| 2017 PHEDA
|-----------------
^r4/lib/gui.txt
^r4/lib/part16.txt
^r4/lib/plqlib.txt

^r4/lib/vesprite.txt
^./fx.vsp

#ti 0 0
#fx 0 0
#objs 0 0
#movs 0 0

#palok )( 32
#palmx )( 32

#pallen 4

|-----------------------------
:tletra
	>b
	b@+ b@+ atxy
	b@+ ink b@ emitf
	;

:+letra | char x y --
	'tletra 'fx p!+ >a
	swap a!+ a!+
	a!+ a!+
	;

|-----------------------------
:stsprite
	dup 7 2 << + @ 0? ( 2drop ; ) drop
	>b
	mpush
	b@+ b@+ 0 mtransi
	b@+ mrotxi
	b@+ mrotyi
	b@+ mrotzi
	b@+ dup dup mscalei
	b@+ 3dvesprite
	mpop
	;

:+sprite | 'spr size x y --
	'stsprite 'objs p!+ >a
	swap a!+ a!+		| x y
	0 0 0 a!+ a!+ a!+	| rx ry rz
	a!+					| s
	a!+					| sprite
	1 a!+				| estado
	;

:sprite.n | nro -- spr
	6 << 'objs 4+ @ + ;

:nxy.sprite | x y nro --
	sprite.n 4+ >a swap a!+ a! ;

:ns.sprite | s nro --
	sprite.n 24 + ! ;

:e.sprite | e nro --
	sprite.n 8 2 << + ! ;

:xy.sprite | x y obj --
	4+ >a swap a!+ a! ;


|---------- animadores
#deltat
#prevt
#t0

:itime
	msec 'prevt ! 0 'deltat ! ;

:dtime
	msec dup prevt - 'deltat ! 'prevt ! ;

:llerp | a b -- r | a + t * (b - a) | t 0.0 .. 1.0
	over - t0 16 *>> + ;
:clerp | a b -- r | a + t * (b - a) | t 0.0 .. 1.0
	over - t0 cos abs 16 *>> + ;
:slerp | a b -- r | a + t * (b - a) | t 0.0 .. 1.0
	over - t0 sin abs 16 *>> + ;

|-------------------
:movlinea
	>a
	a@+ deltat + dup a> 4 - !
	16 << a@+ / 1.0 min 't0 !
	a@+
	4+ a@+ a@+ llerp over !	| x
	4+ a@+ a@+ llerp swap !	| y
	t0 1.0 - 1? ( drop ; )
	;

:+linea | xf yf obj time --
	'movlinea 'movs p!+ >a
	0 a!+ 1000 *. a!+	| 0 tmax
	dup a!+		| obj
	| xf yf obj
	4+ dup @ a!+ rot a!+
	4+ @ a!+ a!
	;

:movlin
	>a
	a@+ deltat + dup a> 4 - !
	16 << a@+ / 0.25 min 't0 !
	a@+
	4+ a@+ a@+ slerp over !	| x
	4+ a@+ a@+ slerp swap !	| y
	t0 0.25 - 1? ( drop ; )
	;

:+lin | xf yf obj time --
	'movlin 'movs p!+ >a
	0 a!+ 1000 *. a!+	| 0 tmax
	dup a!+		| obj
	| xf yf obj
	4+ dup @ a!+ rot a!+
	4+ @ a!+ a!+
	;

:movcircle
	>a
	a@+ deltat + dup a> 4 - !
	16 << a@+ 2* / 0.5 min 't0 !

	t0 a@+ -? ( - neg )( + ) sincos
	a@+ rot over *. >r *. r> swap
	a@+ rot + a@+ rot + | x y
	a@+ xy.sprite

	t0 0.5 - 1? ( drop ; )
	;

:+swap	| v1 v2 suma tiempo --
	'movcircle 'movs p!+ >a
	0 a!+ a!+	| 0 tmax
	a!+			| suma

	over 4+ @ over 4+ @
	2dup - abs 2/ a!+	| ancho
	+ 2/ a!+ 		| centro x
	over 8 + @ over 8 + @
	+ 2/ a!+ 		| centro y
	a!+			| v1
	a!+			| v2
	;

:timex
	>a
	a@+ deltat + dup a> 4 - !
	a@+ >=? ( drop a@ exec 0 ; )
	drop ;

:+time | 'exec tiempo --
	'timex 'ti p!+ >a
	0 a!+ a!+ a!  ;

|--------------
:buildt | ypos "" --
	1 0.5 setfont3d
	sizeprint 2/ neg	| y str xini
	rot rot
	( c@+ 1? )(
		dup emitsize >r
		$ffffff pick4 pick4 +letra
		rot r> + rot rot )
	4drop
	;



:swapc | adr1 adr2
	dup c@ pick2 c@ rot c! swap c! ;

:prepal | "" --
	'fx p.clear
	dup 'palok strcpy
	dup 'palmx strcpy
	count nip
	1- 'palmx swap
	( 1? )(
		rand over mod abs 1+
		pick2 + pick2 swapc
		swap 1+ swap 1- ) 2drop
	-0.3 'palmx buildt
	;

|--------------
#dic
#dici
#dicc 

:loaddic
	here dup 'dic !
	"r4/rtv/juegos/listapalabras.txt" load
	0 swap !+
	dup 'dici ! 'here !
	0 'dicc !
	dic trim
	( dup c@ 1? )( drop
		dup , 1 'dicc +!
		( c@+ $ff and 32 >? )( drop ) drop
		0 over 1- c!
		trim ) drop
	'here !
	;

:randdic
	(
	rnd $7fffffff and dicc mod
	2 << dici + @
	count pallen <>? )( 2drop ) drop ;

|--------------
#xcam #ycam #zcam 2.0

:freelook
	xymouse
	sh 2/ - 7 << swap
	sw 2/ - neg 7 << swap
	neg mrotx
	mroty ;

:main
	33
	itime
	time + + rndseed
	show0 clrscr
		fonti
		dup "%d " print
		dicc "%d" print
		omode
|		freelook
|		msec 4 << mroty
		xcam ycam zcam mtrans

		7 0.4 setfont3d
		0 0 pos 3.0 2.0 dim home
		azul
		"Adivina la Palabra" printc cr
		pallen "de %d letras" printc

		dtime
		'ti p.draw

		'movs p.draw
		'objs p.draw

		1 0.5 setfont3d

		'fx p.draw

		[ randdic prepal ; ] <f1>
		[ 0.3 'palok buildt ; ] <f2>
		[ pallen 1+ 15 >? ( 4 nip ) 'pallen ! ; ] <f3>
		'exit >esc<
		cminiflecha ;



:memoria
	mark
	256 'ti p.ini
	256 'objs p.ini
	256 'movs p.ini
	1024 'fx p.ini
	loaddic
	;

: memoria main ;

| R4i86.txt - PHREDA 2009,2017
| Generacion de codigo para i86-FASM
| Version conversion directa
|
| .. [esp] - pila R
| .. [ebp] eax - pila D
|
|--------------------------------------------

^r4/lib/gui.txt
^r4/lib/parse.txt
|^r4/lib/trace.txt

|----- nro de str y label
#nstr | nro de string
#nlabel | nro de etiqueta

::ini.compila
	0 'nstr ! 0 'nlabel ! ;

|----- pila de compilador
#pilac )( 256	#pilac> 'pilac

:>pilac pilac> ! 4 'pilac> +! ;
:<pilac -4 'pilac> +! pilac> @ ;
:-pilac 'pilac 'pilac> ! ;

|--- asm plano
:label	"_" ,s ,h 	;
:jmp,	"jmp " ,s label ,cr ;

:,DUP
	"lea ebp,[ebp-4]" ,ln
	"mov [ebp],eax" ,ln ;
:,DROP
	"mov eax,[ebp]" ,ln
	"lea ebp,[ebp+4]" ,ln ;
:,NIP
	"lea ebp,[ebp+4]" ,ln ;
:,2DROP
	"mov eax,[ebp+4]" ,ln
	"lea ebp,[ebp+8]" ,ln ;
:,3DROP
	"mov eax,[ebp+8]" ,ln
	"lea ebp,[ebp+12]" ,ln ;
:,4DROP
	"mov eax,[ebp+12]" ,ln
	"lea ebp,[ebp+16]" ,ln ;

:,OVER
	,DUP "mov eax,[ebp+4]" ,ln ;
:,PICK2
	,DUP "mov eax,[ebp+4*2]" ,ln ;
:,PICK3
	,DUP "mov eax,[ebp+4*3]" ,ln ;
:,PICK4
	,DUP "mov eax,[ebp+4*4]" ,ln ;
:,SWAP
	"xchg eax,[ebp]" ,ln ;
:,ROT
	"mov edx,[ebp]" ,ln
	"mov [ebp],eax" ,ln
	"mov eax,[ebp+4]" ,ln
	"mov [ebp+4],edx" ,ln ;
:,2DUP
	"mov edx,[ebp]" ,ln
	"mov [ebp-4],eax" ,ln
	"mov [ebp-8],edx" ,ln
	"lea ebp,[ebp-8]" ,ln ;
:,2OVER
	"mov [ebp-4],eax" ,ln
	"lea ebp,[ebp-8]" ,ln
	"pushd [ebp+16]" ,ln
	"popd [ebp]" ,ln
	"mov eax,[ebp+12]" ,ln ;
:,2SWAP
	"pushd [ebp+4]" ,ln
	"mov [ebp+4],eax" ,ln
	"pushd [ebp+8]" ,ln
	"mov eax,[ebp]" ,ln
	"mov [ebp+8],eax" ,ln
	"popd [ebp]" ,ln
	"pop eax" ,ln ;

:,EXEC
	"mov ecx,eax" ,ln
	,DROP
	over @ $ff and 			| word ;
	16 =? ( "jmp ecx" )( "call ecx" ) nip ,ln ;

:,>R
	"push eax" ,ln ,DROP ;
:,R>
	,DUP "pop eax" ,ln ;
:,R@
	,DUP "mov eax,[esp]" ,ln ;

:,AND
	"and eax,[ebp]" ,ln ,NIP ;
:,OR
	"or eax,[ebp]" ,ln ,NIP ;
:,XOR
	"xor eax,[ebp]" ,ln ,NIP ;
:,NOT
	"not eax" ,ln ;

:,+
	"add eax,[ebp]" ,ln
	,NIP ;
:,-
	"neg eax" ,ln
	"add eax,[ebp]" ,ln
	,NIP ;
:,*
	"imul eax,dword [ebp]" ,ln
	,NIP ;
:,/
	"mov ecx,eax" ,ln
	,DROP
	"cdq" ,ln
	"idiv ecx" ,ln ;
:,*/
	"mov ecx,eax" ,ln
	,DROP
	"cdq" ,ln
	"imul dword [ebp]" ,ln
	"idiv ecx" ,ln
	,NIP ;
:,*>>
	"mov ecx,eax" ,ln
	,DROP
	"cdq" ,ln
	"imul dword [ebp]" ,ln
	"shrd eax,edx,cl" ,ln
	"sar edx,cl" ,ln
	"test cl,32" ,ln
	"cmovne	eax,edx" ,ln
	,NIP ;
:,<</
	"mov ecx,eax" ,ln
	"mov ebx,[ebp]" ,ln
	,2DROP
	"cdq" ,ln
    "shld edx,eax,cl" ,ln
	"shl eax,cl" ,ln
	"idiv ebx" ,ln ;
:,/MOD
	"mov ecx,eax" ,ln
	"mov eax,[ebp]" ,ln
	"cdq" ,ln
	"idiv ecx" ,ln
	"mov [ebp],eax" ,ln
	"mov eax,edx" ,ln ;
:,MOD
	"mov ecx,eax" ,ln
	,DROP
	"cdq" ,ln
	"idiv ecx" ,ln
	"mov eax,edx" ,ln ;
:,<<
	"mov ecx,eax" ,ln ,DROP
	"sal eax,cl" ,ln ;
:,>>
	"mov ecx,eax" ,ln ,DROP
	"sar eax,cl" ,ln ;
:,0>>
	"mov ecx,eax" ,ln ,DROP
	"shr eax,cl" ,ln ;
:,ABS
	"cdq" ,ln
	"add eax,edx" ,ln
	"xor eax,edx" ,ln ;
:,CLZ
	"bsr eax,eax" ,ln
	"xor eax,31" ,ln ;
:,NEG
	"neg eax" ,ln ;
:,1+
	"inc eax" ,ln ;
:,4+
	"add eax,4" ,ln ;
:,1-
	"dec eax" ,ln ;
:,2/
	"sar eax,1" ,ln ;
:,2*
	"sal eax,1" ,ln ; | "add eax,eax" ,ln

:,@
	"mov eax,dword [eax]" ,ln ;
:,C@
	"movsx eax,byte [eax]" ,ln ;
:,W@
	"movsx eax,word [eax]" ,ln ;
:,!
	"mov ecx,[ebp]" ,ln
	"mov [eax],ecx" ,ln
	,2DROP ;
:,C!
	"mov ecx,[ebp]" ,ln
	"mov byte [eax],cl" ,ln
	,2DROP ;
:,W!
	"mov ecx,[ebp]" ,ln
	"mov word [eax],cx" ,ln
	,2DROP ;
:,+!
	"mov ecx,[ebp]" ,ln
	"add [eax],ecx" ,ln
	,2DROP ;
:,W+!
	"mov ecx,[ebp]" ,ln
	"add word [eax],cx" ,ln
	,2DROP ;
:,C+!
	"mov ecx,[ebp]" ,ln
	"add byte [eax],cl" ,ln
	,2DROP ;
:,!+
	"mov ecx,[ebp]" ,ln
	"mov [eax],ecx" ,ln
	"add eax,4" ,ln
	,NIP ;
:,W!+
	"movsx ecx,word [ebp]" ,ln
	"mov [eax],cx" ,ln
	"add eax,2" ,ln
	,NIP ;
:,C!+
	"movsx ecx,byte [ebp]" ,ln
	"mov [eax],cl" ,ln
	"inc eax" ,ln
	,NIP ;
:,@+
	"mov ecx,[eax]" ,ln
	"add eax,4" ,ln
	"mov [ebp-4],eax" ,ln
	"mov eax,ecx" ,ln
	"lea ebp,[ebp-4]" ,ln ;
:,W@+
	"movsx ecx,word [eax]" ,ln
	"add eax,2" ,ln
	"mov [ebp-4],eax" ,ln
	"mov eax,ecx" ,ln
	"lea ebp,[ebp-4]" ,ln ;
:,C@+
	"movsx ecx,byte [eax]" ,ln
	"inc eax" ,ln
	"mov [ebp-4],eax" ,ln
	"mov eax,ecx" ,ln
	"lea ebp,[ebp-4]" ,ln ;

:,>A
	"mov esi,eax" ,ln
	,DROP ;
:,A>
	,DUP
	"mov eax,esi" ,ln ;
:,A@
	,DUP
	"mov eax,dword [esi]" ,ln ;
:,A!
	"mov dword [esi],eax" ,ln
	,DROP ;
:,A+
	"add esi,eax" ,ln
	,DROP ;
:,A@+
	,DUP
	"mov eax,dword [esi]" ,ln
	"add esi,4" ,ln ;
:,A!+
	"mov dword [esi],eax" ,ln
	"add esi,4" ,ln
	,DROP ;
:,>B
	"mov edi,eax" ,ln
	,DROP ;
:,B>
	,DUP
	"mov eax,edi" ,ln ;
:,B@
	,DUP
	"mov eax,dword [edi]" ,ln ;
:,B!
	"mov dword [edi],eax" ,ln
	,DROP ;
:,B+
	"add edi,eax" ,ln
	,DROP ;
:,B@+
	,DUP
	"mov eax,dword [edi]" ,ln
	"add edi,4" ,ln ;
:,B!+
	"mov dword [edi],eax" ,ln
	"add edi,4" ,ln
	,DROP ;

:,MOVE
	"push edi esi" ,ln
	"mov ecx,eax" ,ln
	"mov esi,dword[ebp]" ,ln
	"mov edi,dword[ebp+4]" ,ln
	"rep movsd" ,ln
	"pop esi edi" ,ln
	,3DROP ;
:,MOVE>
	"push edi esi" ,ln
	"mov ecx,eax" ,ln
	"mov esi,dword[ebp]" ,ln
	"mov edi,dword[ebp+4]" ,ln
	"lea esi,[esi+ecx*4-4]" ,ln
	"lea edi,[edi+ecx*4-4]" ,ln
	"std" ,ln
	"rep movsd" ,ln
	"cld" ,ln
	"pop esi edi" ,ln
	,3DROP ;
:,FILL
	"push edi" ,ln
	"mov ecx,eax" ,ln
	"mov edi,dword[ebp]" ,ln
	"mov eax,dword[ebp+4]" ,ln
	"rep stosd" ,ln
	"pop edi" ,ln
	,3DROP ;

:,CMOVE
	"push edi esi" ,ln
	"mov ecx,eax" ,ln
	"mov esi,dword[ebp]" ,ln
	"mov edi,dword[ebp+4]" ,ln
	"rep movsb" ,ln
	"pop esi edi" ,ln
	,3DROP ;
:,CMOVE>
	"push edi esi" ,ln
	"mov ecx,eax" ,ln
	"mov esi,dword[ebp]" ,ln
	"mov edi,dword[ebp+4]" ,ln
	"lea esi,[esi+ecx-1]" ,ln
	"lea edi,[edi+ecx-1]" ,ln
	"std" ,ln
	"rep movsb" ,ln
	"cld" ,ln
	"pop esi edi" ,ln
	,3DROP ;
:,CFILL
	"push edi" ,ln
	"mov ecx,eax" ,ln
	"mov edi,dword[ebp]" ,ln
	"mov eax,dword[ebp+4]" ,ln
	"rep stosb" ,ln
	"pop edi" ,ln
	,3DROP ;

:,0?
	"or eax,eax" ,ln
	"jnz " ,s ;
:,+?
	"or eax,eax" ,ln
	"js " ,s ;
:,-?
	"or eax,eax" ,ln
	"jns " ,s ;
:,1?
	"or eax,eax" ,ln
	"jz " ,s ;

:,=?
	"mov ecx,eax" ,ln ,DROP
	"cmp eax,ecx" ,ln
	"jnz " ,s ;
:,<?
	"mov ecx,eax" ,ln ,DROP
	"cmp eax,ecx" ,ln
	"jge " ,s ;
:,>?
	"mov ecx,eax" ,ln ,DROP
	"cmp eax,ecx" ,ln
	"jle " ,s ;
:,<=?
	"mov ecx,eax" ,ln ,DROP
	"cmp eax,ecx" ,ln
	"jg " ,s ;
:,>=?
	"mov ecx,eax" ,ln ,DROP
	"cmp eax,ecx" ,ln
	"jl " ,s ;
:,<>?
	"mov ecx,eax" ,ln ,DROP
	"cmp eax,ecx" ,ln
	"jz " ,s ;
:,and?
	"mov ecx,eax" ,ln ,DROP
	"test eax,ecx" ,ln
	"jz " ,s ;
:,nand?
	"mov ecx,eax" ,ln ,DROP
	"test eax,ecx" ,ln
	"jnz " ,s ;

|--------- Sistema
:,END		"jmp SYSEND" ,ln ;

:,FFIRST	"call SYSFFIRST" ,ln ;
:,FNEXT  	"call SYSFNEXT" ,ln ;

:,LOAD		"call SYSLOAD" ,ln ;
:,SAVE		"call SYSSAVE" ,ln ;
:,APPEND	"call SYSAPPEND" ,ln ;

:,UPDATE	"call SYSUPDATE" ,ln ;
:,REDRAW	"call SYSREDRAW" ,ln ;

:,MSEC		"call SYSMSEC" ,ln ;
:,TIME		"call SYSTIME" ,ln ;
:,DATE		"call SYSDATE" ,ln ;
:,RUN		"call SYSRUN" ,ln ;
:,SYSTEM	"call SYSYSTEM" ,ln ;

:,SW		,DUP "mov eax,XRES" ,ln ;
:,SH		,DUP "mov eax,YRES" ,ln ;
:,MEM		,DUP "mov eax,[FREE_MEM]" ,ln ;
:,FRAMEV	,DUP "mov eax,SYSFRAME" ,ln ;

:,XYMOUSE | -- x y
	"mov [ebp-4],eax" ,ln
	"lea ebp,[ebp-8]" ,ln
	"mov eax,[SYSXYM]" ,ln
	"mov ecx,eax" ,ln
	"and ecx,$ffff" ,ln
	"shr eax,16" ,ln
	"mov [ebp],ecx" ,ln ;
:,BMOUSE | -- c
	,DUP "mov eax,[SYSBM]" ,ln ;

:,KEY! | v --
	"mov [SYSKEY],eax" ,ln ,DROP ;
:,KEY	| -- c
	,DUP "mov eax,[SYSKEY]" ,ln ;

|---- no implementadas
:,CNTJOY
	,DUP "xor eax,eax" ,ln ;
:,GETJOY ;
:,SLOAD ;
:,SPLAY  ,DROP ;
:,SINFO ;
:,SSET  ,nip ,nip ,nip ,DROP ;

|----------- en asmbase.txt
:,SQRT
:,INK :,INK@ :,ALPHA
:,OP :,LINE :,CURVE :,CURVE3 :,PLINE :,PCURVE :,PCURVE3 :,POLI
:,FCOL :,FCEN :,FMAT :,SFILL :,LFILL :,RFILL :,TFILL
:,OPENURL
:,DOCINI :,DOCEND :,DOCAT :,DOCLINE :,DOCTEXT :,DOCFONT :,DOCBIT :,DOCRES :,DOCSIZE
	;

|------------ Optimizados
#strtos )( 32

:,TOS 'strtos ,s ;

:o,=?
	"cmp eax," ,s ,TOS ,cr
	"jnz " ,s ;
:o,<?
	"cmp eax," ,s ,TOS ,cr
	"jge " ,s ;
:o,>?
	"cmp eax," ,s ,TOS ,cr
	"jle " ,s ;
:o,<=?
	"cmp eax," ,s ,TOS ,cr
	"jg " ,s ;
:o,>=?
	"cmp eax," ,s ,TOS ,cr
	"jl " ,s ;
:o,<>?
	"cmp eax," ,s ,TOS ,cr
	"jz " ,s ;
:o,and?
	"test eax," ,s ,TOS ,cr
	"jz " ,s ;
:o,nand?
	"test eax," ,s ,TOS ,cr
	"jnz " ,s ;
:o,EXEC
	"mov ecx," ,s ,TOS ,cr
	over @ $ff and 			| word ;
	16 =? ( "jmp ecx" )( "call ecx" ) nip ,ln ;

:o,AND
	"and eax," ,s ,TOS ,cr ;
:o,OR
	"or eax," ,s ,TOS ,cr ;
:o,XOR
	"xor eax," ,s ,TOS ,cr ;
:o,+
	"add eax," ,s ,TOS ,cr ;
:o,-
	"sub eax," ,s ,TOS ,cr ;
:o,*
	"imul eax," ,s ,TOS ,cr ;
:o,/
	"mov ecx," ,s ,TOS ,cr
	"cdq" ,ln
	"idiv ecx" ,ln ;
:o,*/
	"mov ecx," ,s ,TOS ,cr
	"cdq" ,ln
	"imul dword [ebp]" ,ln
	"idiv ecx" ,ln
	,NIP ;
:o,*>>
	"cdq" ,ln
	"imul dword [ebp]" ,ln
	,nip
	dup tok>cte
	32 <? ( "shrd eax,edx," ,s ,d ,cr ; )
	32 >? ( "sar edx," ,s dup 32 - ,d ,cr )
	drop
	"mov eax,edx" ,ln ;
:o,<</
	"mov ebx,eax" ,ln
	,DROP
	"cdq" ,ln
    "shld edx,eax," ,s ,TOS ,cr
	"shl eax," ,s ,TOS ,cr
	"idiv ebx" ,ln ;
:o,/MOD
	"mov ebx," ,s ,TOS ,cr
	"cdq" ,ln
	"idiv ebx" ,ln
	"mov [ebp],eax" ,ln
	"mov eax,edx" ,ln ;
:o,MOD
	"mov ecx," ,s ,TOS ,cr
	"cdq" ,ln
	"idiv ecx" ,ln
	"mov eax,edx" ,ln ;
:o,<<
	"sal eax," ,s ,TOS ,cr ;
:o,>>
	"sar eax," ,s ,TOS ,cr ;
:o,0>>
	"shr eax," ,s ,TOS ,cr ;

:o,@
	"mov eax,dword[", ,s ,TOS "]" ,ln ;
:o,C@
	"movsx eax,byte[", ,s ,TOS "]" ,ln ;
:o,W@
	"movsx eax,word[", ,s ,TOS "]" ,ln ;
:o,!
	"mov dword[" ,s ,TOS "],eax" ,ln ,DROP ;
:o,C!
	"mov byte[" ,s ,TOS "],ax" ,ln ,DROP ;
:o,W!
	"mov word[" ,s ,TOS "],al" ,ln ,DROP ;
:o,+!
	"add dword[" ,s ,TOS "],eax" ,ln ,DROP ;
:o,W+!
	"add word[" ,s ,TOS "],ax" ,ln ,DROP ;
:o,C+!
	"add byte[" ,s ,TOS "],al" ,ln ,DROP ;

:o,!+
	"mov ecx," ,s ,TOS ,cr
	"mov dword[ecx],eax" ,ln
	"add ecx,4" ,ln
	"mov eax,ecx" ,ln ;
:o,W!+
	"mov ecx," ,s ,TOS ,cr
	"mov word[ecx],ax" ,ln
	"add ecx,2" ,ln
	"mov eax,ecx" ,ln ;
:o,C!+
	"mov ecx," ,s ,TOS ,cr
	"mov byte[ecx],al" ,ln
	"add ecx,1" ,ln
	"mov eax,ecx" ,ln ;

:o,@+
	,DUP
	"mov ebx," ,s ,TOS ,cr
	"mov eax,dword[ebx]" ,ln
	"add ebx,4" ,ln
	"lea ebp,[ebp-4]" ,ln
	"mov [ebp],ebx" ,ln ;
:o,W@+
	,DUP
	"mov ebx," ,s ,TOS ,cr
	"movsx eax,word[ebx]" ,ln
	"add ebx,2" ,ln
	"lea ebp,[ebp-4]" ,ln
	"mov [ebp],ebx" ,ln ;
:o,C@+
	,DUP
	"mov ebx," ,s ,TOS ,cr
	"movsx eax,byte[ebx]" ,ln
	"add ebx,1" ,ln
	"lea ebp,[ebp-4]" ,ln
	"mov [ebp],ebx" ,ln ;

:o>A
	"mov esi," ,s ,TOS ,cr ;
:oA!
	"mov dword[esi]," ,s ,TOS ,cr ;
:oA+
	"add esi," ,s ,TOS ,cr ;
:oA!+
	"mov dword[esi]," ,s ,TOS ,cr
	"add esi,4" ,ln ;
:o>B
	"mov edi," ,s ,TOS ,cr ;
:oB!
	"mov dword[edi]," ,s ,TOS ,cr ;
:oB+
	"add edi," ,s ,TOS ,cr ;
:oB!+
	"mov dword[edi]," ,s ,TOS ,cr
	"add edi,4" ,ln ;

#nivel1
0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0
0 0 0 0 0 0 o,exec
0 0 0 0 o,=? o,<? o,>? o,<=? o,>=? o,<>? o,AND? o,NAND? | 23-34
0 0 0 0 0 0 0 0	0 0 0 0 0 0 0 0 0 0 	|--- pilas
o,AND o,OR o,XOR 0  					|--- logicas
o,+ o,- o,* o,/ o,*/ o,*>> o,/MOD o,MOD 0  		|--- aritmeticas | 65
0 0 o,<</ 0 0 0 0 0 0 o,<< o,>> o,0>>
o,@ o,C@ o,W@ o,! o,C! o,W! o,+! o,C+! o,W+!  			|--- memoria
o,@+ o,!+ o,C@+ o,C!+ o,W@+ o,W!+
o>A 0 0 oA! oA+ 0 oA!+ | A
o>B 0 0 oB! oB+ 0 oB!+ | B
0 0 0 0 0 0 | Memory
0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0
0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0

,>A	,A> ,A@ ,A! ,A+ ,A@+ ,A!+

|------------ compila CODIGO
|:nro>dicn2   8 >> 5 << 'indicepal + @ ; | nombre
:nro>dicn   8 >> "w%h" mprint ;			| numero de palabra

:+etiqueta	| -- nueva   // y agrega a pila
	nlabel 1+ dup 'nlabel ! dup >pilac ;

:cpycad | adr --
	( c@+ 1? )( 34 =? ( drop c@+ 34 <>? ( 2drop ; ) ) ,c ) 2drop ;

:salto? | adr++ xx xx -- adr++ xx xx 0/1
	pick2 8 - @ $ff and
	23 >=? ( 34 <=? ( 1 nip ; ) ) 0 nip ;

:,defw
    over 4 - @ 8 >> "w%h:" ,print ,cr ;

:,defv ;

:,lits
	,DUP "mov eax,str" ,s nstr ,h ,cr 1 'nstr +! ;

:,wor
	over @ $ff and 			| word ;
	16 =? ( "jmp " )( "call " ) nip ,s
	dup nro>dicn ,s
	dup tok>name " ; %w" ,print ,cr ;

|.... proximo token optimizado
:dumpopt
	dup "; OPT " ,s ,s pick3 @ $ff and 16 - macroname " " ,s ,s ,cr ;

:litopt
	over tok>cte "%d" mprint
	'strtos strcpy
	exec
	rot 1- rot 4+ rot ;

:,lit
	over @ $ff and 2 << 'nivel1 + @ 1? ( litopt ; ) drop
	dup tok>cte ,DUP "mov eax," ,s ,d ,cr ;

:varopt
	"dword[" 'strtos strcpy
	over nro>dicn 'strtos strcat
	"]" 'strtos strcat
	exec
	rot 1- rot 4+ rot ;

:,var
|	over @ $ff and 2 << 'nivel1 + @ 1? ( varopt ; ) drop
	dup nro>dicn ,DUP "mov eax,dword [" ,s ,s "]" ,s
	dup tok>name " ; %w" ,print ,cr ;

:dwoopt
	over nro>dicn 'strtos strcpy
	exec
	rot 1- rot 4+ rot ;

:,dwo
	over @ $ff and 2 << 'nivel1 + @ 1? ( dwoopt ; ) drop
	dup nro>dicn ,DUP "mov eax," ,s ,s
	dup tok>name " ; '%w" ,print ,cr ;

:dvaopt
	over nro>dicn 'strtos strcpy
	exec
	rot 1- rot 4+ rot ;

:,dva
	over @ $ff and 2 << 'nivel1 + @ 1? ( dvaopt ; ) drop
	dup nro>dicn ,DUP "mov eax," ,s ,s
	dup tok>name " ; '%w" ,print ,cr ;

:,;
	over 8 - @ $ff and
	12 =? ( drop ; ) | call
	22 =? ( drop ; ) | exec
	drop
	"ret" ,ln ;
:,(
	+etiqueta salto? 1? ( swap label ,cr 1 )( swap label ":" ,ln 2 ) >pilac drop ;
:,)(
	<pilac salto? 0? ( drop
			1 =? ( <pilac +etiqueta jmp, label ":" ,ln 1 >pilac drop ; )
			)( drop
			2 =? ( <pilac +etiqueta label ,cr >pilac 3 >pilac drop ; ) )
	drop ;
:,)
	<pilac salto? 0? ( drop
			1 =? ( <pilac label ":" ,ln drop ; )
			2 =? ( <pilac jmp, drop ; )
			3 =? ( <pilac jmp, <pilac label ":" ,ln drop ; )
		)( drop
			2 =? ( <pilac label ,cr drop ; ) )
	drop ;
:,[
	,DUP +etiqueta +etiqueta "mov eax," ,s label ,cr jmp, <pilac label ":" ,ln ;
:,]
	<pilac label ":" ,ln ;

:,0 ;

|---- nivel 0 - sin desordenar la pila
#nivel0 ,0
,defw ,defw ,defv ,defv 0 0
,lit ,lit ,lit ,lit ,lits
,wor ,var ,dwo ,dva
,; ,( ,)( ,) ,[ ,] ,EXEC
,0? ,+? ,-? ,1? ,=? ,<? ,>? ,<=? ,>=? ,<>? ,AND? ,NAND? | 23-34
,DUP ,DROP ,OVER ,PICK2 ,PICK3 ,PICK4 ,SWAP ,NIP	|--- pila
,ROT ,2DUP ,2DROP ,3DROP ,4DROP ,2OVER ,2SWAP
,>R ,R> ,R@ 					|--- pila direcciones
,AND ,OR ,XOR ,NOT  								|--- logicas
,+ ,- ,* ,/ ,*/ ,*>> ,/MOD ,MOD ,ABS  			|--- aritmeticas | 65
,SQRT ,CLZ ,<</ ,NEG ,1+ ,4+ ,1- ,2/ ,2* ,<< ,>> ,0>>
,@ ,C@ ,W@ ,! ,C! ,W! ,+! ,C+! ,W+!  			|--- memoria
,@+ ,!+ ,C@+ ,C!+ ,W@+ ,W!+
,>A	,A> ,A@ ,A! ,A+ ,A@+ ,A!+
,>B	,B> ,B@ ,B! ,B+ ,B@+ ,B!+
,MOVE ,MOVE> ,FILL ,CMOVE ,CMOVE> ,CFILL
,MEM
,FFIRST ,FNEXT
,LOAD ,SAVE ,APPEND			|--- memoria bloques
,UPDATE
,XYMOUSE ,BMOUSE
,KEY! ,KEY
,CNTJOY ,GETJOY
,MSEC ,TIME ,DATE ,END ,RUN 				|--- sistema
,SW ,SH ,REDRAW ,FRAMEV  						|--- pantalla
,INK ,INK@ ,ALPHA							|--- color
,OP ,LINE ,CURVE ,CURVE3 ,PLINE ,PCURVE ,PCURVE3 ,POLI		|--- dibujo
,FCOL ,FCEN ,FMAT ,SFILL ,LFILL ,RFILL ,TFILL
,SLOAD ,SPLAY ,SINFO ,SSET					|--- Sonido
,OPENURL
,DOCINI ,DOCEND ,DOCAT ,DOCLINE ,DOCTEXT ,DOCFONT ,DOCBIT ,DOCRES ,DOCSIZE  |-- impresora
,SYSTEM


| compila nivel 0 de palabras
::,token0 | a -- a.
	dup
	$ff and 2 << 'nivel0 + @
	exec drop ;

:,code | w --
	dup dic>tok @ swap dic>len@
	( 1? )( 1- swap @+
|		"; " ,s dup tokenstr ,print ,cr
|		9 ,c nroi ,stackm ,cr
		,token0 swap
|		"r4/compiler/code.asm" savemem | DEBUG
		) 2drop ;

:compilacode | w --
	dup 1- dic>inf @
	$81 and $80 =? ( 2drop ; )
	drop

	"; " ,s
	dup dic>str @ "%w" ,print
	" | " ,s
	dup dic>mov @ ,printmovword
	" | " ,s
	dup dic>inf @ ,printinfowor
	" len:" ,s dup dic>len@ ,d
	" calls:" ,s dup dic>call@ ,d
	,cr
	cntwords 1- =? ( "inicio:" ,ln )
|	dup cellana
|	cell2reg
|	"; ** vreg:" ,s cntvreg 1- ,d ,cr
|	cellinfo
|	blockinfo
|	tokeninfo

	,code

|	"r4/compiler/code.asm" savemem | DEBUG
	;

::iniCODE ;

::,generateCODE
	0 ( cntwords <? )(
		dup dic>inf @
		1 nand? ( over compilacode )	| codigo
		drop 1+ ) drop ;
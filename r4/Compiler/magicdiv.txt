| calculo de division por multiplicacion
| http://www.flounder.com/multiplicative_inverse.htm
|------------------------
^r4/lib/gui.txt

#ad		| d absoluto
#t
#anc
#p
#q1
#r1
#q2
#r2

#mn		| magic mult
#sn		| shift mult

:calcstep
	1 'p +!
	q1 2* 'q1 !
	r1 2* 'r1 !
	r1 anc >=? ( 1 'q1 +! anc neg 'r1 +! ) drop
	q2 2* 'q2 !
	r2 2* 'r2 !
	r2 ad >=? ( 1 'q2 +! ad neg 'r2 +! ) drop
	;

:calcmagic | d --
	dup abs 'ad !
    $80000000 over 31 0>> + 't !
    t dup 1- swap ad mod - 'anc !
    31 'p !
    $80000000 anc / abs 'q1 !
    $80000000 q1 anc * - abs 'r1 !
	$80000000 ad / abs 'q2 !
	$80000000 q2 ad * - abs 'r2 !
	( calcstep
		ad r2 -	| delta
		q1 =? ( r1 0? ( swap 1+ swap ) drop )
		q1 >? )( drop ) drop
	q2 1+
	swap -? ( drop neg )( drop )
	'mn !
	p 'sn !
	;

:divc | nn -- res
	dup mn sn *>> swap 31 >> -
	;

:main
	show clrscr
		13 calcmagic
		sn mn "%h %d " print cr
		cr
		-141 ( 560 <? )( 33 +
			dup "%d/13=" print
			dup 13 / "%d    " print
			dup divc "%d	" print
			dup $80000000 13 / 31 *>> "%d     " print
			cr ) drop
		cr
		1 ( 1024 <? )(
			dup "%d " print
			32 over clz - "%d " print
			cr
			2* ) drop

		'exit >esc<
		;
: main ;
| Convierte SVG font a spr
| PHREDA 2014
|---------------------------------------------
^r4/lib/gui.txt
^r4/lib/parse.txt
^r4/lib/trace.txt
^r4/lib/dlgfile.txt

^r4/lib/vesprite.txt

#$start
#$end

|------------------ PARSE
:=p | s1 s2 -- 1/0
	( c@+ 1? )( rot c@+ rot - 1? ( 3drop 0 ; ) drop swap ) 3drop 1 ;

:findl | adr "texto" -- adr'/0
	( 2dup =p 0? )( drop swap 1+ dup c@ 32 <? ( 3drop 0 ; ) drop swap ) 2drop ;

:findc | 13 adr -- adr'
	( c@+ 1? )( pick2 =? ( drop nip ; ) drop ) nip nip ;

:find | adr "texto" -- adr'
	( 2dup =p 0? )( drop swap 1+ dup c@ 0? ( 3drop 0 ; ) drop swap ) 2drop ;

:nextline | adr -- adr'
	( c@+ 32 >=? )( drop ) 0? ( nip ; ) drop ;

:>>,
	trim
	dup c@ $2c =? ( drop 1+ ; ) drop ;

|-------------------------
#colsvg
#svgx #svgy

#$spr

:,spr | nro --
	 $spr !+ '$spr ! ;

:getcolor | adr --
	$23 swap findc 0? ( drop ; ) | #
	str$>nro 'colsvg !
	;

|--------buffer----------
#yend #xend
#buffer )( $ffff
#buffer> 'buffer

#xmax #ymax #xmin #ymin

#scale
#sx #sy

#x1 #y1
#x2 #y2
#x3 #y3

:buff.ini
	'buffer 'buffer> ! ;

:,buff | x y t --
	>r swap r> buffer> !+ !+ !+ 'buffer> ! ;

:,line | x y --
	3 ,buff ;

:,op | x y --
	2dup 'yend ! 'xend !
	1 ,buff ;

:,cp | x y --
	2 ,buff ;

:,curve | x y --
	4 ,buff ;

:,cp2 | x y --
	8 ,buff ;

:,cp3 | x y --
	9 ,buff ;

:,curve3 | x y --
	10 ,buff ;

:,last | --
	xend yend 3 ,buff ;

:,poly
	colsvg 4 << 0 $d ,buff ;


| a 14 bits
:buff.redim
	'buffer
	4+
	@+ dup 'xmax ! 'xmin !
	@+ dup 'ymax ! 'ymin !
	( buffer> <? )(
		@+ $a <=? ( drop
			@+ xmax >? ( dup 'xmax ! ) xmin <? ( dup 'xmin ! ) drop
			@+ ymax >? ( dup 'ymax ! ) ymin <? ( dup 'ymin ! ) drop
			)( drop 8 + )
		) drop

	xmax xmin - ymax ymin - max 1.0 swap /. 'scale !
	xmin xmax + 2/ neg 'sx !
	ymin ymax + 2/ neg 'sy !

	'buffer
	( buffer> <? )(
		@+ $a <=? ( drop
			dup @ sx + scale *. swap !+
			dup @ sy + scale *. swap !+
			)( drop 8 + )
		) drop
	;

:xy>o | x y -- d
	2* $3fff0 and swap
	15 << $fffc0000 and or ;

:buff.emit
	'buffer ( buffer> <? )( >r
		r@+
		$a <=? (
			r@+ r@+ xy>o swap or ,spr
		)(
			r@+ r@+ or or ,spr )
		r> ) drop
	0 ,spr ;

|--vesprite
:valxy
	@+ swap @+ rot swap xy>o ;

:e0 8 + ;
:e1 valxy 4 or ,spr ;
:e3 valxy 5 or ,spr ;
:e2 valxy 6 or ,spr 4+ valxy ,spr ;
:e8 valxy 7 or ,spr 4+ valxy ,spr 4+ valxy ,spr ;
:eb	colsvg 8 << $c or ,spr ;

#lemit e0 e1 e2 e3 e0 e0 e0 e0 e8 e0 e0 eb eb eb eb eb

:buff.emitv
	'buffer ( buffer> <? )(
		@+ 2 << 'lemit + @ exec
		) drop
	0 ,spr ;


|----------
:getpoly | adr --
	$22 swap findc 0? ( drop ; ) | "
	getfenro 'svgx ! 1+ getfenro 'svgy ! trim
	svgx svgy ,op
	( dup c@ $22 <>? )( drop
		getfenro 'svgx ! 1+ getfenro 'svgy ! trim
		svgx svgy ,line
		) drop
	,last
	,poly
	;


|------------------------
:getvb | adr --
	getfenro 'x1 ! >>,
	getfenro 'y1 ! >>,
	getfenro 'x2 ! >>,
	getfenro 'y2 ! >>,
	x2 x1 - y2 y1 - dim
	x1 x2 + 2/ y1 y2 + 2/ pos
	;

|----------------------SVG path-----------------------
:lineto | adr -- adr' ; XY
	x1 y1 'y2 ! 'x2 !
	getfenro 'x1 ! >>,
	getfenro 'y1 ! >>,
	x1 y1 ,line
	dup c@ $22 =? ( drop ; )
	$40 <? ( drop lineto ; ) drop ;

:linetor | adr -- adr' ; xy
	x1 y1 'y2 ! 'x2 !
	getfenro 'x1 +! >>,
	getfenro 'y1 +! >>,
	x1 y1 ,line
	dup c@ $22 =? ( drop ; )
	$40 <? ( drop linetor ; ) drop ;

:moveto | adr -- adr' ; XY
	getfenro 'x1 !	>>,
	getfenro 'y1 !  >>,
	x1 y1 ,op
   	dup c@ $22 =? ( drop ; )
	$40 <? ( drop lineto ; ) drop ;

:movetor | adr -- adr' ; xy
	getfenro 'x1 +! >>,
	getfenro 'y1 +! >>,
	x1 y1 ,op
	dup c@ $22 =? ( drop ; )
	$40 <? ( drop linetor ; ) drop ;

:hori | adr -- adr' ; X
	x1 y1 'y2 ! 'x2 !
	getfenro 'x1 !
	x1 y1 ,line ;
:horir | adr -- adr' ; X
	x1 y1 'y2 ! 'x2 !
	getfenro 'x1 +!
	x1 y1 ,line ;
:vert | adr -- adr' ; Y
	x1 y1 'y2 ! 'x2 !
	getfenro 'y1 !
	x1 y1 ,line ;
:vertr | adr -- adr' ; Y
	x1 y1 'y2 ! 'x2 !
	getfenro 'y1 +!
	x1 y1 ,line ;
:ccurve | adr -- adr' ; XYXYXY
	getfenro 'x3 ! >>,
	getfenro 'y3 ! >>,
	getfenro 'x2 ! >>,
	getfenro 'y2 ! >>,
	getfenro 'x1 ! >>,
	getfenro 'y1 ! >>,
	x1 y1 ,cp2
	x2 y2 ,cp3
	x3 y3 ,curve3
	dup c@ $22 =? ( drop ; )
	$40 <? ( drop ccurve ; ) drop
	;
:ccurver | adr -- adr' ; XYXYXY
	getfenro x1 + 'x3 ! >>,
	getfenro y1 + 'y3 ! >>,
	getfenro x1 + 'x2 ! >>,
	getfenro y1 + 'y2 ! >>,
	getfenro x1 + 'x1 ! >>,
	getfenro y1 + 'y1 ! >>,
	x1 y1 ,cp2
	x2 y2 ,cp3
	x3 y3 ,curve3
	dup c@ $22 =? ( drop ; )
	$40 <? ( drop ccurver ; ) drop
	;
:surve | adr -- adr' ; XYXY
|	x1 2* x2 - 'x3 !
|	y1 2* y2 - 'y3 !
	x1 dup x2 - + 'x3 !
	y1 dup y2 - + 'y3 !
	getfenro 'x2 ! >>,
	getfenro 'y2 ! >>,
	getfenro 'x1 ! >>,
	getfenro 'y1 ! >>,
	x1 y1 ,cp2
	x2 y2 ,cp3
	x3 y3 ,curve3
	dup c@ $22 =? ( drop ; )
	$40 <? ( drop surve ; ) drop
	;

:surver | adr -- adr' ; XYXY
|	x1 2* x2 - 'x3 !
|	y1 2* y2 - 'y3 !
	x1 dup x2 - + 'x3 !
	y1 dup y2 - + 'y3 !
	getfenro x1 + 'x2 ! >>,
	getfenro y1 + 'y2 ! >>,
	getfenro x1 + 'x1 ! >>,
	getfenro y1 + 'y1 ! >>,
	x1 y1 ,cp2
	x2 y2 ,cp3
	x3 y3 ,curve3
	dup c@ $22 =? ( drop ; )
	$40 <? ( drop surver ; ) drop
	;
:qurve | adr -- adr' ; XYXY
	getfenro 'x2 ! >>,
	getfenro 'y2 ! >>,
	getfenro 'x1 ! >>,
	getfenro 'y1 ! >>,
	x1 y1 ,cp
	x2 y2 ,curve
	dup c@ $22 =? ( drop ; )
	$40 <? ( drop qurve ; ) drop
	;

:qurver | adr -- adr' ; XYXY
	getfenro x1 + 'x2 ! >>,
	getfenro y1 + 'y2 ! >>,
	getfenro x1 + 'x1 ! >>,
	getfenro y1 + 'y1 ! >>,
	x1 y1 ,cp
	x2 y2 ,curve
	dup c@ $22 =? ( drop ; )
	$40 <? ( drop qurver ; ) drop
	;

:turve | adr -- adr' ; XY
	x1 2* x2 - 'x2 !
	y1 2* y2 - 'y2 !
	getfenro 'x1 !
	getfenro 'y1 !
	x1 y1 ,cp
	x2 y2 ,curve
	dup c@ $22 =? ( drop ; )
	$40 <? ( drop turve ; ) drop
	;

:turver | adr -- adr' ; XY
	x1 2* x2 - 'x2 !
	y1 2* y2 - 'y2 !
	getfenro x1 + 'x1 ! >>,
	getfenro y1 + 'y1 ! >>,
	x1 y1 ,cp
	x2 y2 ,curve
	dup c@ $22 =? ( drop ; )
	$40 <? ( drop qurver ; ) drop
	;

:ellip | adr -- adr' ; XY
:ellipr
	getfenro 'x1 ! >>,
	getfenro 'y1 ! >>,
	getfenro 'x1 ! >>,
	getfenro 'y1 ! >>,
	getfenro 'x1 ! >>,
	getfenro 'y1 ! >>,
	;
:close | adr -- adr'
    ,last
	|,poly
	;

:resolve | adr char -- adr'
	$4d =? ( drop moveto ; )	| M move
	$6d =? ( drop movetor ; )	| m move rel
	$4c =? ( drop lineto ; )	| L lineto
	$6c =? ( drop linetor ; )	| l lineto rel
	$48 =? ( drop hori ; )		| H
	$68 =? ( drop horir ; )		| h
	$56 =? ( drop vert ; )		| V
	$76 =? ( drop vertr ; )		| v
	$43 =? ( drop ccurve ; )	| C
	$63 =? ( drop ccurver ; )	| c
	$53 =? ( drop surve ; )		| S
	$73 =? ( drop surver ; )	| s
	$51 =? ( drop qurve ; ) 	| Q curve
	$71 =? ( drop qurver ; )	| q curve rel
	$54 =? ( drop turve ; )		| T
	$74 =? ( drop turver ; )	| t
	$41 =? ( drop ellip ; )		| A
	$61 =? ( drop ellipr ; )	| a
	$5a =? ( drop close ; )		| Z
	$7a =? ( drop close ; )		| z
	drop ;


:getpath | adr --
	0 dup 'x1 ! 'y1 !
	0 dup 'x2 ! 'y2 !
	0 dup 'x3 ! 'y3 !
	3 +
	( trim c@+ 0? ( drop 1- ; )
		$22 <>? )( resolve ) drop 
		,poly ;

|------------------------

:decodepoly | adr adrpoly -- adr
	dup "fill=" findl 1? ( getcolor ) drop
	dup "points=" findl 1? ( getpoly ) drop
	drop
	;

:decodepath | adr adrpoly -- adr
	dup "fill=" findl 1? ( getcolor ) drop
	dup "d=" findl 1? ( getpath ) drop
	drop
	">" find
	;


:next> | adr --
	( c@+ 1? )(
		$3e =? ( drop ; )
		drop ) drop 1- ;

:decode<svg | adr -- adr
	5 + | <svg

	dup "viewbox=" findl 1? ( getvb ) drop
	drop
	;


:es<svg
	next> ;
:es<polygon
	next> ;

:esPd
	getpath ;
:esPi
	4+ ( c@+ 0? ( drop 1- ; )
		$22 <>? )( drop ) drop ;

:pathd
	dup "id=" =p 1? ( drop esPi )( drop )
	dup "d=" =p 1? ( drop esPd ; ) drop
|	dup "fill=" =p 1? ( drop esPf )( drop )
|	dup "style=" =p 1? ( drop esPs )( drop )
	1+
	;

:es<path
	( trim dup c@ 1? )(
		$3e =? ( drop 1+ ; ) drop | >
		pathd ) drop ;

:es<
|	dup "svg" =p 1? ( drop es<svg ; ) drop
|	dup "polygon" =p 1? ( drop es<polygon ; ) drop
	dup "path" =p 1? ( drop es<path ; ) drop

|	dup "polyline" =p 1? ( decodepath ; ) drop
|	dup "rect" =p 1? ( decodepath ; ) drop
|	dup "circle" =p 1? ( decodepath ; ) drop
|	dup "ellipse" =p 1? ( decodepath ; ) drop
|	dup "line" =p 1? ( decodepath ; ) drop

	next> ;

:convertsvg | adr --
	buff.ini

	$end '$spr !
	( trim c@+ 1? )(
		$3c =? ( drop es< )( drop )
		) 2drop

	buff.redim
|	buff.emit
	buff.emitv
	;

|------------- carga
:cargasvg
	"media/svg/" dlgfileload 0? ( drop ; )
	$start swap load 0 swap !+ '$end !
	$start convertsvg
	;

|------------- muestra
:muestraspr
	0.8 %s dup dim 0 dup fpos
	$c0c0c0 ink gc.fbox
	gris 8 rgrilla
|	$end nsprite
	$end vesprite
	;

:main
|	33
	show clrscr
|		dup "%d " print cr cr2

		cyan cr
		'cargasvg dup <f1> "F1-Load SVG" sp btnt
|		'savespr "SAVE" sp btnt
		cr cr
|		buffer> 'buffer - 3 / "%d " print cr
|		xmin ymin "%f %f" print cr
|		xmax ymax "%f %f" print cr
		muestraspr

		'exit >esc<
		cminiflecha ;

:me mark here dup '$start ! dup '$end ! '$spr ! 0 $spr ! ;

: me main
$end $spr over - "mem/notepad.spr" save
;

| Editor de includes en vsprite
| PHREDA 2010
|--------------------------------------------------
^r4/lib/gui.txt
^r4/lib/btn.txt
^r4/lib/input.txt
^r4/lib/trazo.txt

^r4/lib/trace.txt

#nombre )( 256

#orden 0
#nroico 0
#nomico )( 32

#actual
#cntvar
#indexn )( 3200 | 100 nombres de 32
#index )( $ffff

|------------------------------
#icohw $f0f
#icomem )( 256

:pl
	ink@
	dup px!+ dup px!+ dup px!+ dup px!+ dup px!+ dup px!+ dup px!+ px!+
	sw 8 - px+! ;
:px8
	pl pl pl pl pl pl pl pl
	sw 3 << neg 8 + px+! ;
:px+
	8 px+! ;
:pxline
	3 << sw 3 << swap - px+! ;

::drawzico
	>r r@+ dup $ff and swap 8 >> $ff and
	( 1? )( r@+
		pick2 ( 1? )( 1- swap
			1 and? ( px8 )( px+ )
			2/ swap ) 2drop
		over pxline
		1- ) 2drop
	rdrop ;

:invp | x y --
	2 << 'icomem + >r
	1 swap <<
	r @ xor r> !
	;

#xa #ya
:dn
	xymouse 'ya ! 'xa ! ;
:mv
	xa ya xymouse op line ;
:up
	'invp vop!
	xymouse 48 - 3 >>
	swap 10 - 3 >> swap
	vop
	xa 10 - 3 >>
	ya 48 - 3 >>
	vline! ;

:editico
	verde
	2 26 gotoxy
	[ icohw dup $ff and 1- 0 max swap $ff00 and or 'icohw ! ; ] " - " btnt sp
	icohw $ff and "w:%d" print
	[ icohw dup $ff and 1+ 32 min swap $ff00 and or 'icohw ! ; ] " + " sp btnt

	2 28 gotoxy
	[ icohw dup $ff00 and $100 - 0 max swap $ff and or 'icohw ! ; ] " - " btnt sp
	icohw 8 >> $ff and "h:%d" print
	[ icohw dup $ff00 and $100 + $2000 min swap $ff and or 'icohw ! ; ] " + " sp btnt

	gris
	10 48
	icohw $ff and 3 << pick2 +
	icohw 8 >> $ff and 3 << pick2 +
	gcxyxy gc.fbox

	blanco
	'dn 'mv 'up guiMap
	10 48 setxy
	'icohw drawzico
	;


|--- parse
:getnro | txt -- txt' 0 / nro
	( dup c@ 33 <? )( 0? ( ; ) drop 1+ ) drop
	dup ?numero 0? ( ; )
	drop rot drop ;

:cpynom | adr en -- adr''
	swap ( c@+ $ff and 32 >? )(
		rot c!+ swap )
	drop 0 rot c! ;

:getsize@ 8 >> $ff and ;

:cadadibujo | adr $23 -- adr'
	drop c@+ $3a <>? ( drop ; ) drop | #:
	cntvar 5 << 'indexn + cpynom
	cntvar 8 << 'index + >r
	getnro dup r!+ getsize@
	( 1? )( swap getnro r!+ swap 1- ) drop
	rdrop
	1 'cntvar +! ;

:parsefile | "" --
	here dup rot load 0 swap !+ 'here !
	0 'cntvar !
	( c@+ 1? )(
		$23 =? ( cadadibujo dup )
		drop ) 2drop
	;

:copyico | adr --
	'icohw >r
	@+ dup r!+
	getsize@
	( 1? )( swap @+ r!+ swap 1- )
	2drop
	rdrop
	;

:icocopy | adr --
	>r
	'icohw @+ dup r!+
	getsize@
	( 1? )( swap @+ r!+ swap 1- )
	2drop rdrop
	;

:actual! | nro --
	actual 8 << 'index + icocopy
	dup 'actual !
	8 << 'index + copyico
	refreshfoco ;

|--- write
:,codigo | adr --
	0 swap
	( @+ 1? )(
		" $%h" ,print
		swap 1+ 16 >? ( 0 nip ,nl ) swap
		) 3drop
	" 0" ,s ,nl ;

:writefile | "" --
	mark
	"| ICO file" ,s ,nl
	0 ( cntvar <? )(
		'indexn over 5 << + "#:%s" ,print ,nl
		dup 8 << 'index + ,codigo
		1+ ) drop
	savemem
	empty ;


|--------------------------------------------------
:newdib
	cntvar "new%d" mprint
	cntvar 5 << 'indexn + strcpy
	cntvar 8 << 'index + >r
	$f0f r!+
	16 ( 1? )( 1- 0 r!+ ) drop rdrop
	1 'cntvar +!
	cntvar 1- actual!
	;

:copydib
	cntvar "new%d" mprint
	cntvar 5 << 'indexn + strcpy
	cntvar 8 << 'index + >r
	actual 8 << 'index + @+ r!+
	32 ( 1? )( 1- swap @+ r!+ swap ) 2drop rdrop
	1 'cntvar +!
	cntvar 1- actual!
	;

:deldib
	cntvar 0? ( drop ; ) drop
	actual 8 << 'index +
	actual 1+ 8 << 'index +
	cntvar actual - 8 << 1+ move

	actual 5 << 'indexn +
	actual 1+ 5 << 'indexn +
	cntvar actual - 5 << cmove

	-1 'cntvar +!
	actual 1? ( -1 'actual +! actual 8 << 'index + copyico ) drop
	;

:import
	1 'orden !
	cntvar dup 'nroico !
	"svg%d" mprint 'nomico strcpy

|	'orden 40 "mem/inc-icoo.mem" save
|	mark
|	"mem/notepad.ico" savemem
|	empty
|	"r4/system/svg-spr.txt" run
	;

#namedraw 0

:editname
	namedraw 0? ( drop ; )
	24 input
	[ 0 'namedraw ! 'nombre writefile ; ] lostfoco
	;

:rendib
	actual 5 << 'indexn + 'namedraw ! ;


|----------------------------

:cadadib | n n -- n n
	actual =? ( verde )( negro )
	[ dup actual! ; ]
	over 5 << 'indexn + " %s " mprint link
	blanco
	dup 8 << 'index + drawico
	sp
	cr cr
	;

:tabladib
	36 4 gotoxy chome!
	0 ( cntvar <? )( cadadib 1+ ) drop ;

:main
	'nombre "mem/inc-ico.mem" load drop
	'nombre parsefile
	actual 8 << 'index + copyico
	show clrscr
		fonti
		verde oscuro 3 linesfill blanco
		verde dup " :R%d" print
		blanco "eDIT iCO" print
		'nombre "%s " printr
		cr
		rojo
		sp 'exit dup >esc< "esc-Exit" link
		sp verde
		sp 'newdib dup <f1> "f1-New" link
		sp 'copydib dup <f2> "f2-Copy" link
		sp 'deldib dup <f3> "f3-Del" link
		sp 'rendib dup <f4> "f4-Rename" link
		sp 'import dup <f5> "f5-IMG" link
        sp editname
		cr
		cr
	| acciones

		[ actual 1+ actual! ; ] <dn>
		[ actual 1- actual! ; ] <up>
		scr
        editico
		tabladib

		cminiflecha ;

|--------------------------------------------------
:	4 0 paper
	mark
	main
||	'nombre writefile
	;


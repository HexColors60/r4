^r4/lib/gui.txt
^./fichas.bmr
^r4/lib/bmr.txt
^r4/lib/miniscr.txt
^r4/lib/trace.txt

#tablero
0 0 0 0 0 0 0
0 0 0 0 0 0 0
0 0 0 0 0 0 0
0 0 0 0 0 0 0
0 0 0 0 0 0 0
0 0 0 0 0 0 0
3 3 3 3 3 3 3

#LISTAD 'tabv 'taba 'tabr

:dibujaficha | y x t f
	pick2 16 * 56 +
	pick4 16 * 192 4 / +
	rot
	4 * 'listad + @
	bmr.draw
	;

:dibujatablero
	'tablero
	0 ( 6 <? )(
		0 ( 7 <? )(
			rot @+ dibujaficha rot rot
		1+ ) drop cr
	1+ ) 2drop ;

#columna 0
#turno 1

:xy-tab | x y -- ad
	7 * + 2 << 'tablero + ;

|------- horizontal
:primeroizq | x y -- x y
	( swap 1- -? ( 1+ swap ; ) swap
		2dup xy-tab @ turno =? )( drop ) drop
	swap 1+ swap ;

:iguales  | x y -- x
	( 2dup xy-tab @ turno =? )( drop swap 1+
|		5 >? ( nip nip ; )
		swap )
	2drop 1- ;

:ganah? | x y -- score
	primeroizq over swap iguales swap - ;

|------- diagonal
:primerod1 | x y -- xi yi
	(   1- -? ( 1+ ; ) swap
		1- -? ( 1+ swap 1+ ; ) swap
		2dup xy-tab @ turno =? )( drop ) drop
	swap 1+	swap 1+ ;

:igualesd1  | x y -- x
	( 2dup xy-tab @ turno =? )( drop
		1+ swap 1+
|		5 >? ( nip nip ; )
		swap )
	2drop 1- ;

:ganad1? | x y -- score
	primerod1 over swap igualesd1 swap - ;

:primerod2 | x y -- xi yi
	(   1+ swap
		1- -? ( 1+ swap 1- ; )
		swap
		2dup xy-tab @ turno =? )( drop ) drop
	swap 1+ swap 1- ;

:igualesd2  | x y -- x
	( 2dup xy-tab @ turno =? )( drop
		1- swap 1+
|		5 >? ( nip nip ; )
		swap )
	2drop 1- ;

:ganad2? | x y -- score
	primerod2 over swap igualesd2 swap - ;

|------- vertical
:ganav? | x y -- score
	swap over
	( 1+ 2dup xy-tab @ turno =? )( drop ) drop
	nip 1- swap - ;

:gana? | x y -- x y g
	2dup ganah? 2 >? ( nip nip ; ) drop
	2dup ganav? 2 >? ( nip nip ; ) drop
	2dup ganad1? 2 >? ( nip nip ; ) drop
	ganad2? ;


:caerficha | c -- x y
	0 ( 2dup xy-tab @ 0? )( drop 1+ ) drop 1- ;

#maxscore 0
#colscore 0

:maxscore | x y -- score
	-? ( 2drop -10 ; )
	2dup xy-tab turno swap !
	2dup ganah? >a
	2dup ganav? a> max >a
	2dup ganad1? a> max >a
	2dup ganad2? a> max >a
	xy-tab 0 swap !
	a> ;

:scorecol
	blanco
	0 ( 7 <? )(
		dup caerficha
		maxscore
		|over 2 << 'scorecol !
		over 16 * 60 +
		192 4 / 32 - atxy
		"%d" print
		1+ ) drop ;


:poneficha
	columna caerficha
	-? ( 2drop ; )

	2dup xy-tab turno swap ! | pone ficha
	gana? 2 >? ( drop trace ; ) drop

	turno
	3 xor | bit trick
|	1 =? ( 2 )( 1 ) nip
	'turno !
	;

:teclado
	[ columna 1+ 6 min 'columna ! ; ] <ri>
	[ columna 1- 0 max 'columna !  ; ] <le>
	'poneficha <dn>
	;

:fichausuario
	columna 16 * 56 +
	192 4 / 16 -
	turno
	1 =? ( 'fichaa )( 'fichar ) nip
	bmr.draw
	;

:main
	224 192 miniscreen
	33
	show clrscr
	dup "%d" print
		fichausuario
		dibujatablero
		scorecol

		teclado

		minidraw
		'exit >esc<
		cminiflecha ;

: main ;

| Golden Waves
| Adaptacion del programa homonimo en BASIC-256. Galileo (2016)

^r4/lib/gui.txt

#x #y #h #d

:conv 6.2832 /. ;

:color | r g b --
  8 << or 8 << or ink ;

:switch | t y1 x1 -- t y1 x1
  pick2
  10.0 >? ( 20.0 <? ( pick2 16 << 4.0 /. + conv sin 60.0 *. 65.0 + 16 >> 'h ! ; ) )
  20.0 >? ( 30.0 <? ( pick2 pick2 swap - 16 << 4.0 /. swap +
    conv sin 60.0 *. 65.0 + 16 >> 'h ! ; ) )
  30.0 >? ( 40.0 <? ( pick2 16 << 2.0 /. over + conv sin 30.0 *.
    pick2 16 << 2.0 /. rot + conv sin 30.0 *. + 65.0 + 16 >> 'h ! ; ) )
  40.0 >? ( 50.0 <? ( pick2 pick2 + 16 << 4.0 /. swap +
    conv sin 60.0 *. 65.0 + 16 >> 'h ! ; ) )
  50.0 >? ( 60.0 <? ( d 0.3 *. swap + conv sin 60.0 *. 65.0 + 16 >> 'h ! ; ) ) 
  over 16 << 4.0 /. swap + conv sin 60.0 *. 65.0 + 16 >> 'h !
;

:calcula | t y1 x1 -- 
  24 over - 12 * pick2 12 * + 'x !
  24 over - -6 * pick2 6 * 300 + + 'y !
  10 over - dup * pick2 10 swap - dup * + 16 << sqrt. 'd !
  switch
  h 100 h + 100 h + color
  sfill x y h - op x 10 + y 5 + h - pline x 20 + y h - pline
    x 10 + y 5 - h - pline x y h - pline poli 
  0 60 60 color
  sfill x y h - op x 10 + y 5 + h - pline x 10 + y pline x y 5 - pline x y h - pline poli
  0 150 150 color
  sfill x 10 + y 5 + h - op x 10 + y pline x 20 + y 5 - pline
    x 20 + y h - pline x 10 + y 5 + h - pline poli
;

:buclex | t y1 --
  0 ( 24 <? )( calcula 1+ ) drop ;

:bucley | t --
  0 ( 24 <? )( buclex 1+ ) drop ;

:gwaves 
  1.0 ( 60.0 <? )( cls bucley 0 update drop redraw 0.1 + ) drop ;

: $800000 paper gwaves show 'exit >esc< ;
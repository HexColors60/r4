^r4/lib/gui.txt
^r4/lib/vesprite.txt
^r4/lib/part16.txt
^r4/lib/penner.txt
^./draws.vsp

#aliens 0 0
#shoot 0 0
#fx 0 0

#yp -0.8
#xp
#xv
#xa

:r1 rand 1.0 mod ;
:r.1 rand 0.1 mod ;
:r.01 rand 0.01 mod ;
:r.001 rand 0.001 mod ;
:r.0001 rand 0.0001 mod ;

:dalien1 | adr --
	>b
	80 qdim
	b@+
	-1.0 <? ( drop 0 ; ) 1.0 >? ( drop 0 ; )
	b@+
	-1.0 <? ( 2drop 0 ; )
	fpos
	b@+ b> 12 - +!
	b@+ b> 12 - +!
	b@+ b@+ rvesprite
	;

:addalien
	'dalien1 'aliens p!+ >a
	r1 a!+ 1.0 a!+
	r.001 a!+ r.01 abs neg -0.001 + a!+
	rand %10000 and? ( 'alien1 )( 'alien2 ) a!+ drop
	r1 a!+
	;


|-------------------------------------------------------
:explo | adr --
	>b
	b@+ 70 >? ( drop 0 ; ) qdim
	4 b> 4 - +!
	b@+ b@+ fpos
	'explo1 msec 4 << rvesprite
	;

:+explosion | x y --
	'explo 'fx p!+ >a
	40 a!+
	swap a!+ a!+
	;

|-------------------------------------------------------
:disparo
	>b
	20 qdim
	b@+ b@+
	1.0 >? ( 2drop 0 ; )
	2dup 'aliens 0.06 p.in2d? 1? ( 'aliens p.del +explosion 0 ; )( drop )
	fpos
	b@+ b> 12 - +!
	b@+ b> 12 - +!
	'bala b@+ rvesprite
	;

:+disparo
	'disparo 'shoot p!+ >a
	xp a!+ yp a!+
	xv 3 << neg 0.25 + sincos
	5 >> a!+ 5 >> a!+
	0 a!+
	;

:player
	80 qdim
	xp yp fpos
	'nave xv 3 << rvesprite
	xv xp + -0.9 max 0.9 min 'xp !
	xv xa +
	-0.006 max 0.006 min 'xv !
	[ -0.0004 'xa ! ; ] <le>
	[ 0.0004 'xa ! ; ] <ri>
	[ 0 'xa ! ; ] dup >le< >ri<
	'+disparo <spc>
	;

:alien
	rand
	$1f00 nand? ( addalien ) drop
	;

#inter )( $fff

:testinter
	msec
	6 <<
	$10000 and? ( $ffff xor )
	$ffff and
|	Ela_Out
	Bou_Out
	'star 'star2
	'inter vespriteInter

	'inter vesprite
	;

:main
	33
	show clrscr
		negro
		dup "%d" print

		'aliens p.draw
		'shoot p.draw
		'fx p.draw 

		player
		alien

		200 qdim
		0 0 fpos

		'addalien <f1>
		'exit >esc<
		;

:memory
	$cccccc 'paper !
	mark
	200 'aliens p.ini
	200 'shoot p.ini
	200 'fx p.ini
	;

: memory main ;

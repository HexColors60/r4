^r4/lib/gui.txt
^r4/lib/arr8.txt
^r4/lib/bmr.txt
^./draws.bmr

#xn #yn
#xa #ya
#pnts #lives

#stars 0 0
#shoots 0 0
#aliens 0 0
#explot 0 0

|--------------------
:*stars | adr --
	>a a@+ a@+ a@+ + 				| x y+s
	sh fixed >=? ( 0 nip )          | x 0
	a@+ pick2 int pick2 int xy>v !
	swap -16 a+ a!+ a! ;

:makestars
	1000
	dup 'stars arr8.ini
	( 1? )( 1-
		'*stars 'stars arr8!+ >a
		sw fixed nrand a!+
		sh fixed nrand a!+
        2.0 nrand 0.5 + a!+
        $7f nrand $7f +
		dup 16 << or dup 8 << or
        a!+
		) drop ;

|--------------------
:*expl | adr --
	>a a@+ a@+ a@
	1+
	20 >? ( 3drop 0 ; )
	dup a!
	circle ;

:expl | x y --
	'*expl 'explot arr8!+ >a
	swap int a!+ int a!+ 0 a! ;

|--------------------
:*bullet | adr --
	@+ int over @ int 'bullet bmr.draw
	dup @ 4.0 -
	-? ( 2drop 0 ; )
	swap ! ;

:fire
	'*bullet 'shoots arr8!+ >a
	xn a!+ yn a!+ ;

|--------------------
:*aliens | adr --
	dup @+ int swap @ int
	msec
	$80 and? ( 'alien1 )( 'alien2 ) nip
	bmr.draw
	>a
	a@ msec 6 <<
	a> 8 + @ $10 and? ( drop neg )( drop )
	sin 1 << + a!+
	a@ msec 5 << cos 1 << + 1.1 + a!+
	;

:+alien | x -- x
	'*aliens 'aliens arr8!+ >a
	dup fixed a!+
	over 5 << fixed neg a!+
	rand a!+
	;

:oleada
	sw 4 >>
	16 ( 1? )( 1- swap
		+alien
		sw 4 >> +
		swap ) 2drop ;


|--------------------
:player
	xn int yn int
	msec
	$10 and? ( 'ship1 )( 'ship2 ) nip
	bmr.draw
	[ -1.0 'xa ! ; ] <le>
	[ 1.0 'xa ! ; ] <ri>
	[ 0 'xa ! ; ] dup >le< >ri<
	[ -1.0 'ya ! ; ] <up>
	[ 1.0 'ya ! ; ] <dn>
	[ 0 'ya ! ; ] dup >up< >dn<
	'oleada <f1>
	'+alien <f2>
	[ xn yn expl ; ] >spc<
	'fire >ctrl<
	xa 'xn +!
	ya 'yn +!
	;

|--------------------
:main
	oleada
	33 fonti2
	show clrscr
|		blanco dup "%d" print
		blanco cr
		pnts " %d"print
		lives "%d " printr

		'stars arr8.exec
		'explot arr8.exec
		'shoots arr8.exec
		'aliens arr8.exec
		player

		'exit >esc<
		;

|--------------------
:memory
	0 'paper !
	mark
	makestars
	200 'shoots arr8.ini
	100 'aliens arr8.ini
	200 'explot arr8.ini
	sw 2/ fixed 'xn !
	sh dup 2 >> - fixed 'yn !
	5 'lives !
	0 'pnts !
	;

: memory main ;

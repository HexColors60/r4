^r4/lib/gui.txt
^r4/lib/arr8.txt
^r4/lib/bmr.txt
^./draws.bmr

#xn
#xa
#yn
#stars 0 0
#shoots 0 0

:*stars | adr --
	>a a@+ a@+ a@+ + 				| x y+s
	sh fixed >=? ( 0 nip )          | x 0
	a@+ pick2 int pick2 int xy>v !
	swap -16 a+ a!+ a! ;

:makestars
	1000
	dup 'stars arr8.ini
	( 1? )( 1-
		'*stars 'stars arr8!+ >a
		sw fixed nrand a!+
		sh fixed nrand a!+
        2.0 nrand 0.5 + a!+
        $7f nrand $7f +
		dup 16 << or dup 8 << or
        a!+
		) drop ;

:*bullet | adr --
	@+ int over @ int 'bullet bmr.draw
	dup @ 3.0 -
	-? ( drop 0 ; )
|	sh fixed >=? ( 2drop 0 ; )
	swap ! 
	;

:fire
	'*bullet 'shoots arr8!+ >a
	xn a!+ yn a!+ ;

:player
	xn int yn int 'ship1 bmr.draw
	[ -1.0 'xa ! ; ] <le>
	[ 1.0 'xa ! ; ] <ri>
	[ 0 'xa ! ; ] dup >le< >ri<
	'fire >spc<
	xa 'xn +!
	;

:main
	33
	show clrscr
		blanco dup "%d" print

		'stars arr8.exec
		'shoots arr8.exec
		player

		'exit >esc<
		;

:memory
	0 'paper !
	mark
	1000 'shoots arr8.ini
	makestars
	sw 2/ fixed 'xn !
	sh dup 2 >> - fixed 'yn !
	;

: memory main ;

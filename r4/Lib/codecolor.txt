| consola de codigo coloreada
| con seleccion
| PHREDA 2009
|----------------------------
^r4/lib/macrosr4.txt

:lineacom | adr c -- adr++
	( 13 =? ( drop 1- ; ) emit c@+ 0? )
	drop 1- ;

:palstr | adr c -- adr++
	( emit c@+ 34 =? ( emit ; ) $ff and 32 <? )
	drop 1- ;

:npal
	( $ff and 32 >? )( emit c@+ )
	drop 1- ;

:palabracol | adr c -- ar..
	32 =? ( emit ; )
	9 =? ( emit ; )
	10 =? ( drop ; )
	$5e =? ( amarillo npal ; )	| $5e ^  Include
	$7c =? ( gris lineacom ; )	| $7c |	 Comentario
	$3A =? ( rojo npal ; )		| $3a :  Definicion
	$23 =? ( violeta npal ; )	| $23 #  Variable
	$22 =? ( blanco palstr ; )	| $22 "	 Cadena
	$27 =? ( cyan npal ; )		| $27 ' Direccion
	over 1- isNro 1? ( drop amarillo npal ; ) drop
	over 1- ?macro 1? ( drop verde npal ; ) drop		| macro
	naranja npal ;
|	over 1- ?macro 1? ( drop naranja npal ; ) drop		| macro
|	verde npal ;

::>>lineacolor>> | adr -- adr++
	( c@+ 1? )(
		13 =? ( drop ; )
		palabracol
|		ccx tx2 >? ( cr ) drop
		) drop 1- ;

::>>lineacolor0 | adr -- adr++/0
	( c@+ 1? )(
		13 =? ( drop ; )
		palabracol
|		ccx tx2 >? ( cr ) drop
		) nip ;

|----------------------------
::code.lines | adr -- adr..
	rows swap 0
	( pick2 <? )( swap
		>>lineacolor>> cr
		swap 1+ )
	drop nip ;

::code.nlines | adr rows -- adr..
	swap 0
	( pick2 <? )( swap
		>>lineacolor>> cr
		swap 1+ )
	drop nip ;

|-- uso
|^codecolor.txt

|:drawcursor
|	blink 0? ( drop ; ) drop
|	actual> pantafin> >? ( drop ; ) drop
|	pantaini> ( actual> <? )( c@+ gemit ) printcurw drop
|	;
|:drawcode
|	fonti |20 sfont
|	drawcursor
|	home
|	pantaini> code.lines 'pantafin> !
|	;

::cada.lineaexec | 'exec  adr -- adr..
	rows swap 0
	( pick2 <? )( swap
		>>lineacolor>>
		over pick3 exec
		cr
		swap 1+ )
	drop nip nip ;


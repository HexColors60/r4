| Array 8 vals
| PHREDA 2017
|------

::arr8.ini | cantidad list --
	here dup rot !+ ! 5 << 'here +! ;

::arr8.clear | list --
	dup 4+ @ swap ! ;

::arr8!+ | 'act list -- adr
	dup >r @ !+
	32 r> +! ;

::arr8! | list -- adr
	dup >r @
	32 r> +! ;

:del | list end now -- list end- now-
	nip over @ swap	| recalc end!!
	swap 32 - 2dup 8 move
	dup pick3 !
	swap 32 - ;

::arr8.exec | list --
	dup @+ swap @
	( over <? )(
		dup @+ exec 0? ( drop del )
		32 + ) 3drop ;

::arr8.nro | nro list -- adr
	4+ @ swap 5 << + ;

::arr8.cnt | list --
	@+ swap @ | last fist
	- 5 >> ;

::arr8.del | adr list --
	>a a@ 32 - 8 move a> dup @ 32 - swap ! ;

::arr8.mapv | 'vector list --
	@+ swap @
	( over <? )(
		pick2 exec
		32 + ) 3drop ;

::arr8.mapd | 'vector list --
	@+ swap @
	( over <? )(
		pick2 exec 0? ( drop dup del )
		32 + ) 3drop ;

::arr8.map2 | 'vec 'list ---
	@+ swap @
	( over <? )(
		dup 32 + ( pick2 <? )(
			pick3 exec
			32 + ) drop
		32 + )
	3drop ;


:up | adr -- adr ; swap 64 -
	dup dup 32 - >a | p1 r:p2
	8 ( 1? )( 1- swap
		a@ over @ a!+ swap !+
		swap )
	2drop ;

::arr8.lastsort | col 'list --
	@+ swap @ swap 32 - | first last
	( over >? )(
		dup 32 -
		pick3 2 << + @
		over pick4 2 << + @
		<? ( 4drop ; ) drop
		up
		32 - ) 3drop ;

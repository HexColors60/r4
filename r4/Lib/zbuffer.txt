| zbuffer - convex polyfill
| PHREDA 2016
|-------------------------
^r4/lib/trace.txt

#:zb
#zbw #zbh
#zcnt #:zbo

#segs	| mem polys
#px #py #pz
#ymin #ymax
#zs #zbase

#o #z

::zo! 'o ! ;
::zz! 'z ! ;

|--- maskbuffer | zbw*zbh
:zb.adr | x y -- d
	zbw * + 2 << zb + ;

::zb@ | x y -- b
	zb.adr @ ;

::zbo! | o z x y --
	zb.adr >r r!+ r> zbo + ! ;

::zb! | x y z --
	rot rot zb.adr >r r!+ o r> zbo + ! ;

::zbw! | x y z w --
	swap 'z !
	rot rot zb.adr >r | w
	( 1? )(
		r@+
		z >? ( z r 4 - ! o r zbo + ! )
		drop 1- ) drop
	rdrop ;

::zbh! | x y z h --
	swap 'z !
	rot rot zb.adr >r
	zbw 1- 2 << swap
	( 1? )(
		r@+
		z >? ( z r 4 - ! o r zbo + ! )
		drop
		over r+ 1- ) 2drop
	rdrop ;

::zb.clear
	zb >r zcnt ( 1? )( 1- $7fffffff r!+ ) drop rdrop ;

::zb.ini | w h --
	2dup * dup 'zcnt ! dup 2 << 4 - 'zbo !
	here dup 'zb ! 		| zbuffer,cbuffer
	swap 3 << + dup 'segs !
	over 4 << + 'here !
	dup 1- 'ymax ! 'zbh ! 'zbw !
	zb.clear ;

::zb.occ | w h x y z -- 1/0
	'z !
	zb.adr >r
	zbw pick2 - 2 <<
	swap ( 1? )(
		pick2 ( 1? )( r@+
			z >? ( nip 4drop rdrop 1 ; )
			drop 1- ) drop
		over r+
		1- ) 3drop
	rdrop 0 ;

::zb.set | w h x y z o --
	'o ! 'z !
	zb.adr >r
	zbw pick2 - 2 <<
	swap ( 1? )(
		pick2 ( 1? )( r@+
			z >? ( z r 4 - ! o r zbo + ! )
			drop 1- ) drop
		over r+
		1- ) 3drop
	rdrop ;

::zb.fill | w h x y --
	zb.adr >r
	zbw pick2 - 2 <<
	swap ( 1? )(
		pick2 ( 1? )( r@+
			z >? ( z r 4 - ! o r zbo + ! )
			drop 1- ) drop
		over r+
		1- ) 3drop
	rdrop ;

|--- convex polygon
#FBASE	16

::zop | x y z --
	'zbase ! 0 'pz !
	ymin 4 << segs + >r
	ymax ymin - 1+ ( 1? )( 1- $7fffffff r!+ -1 r!+ 8 r+ ) drop rdrop
	dup 'ymin ! dup 'ymax !

	over FBASE << dup pick2 4 << segs + >r r!+ r!+
	'py ! 'px !
	0 r!+ 0 r> !
	;

:enlinea | x y --
	4 << segs + >r
	dup FBASE <<
	r@+ <? ( dup r 4 - ! pz r 4+ ! )
	r@+ >? ( dup r 4 - ! pz r 4+ ! )
	drop rdrop
	'px ! ;

::zline | x y z --
	pz 'z !
	zbase - 11 << 'pz !
	py =? ( enlinea ; )
	px py
	2over 'py ! 'px !
	pick2 >? ( 2swap z pz dup 'z ! - )( pz z - ) 'zs !
	ymin <? ( dup 'ymin ! )
 	rot ymax >? ( dup 'ymax ! )		| comprueba el mayor
	>r >r FBASE << swap FBASE << 	| x1 x2
	over - r> r> over -				| x1 (x2-x1) y1 (y2-y1)
	1+ rot over / 					| x1 y1 (y2-y1) t
	rot 4 << segs + >r				| x1 (y2-y1) t
	zs pick2 / dup 'z +! 'zs !
	rot over + rot				| t x1 cnt
	( 1? )( 1- swap
		r@+ <? ( dup r 4 - ! z r 4+ ! )
		r@+ >? ( dup r 4 - ! z r 4+ ! )
		8 r+
		zs 'z +!
		pick2 + swap )
	3drop rdrop ;

#adrline

:zbline | cnt x zi zf --
	over - pick3 /
	rot 2 << adrline + >r
	swap over 2/ +
	rot	| zs zi cnt
	( 1? )(
		over 11 >> zbase +
		r@+ <? ( r 4 - ! o r zbo + ! )( drop )
		swap pick2 + swap
		1- ) 3drop
	rdrop ;

::zpoly | --
	ymax ymin
	dup 4 << segs + >r
	dup zbw * 2 << zb + 'adrline !
	( over <=? )(
		r@+ FBASE >> r@+ FBASE >> over - 1+
		swap r@+ r@+
		zbline
		zbw 2 << 'adrline +!
		1+ ) 2drop rdrop ;

|------ ellipse
#dx #dy

:zhor | w x y --
	zb.adr >r | w
	( 1? )( r@+ z >? ( z r 4 - ! o r zbo + ! )
		drop 1- ) drop
	rdrop ;

:hfill | x y -- x y
	over 2* px pick3 - py pick3 - zhor
	over 2* px pick3 - py pick3 + zhor ;

:hfill1 | x y -- x y
	over 2* px pick3 - py zhor ;

::zellipse | a b x y z --
	'z ! 'py ! 'px !
	over dup * dup 2*		| a b c 2aa
	swap dup >r 'dy ! 		| a b 2aa
	rot rot over neg 2* 1+	| 2aa a b c
	swap dup * dup 2* 		| 2aa a c b 2bb
	rot rot * dup r+ 'dx !	| 2aa a 2bb
	swap 1					| 2aa 2bb x y
	pick3 'dy +! dy r+
	hfill1
	( swap +? )( swap 		| 2aa 2bb x y
		r 2*
		dx >=? ( rot 1- rot rot pick3 'dx +! dx r+ )
		dy <=? ( rot rot hfill 1+ rot pick4 'dy +! dy r+ )
		drop
		)
	4drop rdrop ;

|--------------
:ztest
	0 0 setxy
	zb >r
	sh ( 1? )( 1-
		sw ( 1? )( 1-
			r@+ $7fffffff <>? ( r zbo + @ )( 0 ) px!+ drop
			) drop
		) drop rdrop ;
|-------------
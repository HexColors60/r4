| zbuffer - convex polyfill
| PHREDA 2016
|-------------------------
#zb
#zbw #zbh
#zcnt #zbo

#segs	| mem polys
#py #px
#ymin 0
#ymax 1023

|--- maskbuffer | zbw*zbh
:zb.adr | x y -- d
	zbw * + 2 << zb + ;

::zb@ | x y -- b
	zb.adr @ ;

::zb! | o z x y --
	zb.adr dup >r !
	r> zcnt 2 << + ! ;

::zb.clear
	zb >r zcnt ( 1? )( 1- $7fffffff r!+ ) drop rdrop ;

::zb.ini | w h --
	dup * dup 'zcnt !
	here dup 'zb ! | zbuffer,cbuffer
	3 << + dup 'segs !
	1024 3 << + 'here !
	'zbh ! 'zbw !
	zcnt 2 << 4 - 'zbo !
	;

#z
::zb.occ | w h x y z -- 1/0
	'z !
	zb.adr >r
	zbw pick2 -
	swap ( 1? )(
		pick2 ( 1? )( r@+
			z >? ( 4drop rdrop 1 ; )
			drop 1- ) drop
		over r+
		1- ) 3drop
	rdrop 0 ;

#o
::zb.set | w h x y z o --
	'o ! 'z !
	zb.adr >r
	zbw pick2 -
	swap ( 1? )(
		pick2 ( 1? )( r@+
			z <? ( z r 4 - ! o r zbo + ! )
			drop 1- ) drop
		over r+
		1- ) 3drop
	rdrop ;

::zb.line | cnt x y --
	zb.adr >r
	( 1? )( r@+
		z <? ( z r 4 - ! o r zbo + ! )
		drop 1- ) drop
	rdrop ;

|--- convex polygon
#FBASE	16
#MIDBASE $7fff

::zop | x y --
	ymin 3 << segs + >r
	ymax ymin - 1+ ( 1? )( 1- $7fffffff r!+ -1 r!+ ) drop rdrop
	dup 'ymin ! dup 'ymax !
	over FBASE << dup pick2 3 << segs + !+ !
	'py ! 'px !
	;

::zop2 | x y --
	ymin <? ( dup 'ymin ! )
 	ymax >? ( dup 'ymax ! )
	2dup 3 << segs + >r
	dup FBASE <<
	r@+ <? ( dup r 4 - ! )
	r@+ >? ( dup r 4 - ! )
	2drop rdrop
	'py ! 'px !
	;

:enlinea | x y --
	3 << segs + >r
	dup FBASE <<
	r@+ <? ( dup r 4 - ! )
	r@+ >? ( dup r 4 - ! )
	drop
	'px !
	rdrop ;

::zline | x y --
	py =? ( enlinea ; )
	px py pick2 >? ( 2swap 2dup )( 2over ) | x2 y2 x1 y1
	'py ! 'px !
	ymin <? ( dup 'ymin ! )
 	rot ymax >? ( dup 'ymax ! )		| comprueba el mayor
	>r >r FBASE << swap FBASE << 	| x1 x2
	over - r> r> over -				| x1 (x2-x1) y1 (y2-y1)
	rot over /						| x1 y1 (y2-y1) t
	rot 3 << segs + >r				| x1 (y2-y1) t
	rot rot 1+				| t x1 cnt
	( 1? )( 1- swap
		r@+ <? ( dup r 4 - ! )
		r@+ >? ( dup r 4 - ! )
		pick2 + swap )
	3drop rdrop ;

::zpoly | z o --
	'o ! 'z !
	ymax
	ymin dup 3 << segs + >r
	( over <=? )(
		r@+ FBASE >> r@+ FBASE >>
		over - 1+ swap pick2 zb.line
		1+ ) 2drop rdrop ;

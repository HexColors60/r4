| wbuffer - convex polyfill
| PHREDA 2016
|-------------------------
^r4/lib/trace.txt

#:zb
#zbw #zbh
#zcnt #:zbo

#segs	| mem polys
#px #py #pz
#ymin #ymax
#zstep
#zbase

#o #z

::zo! 'o ! ;
::zz! 'z ! ;

|--- maskbuffer | zbw*zbh
:zb.adr | x y -- d
	zbw * + 2 << zb + ;

::zb@ | x y -- b
	zb.adr @ ;

::zbo! | o z x y --
	zb.adr >a a!+ a> zbo + ! ;

::zb! | x y z --
	rot rot zb.adr >a a!+ o a> zbo + ! ;

::zbw! | x y z w --
	swap 'z !
	rot rot zb.adr >b | w
	( 1? )(
		b@+
		z <? ( z b> 4 - ! o b> zbo + ! )
		drop 1- ) drop ;

::zbh! | x y z h --
	swap 'z !
	rot rot zb.adr >a
	zbw 1- 2 << swap
	( 1? )(
		a@+
		z <? ( z a> 4 - ! o a> zbo + ! )
		drop
		over a+ 1- ) 2drop
	;

::zb.clear
	zb >a zcnt ( 1? )( 1- 0 a!+ ) drop ;

::zb.ini | w h --
	2dup * dup 'zcnt ! dup 2 << 4 - 'zbo !
	here dup 'zb ! 		| zbuffer,cbuffer
	swap 3 << + dup 'segs !
	over 4 << + 'here !
	dup 1- 'ymax ! 'zbh ! 'zbw !
	zb.clear ;

::zb.occ | w h x y z -- 1/0
	'z !
	zb.adr >b
	zbw pick2 - 2 <<
	swap ( 1? )(
		pick2 ( 1? )( b@+
			z <? ( nip 4drop 1 ; )
			drop 1- ) drop
		over b+
		1- ) 3drop
	0 ;

::zb.set | w h x y z o --
	'o ! 'z !
	zb.adr >b
	zbw pick2 - 2 <<
	swap ( 1? )(
		pick2 ( 1? )( b@+
			z <? ( z b> 4 - ! o b> zbo + ! )
			drop 1- ) drop
		over b+
		1- ) 3drop
	;

::zb.fill | w h x y --
	zb.adr >b
	zbw pick2 - 2 <<
	swap ( 1? )(
		pick2 ( 1? )( b@+
			z <? ( z b> 4 - ! o b> zbo + ! )
			drop 1- ) drop
		over b+
		1- ) 3drop
	;

|--- convex polygon
#FBASE	12
#vert )( 32
#vert> 'vert

::zop | x y z --
	'zbase ! 0 'pz !
	ymin 4 << segs + >a
	ymax ymin - 1+ ( 1? )( 1- $7fffffff a!+ -1 a!+ 8 a+ ) drop
	dup 'ymin ! dup 'ymax !
	over FBASE << dup pick2 4 << segs + !+ !+ 0 dup rot !+ !
	'py ! 'px !
	'vert 'vert> !
	;

:enlinea | x y --
	4 << segs + >a
	dup FBASE <<
	a@+ <? ( dup a> 4 - ! pz a> 4+ ! )
	a@+ >? ( dup a> 4 - ! pz a> 4+ ! )
	drop
	'px ! ;

::zline | x y z --
	pz 'z !
	zbase - 11 << 'pz !
	py =? ( enlinea ; )
	px py
	2over 'py ! 'px !
	pick2 >? ( 2swap z pz dup 'z ! - )( pz z - ) 'zstep !
	ymin <? ( dup 'ymin ! )
 	rot ymax >? ( dup 'ymax ! )		| comprueba el mayor
	>r >r FBASE << swap FBASE << 	| x1 x2
	over - r> r> over -				| x1 (x2-x1) y1 (y2-y1)
	rot over / 					| x1 y1 (y2-y1) t
	rot 4 << segs + dup >a
	vert> !+ 'vert> !				| x1 (y2-y1) t
	zstep pick2 / 'zstep !
	rot over + rot				| t x1 cnt
	1+ ( 1? )( 1- swap
		a@+ <? ( dup a> 4 - ! z a> 4+ ! )
		a@+ >? ( dup a> 4 - ! z a> 4+ ! )
		8 a+
		zstep 'z +!
		pick2 + swap )
	3drop ;

:zbline | cnt x zi --
	swap 2 << py + >a
	swap ( 1? )(
		over 11 >> zbase +
		a@+ >? ( a> 4 - ! o a> zbo + ! )( drop )
		swap zstep + swap
		1- ) 2drop
	;

::zpoly | --
	'vert @+
	@+ swap @+ rot - 'py ! 'zstep !
	( vert> <? )( @+
		@+ swap @+ rot - py >? ( 'py ! 'zstep ! )( 2drop )
		) drop
	zstep @+ swap @ swap - py FBASE >> 1+ / 'zstep !
	ymax ymin
	dup 4 << segs + >b
	dup zbw * 2 << zb + 'py !
	( over <=? )(
		b@+ FBASE >> b@+ FBASE >> over - 1+
		swap b@+ 4 b+
		zbline
		zbw 2 << 'py +!
		1+ ) 2drop ;

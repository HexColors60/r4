| PHREDA 2016
| 2017 idea uno negativo
| referencia con 10.22 punto fijo
|--------------------------
^r4/lib/gui.txt
^r4/lib/loadpng.txt
^r4/lib/loadtga.txt
^r4/lib/loadpng.txt
^r4/lib/trace.txt

#:tritexture 0
#:trizbuffer 0

#minx #maxx
#miny #maxy
#va0 #va1
#vb0 #vb1
#inv_p0z #inv_p1z #inv_p2z
#inv_p0u #inv_p1u #inv_p2u
#inv_p0v #inv_p1v #inv_p2v
#inv_Pz
#oiz #o0 #o1 #o2

:loop | xv0 yv0 y x -- xv0 yv0 y x
	pick3 over -	| va2
	pick3 pick3 -	| vb2

	va1 over * pick2 vb1 * -
	oiz * -? ( 3drop 4 a+ ; )
    'o2 !
	swap vb0 * swap va0 * -
	oiz * -? ( drop 4 a+ ; )
	'o1 !
	$1000000 o1 o2 + -
	-? ( drop 4 a+ ; )
	'o0 !

	o2 inv_p2z 22 *>>
	o1 inv_p1z 22 *>> +
	o0 inv_p0z 22 *>> +
	a> trizbuffer + @ <? ( drop 4 a+ ; )
	dup	a> trizbuffer + !

	'inv_Pz !
	|---- correct
	inv_p0u o0 22 *>>
	inv_p1u o1 22 *>> +
	inv_p2u o2 22 *>> +
	inv_Pz / $3ff and
	inv_p0v o0 22 *>>
	inv_p1v o1 22 *>> +
	inv_p2v o2 22 *>> +
	inv_Pz / $3ff and

	| u v
|	8 << | 256x256
	tritexture @ $ffff and *
	or
	1+ 2 << tritexture + @
	a!+
	;

::tritex | vert --
	dup @ over 16 + @ pick2 32 + @ | x0 x1 x2
	dup pick3 - 'va0 ! over pick3 - 'va1 !
	pick2 pick2 pick2
	min min
	sw >=? ( 4drop drop ; )
	clamp0 'minx !
	max max
	-? ( 2drop ; )
	sw clampMax 'maxx !
	dup 4+ @ over 20 + @ pick2 36 + @ | y0 y1 y2
	dup pick3 - 'vb0 ! over pick3 - 'vb1 !
	pick2 pick2 pick2
	min min
	sh >=? ( 4drop drop ; )
	clamp0 'miny !
	max max
	-? ( 2drop ; )
	sh clampMax 'maxy !

	va0 vb1 * va1 vb0 * - 0? ( 2drop ; )
	$1000000 swap / 'oiz !

	$1000000 over 8 + @ /
	over 12 + @ dup >r
	16 0>> over * 'inv_p0u !
	r> $ffff and over * 'inv_p0v !
	'inv_p0z !
	$1000000 over 24 + @ /
	over 28 + @ dup >r
	16 0>> over * 'inv_p1u !
	r> $ffff and over * 'inv_p1v !
	'inv_p1z !
	$1000000 over 40 + @ /
	over 44 + @ dup >r
	16 0>> over * 'inv_p2u !
	r> $ffff and over * 'inv_p2v !
	'inv_p2z !
	@+ swap @	| vx0 vy0
	minx miny setxy
	miny ( maxy <? )(
		minx ( maxx <? )(
			loop
			1+ ) drop
		sw maxx minx - - 2 << a+
		1+ ) 3drop ;

|-------------------------
| http://www.hugi.scene.org/online/coding/hugi%2017%20-%20cotriang.htm

#dXdY_V1V3
#dUdY_V1V3
#dVdY_V1V3
#dZdY_V1V3
#dXdY_V2V3
#dUdY_V2V3
#dVdY_V2V3
#dZdY_V2V3
#dXdY_V1V2
#dUdY_V1V2
#dVdY_V1V2
#dZdY_V1V2

#Left_dXdY #Right_dXdY #LeftX #RightX
#Left_dUdY #LeftU
#Left_dVdY #LeftV
#Left_dZdY #LeftZ
#PK_dudx #PK_dvdx #PK_dzdx
#PK_dudx_ #PK_dvdx_ #PK_dzdx_
#wtexture
#mtexture

#u_a #v_a #z_a #u_b #v_b #z_b #u_c #v_c #z_c
#v1 #v2 #v3

:drawtex | du dv u v xcount --
	( 1? )( >r
		over 16 0>> wtexture *
		over 16 0>> or
		1+ 2 << mtexture + @
		a!+
		swap pick3 +
		swap pick2 +
		r> 1- ) drop 4drop ;

:drawsubspan4 | u1 v1 u2 v2 --
	pick2 - 4 >> swap
	pick3 - 4 >> swap	| u v du dv
	2swap 16 drawtex ;

:drawsu4 | u1 v1 u v z -- u2 v2
	1.0 swap /				| u1 v1 uiz viz z
	rot over *. rot rot *.	| u1 v1 u2 v2
	2dup >r >r
	drawsubspan4
	r> r> ;

#dLeftz
#dLeftu
#dLeftv

:texture!+ | u v --
	16 0>> $ff and
	wtexture * swap 
	16 0>> $ff and
	or
	1+ 2 << mtexture + @ a!+ ;

:drawfull | z u v -- z u v
	over pick3 /.
	over pick4 /.
	texture!+
	rot dLeftz +
	rot dLeftu +
	rot dLeftv + ;

:linesegment
	RightX ceil
	LeftX 16 0>>  		| x1
	dup 2 << b> + >a
	- 1+ | width
	LeftZ dup pick2 / 'dLeftz !
	swap
	LeftU dup pick2 / 'dLeftu !
	swap
	LeftV dup pick2 / 'dLeftv !
	swap
	( 1? )( 1- >r
		drawfull
		r> ) 4drop ;

:drawsegment | ycnt --
	( 1? )( >r
        linesegment
		Left_dUdY 'LeftU +!
		Left_dVdY 'LeftV +!
		Left_dZdY 'LeftZ +!
		Left_dXdY 'LeftX +!
		Right_dXdY 'RightX +!
		sw 2 << b+
		r> 1- ) drop ;

:>x @ ;
:>y 4+ @ ;
:>z 8 + @ ;
:>uv 12 + @ ;

:sortv
	v1 >y v2 >y >? ( v1 v2 'v1 ! 'v2 ! ) drop
	v1 >y v3 >y >? ( v1 v3 'v1 ! 'v3 ! ) drop
	v2 >y v3 >y >? ( v2 v3 'v2 ! 'v3 ! ) drop
	;

#y1i #y2i #y3i

:1-2
	dUdY_V1V2 'Left_dUdY !
	dVdY_V1V2 'Left_dVdY !
	dZdY_V1V2 'Left_dZdY !
	dXdY_V1V2 'Left_dXdY !
	u_a 16 << 'LeftU !
	v_a 16 << 'LeftV !
	z_a 16 << 'LeftZ !
	v1 >x 16 << 'LeftX !
	y2i y1i - drawsegment ;

:2-3
	dUdY_V2V3 'Left_dUdY !
	dVdY_V2V3 'Left_dVdY !
	dZdY_V2V3 'Left_dZdY !
	dXdY_V2V3 'Left_dXdY !
	u_b 16 << 'LeftU !
	v_b 16 << 'LeftV !
	z_b 16 << 'LeftZ !
	v2 >x 16 << 'LeftX !
	y3i y2i - drawsegment ;

:leftside
    dXdY_V1V3 'Right_dXdY !
	v1 >x 16 << 'RightX !
	y1i y2i <? ( 1-2 ) drop
	y2i y3i <? ( 2-3 ) drop
	;

:1-2
	y2i y1i - drawsegment ;

:2-3
	dXdY_V2V3 'Right_dXdY !
	v2 >x 16 << 'RightX !
	y3i y2i - drawsegment ;

:rigthside
	dXdY_V1V3 'Left_dXdY !
	dUdY_V1V3 'Left_dUdY !
	dVdY_V1V3 'Left_dVdY !
	dZdY_V1V3 'Left_dZdY !
	u_a 16 << 'LeftU !
	v_a 16 << 'LeftV !
	z_a 16 << 'LeftZ !
	v1 >x 16 << 'LeftX !
	dXdY_V1V2 'Right_dXdY !
	v1 >x 16 << 'RightX !
	y1i y2i <? ( 1-2 ) drop
	y2i y3i <? ( 2-3 ) drop
	;

:tritex2 | 'vert --
	dup 'v1 ! 16 + dup 'v2 ! 16 + 'v3 !
	sortv
	v1 >y 'y1i !
	v2 >y 'y2i !
	v3 >y 'y3i !

	y1i y3i =? ( drop ; ) drop

	1.0 v1 >z / 'z_a !
	1.0 v2 >z / 'z_b !
	1.0 v3 >z / 'z_c !
	v1 >uv dup $ffff and z_a * 'u_a ! 16 0>> z_a * 'v_a !
	v2 >uv dup $ffff and z_b * 'u_b ! 16 0>> z_b * 'v_b !
	v3 >uv dup $ffff and z_c * 'u_c ! 16 0>> z_c * 'v_c !

	v3 >y v1 >y - 1? ( | v3y-v1y
	v3 @ v1 @ - over 16 <</ 'dXdY_V1V3 !
	u_c u_a - over / 'dUdY_V1V3 !
	v_c v_a - over / 'dVdY_V1V3 !
	z_c z_a - swap / 'dZdY_V1V3 !
	)

	v3 >y v2 >y - 1? ( | v3y-v2y
	v3 @ v2 @ - over 16 <</ 'dXdY_V2V3 !
	u_c u_b - over / 'dUdY_V2V3 !
	v_c v_b - over / 'dVdY_V2V3 !
	z_c z_b - swap / 'dZdY_V2V3 !
	)

	v2 >y v1 >y - 1? ( | v2y-v1y
	v2 @ v1 @ - over 16 <</ 'dXdY_V1V2 !
	u_b u_a - over / 'dUdY_V1V2 !
	v_b v_a - over / 'dVdY_V1V2 !
	z_b z_a - swap / 'dZdY_V1V2 !
	)

	v3 >x v1 >x - v2 >y v1 >y - *
	v2 >x v1 >x - v3 >y v1 >y - * -
	0? ( drop ; )
	1.0 swap /		| denom
	u_c u_a - v2 >y v1 >y - *
	u_b u_a - v3 >y v1 >y - * - over *.
	'dLeftU !
|	4 << 'PK_dudx !
	v_c v_a - v2 >y v1 >y - *
	v_b v_a - v3 >y v1 >y - * - over *.
	'dLeftV !
|	4 << 'PK_dvdx !
	z_c z_a - v2 >y v1 >y - *
	z_b z_a - v3 >y v1 >y - * - over *.
	'dLeftZ !
|	4 << 'PK_dzdx !
	drop

	y1i sw * 2 << framev + >b
	dXdY_V1V3 dXdY_V1V2 | <? v2 leftside
	>? ( drop leftside ; )
	drop rigthside ;

|-------------------------
| x y z uv
#vertex
189 183 40 $ff0000
200 100 120 $ff
60 160 100 $ff00ff

#vertex2
189 183 40 $ff0000
200 100 120 $ff
360 260 200 $00

:main
	33
	mark
|	"media/obj/dragon.tga" loadtga 'texture !
	"media/obj/cube.png" loadpng 'tritexture !

	tritexture dup 'mtexture !
	@ $ffff and 'wtexture !

	show clrscr
		dup "%d" print cr

		xymouse swap 'vertex !+ !
		'vertex tritex

		xymouse swap 'vertex2 !+ !
		'vertex2 tritex2

		'exit >esc<
		cminiflecha
		;

: main ;

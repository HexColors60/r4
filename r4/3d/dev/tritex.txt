| PHREDA 2016
| 2017 idea uno negativo
| referencia con 10.22 punto fijo
|--------------------------
^r4/lib/gui.txt
^r4/lib/loadpng.txt
^r4/lib/loadtga.txt
^r4/lib/loadpng.txt
^r4/lib/trace.txt

#:tritexture 0
#:trizbuffer 0

#minx #maxx
#miny #maxy
#va0 #va1
#vb0 #vb1
#inv_p0z #inv_p1z #inv_p2z
#inv_p0u #inv_p1u #inv_p2u
#inv_p0v #inv_p1v #inv_p2v
#inv_Pz
#oiz #o0 #o1 #o2

:loop | xv0 yv0 y x -- xv0 yv0 y x
	pick3 over -	| va2
	pick3 pick3 -	| vb2

	va1 over * pick2 vb1 * -
	oiz * -? ( 3drop 4 a+ ; )
    'o2 !
	swap vb0 * swap va0 * -
	oiz * -? ( drop 4 a+ ; )
	'o1 !
	$1000000 o1 o2 + -
	-? ( drop 4 a+ ; )
	'o0 !

	o2 inv_p2z 22 *>>
	o1 inv_p1z 22 *>> +
	o0 inv_p0z 22 *>> +
	a> trizbuffer + @ <? ( drop 4 a+ ; )
	dup	a> trizbuffer + !

	'inv_Pz !
	|---- correct
	inv_p0u o0 22 *>>
	inv_p1u o1 22 *>> +
	inv_p2u o2 22 *>> +
	inv_Pz / $3ff and
	inv_p0v o0 22 *>>
	inv_p1v o1 22 *>> +
	inv_p2v o2 22 *>> +
	inv_Pz / $3ff and

	| u v
|	8 << | 256x256
	tritexture @ $ffff and *
	or
	1+ 2 << tritexture + @
	a!+
	;

::tritex | vert --
	dup @ over 16 + @ pick2 32 + @ | x0 x1 x2
	dup pick3 - 'va0 ! over pick3 - 'va1 !
	pick2 pick2 pick2
	min min
	sw >=? ( 4drop drop ; )
	clamp0 'minx !
	max max
	-? ( 2drop ; )
	sw clampMax 'maxx !
	dup 4+ @ over 20 + @ pick2 36 + @ | y0 y1 y2
	dup pick3 - 'vb0 ! over pick3 - 'vb1 !
	pick2 pick2 pick2
	min min
	sh >=? ( 4drop drop ; )
	clamp0 'miny !
	max max
	-? ( 2drop ; )
	sh clampMax 'maxy !

	va0 vb1 * va1 vb0 * - 0? ( 2drop ; )
	$1000000 swap / 'oiz !

	$1000000 over 8 + @ /
	over 12 + @ dup >r
	16 0>> over * 'inv_p0u !
	r> $ffff and over * 'inv_p0v !
	'inv_p0z !
	$1000000 over 24 + @ /
	over 28 + @ dup >r
	16 0>> over * 'inv_p1u !
	r> $ffff and over * 'inv_p1v !
	'inv_p1z !
	$1000000 over 40 + @ /
	over 44 + @ dup >r
	16 0>> over * 'inv_p2u !
	r> $ffff and over * 'inv_p2v !
	'inv_p2z !
	@+ swap @	| vx0 vy0
	minx miny setxy
	miny ( maxy <? )(
		minx ( maxx <? )(
			loop
			1+ ) drop
		sw maxx minx - - 2 << a+
		1+ ) 3drop ;

|-------------------------
| http://www.hugi.scene.org/online/coding/hugi%2017%20-%20cotriang.htm

#wtexture

#Right_dXdY #RightX
#Left_dXdY #LeftX
#Left_dUdY #LeftU
#Left_dVdY #LeftV
#Left_dZdY #LeftZ

#u_a #v_a #z_a
#u_b #v_b #z_b
#u_c #v_c #z_c

#dLeftz
#dLeftu
#dLeftv

#v1 #v2 #v3
#y31 #y32 #y21


:texture!+ | u v --
	21 0>> $3ff and
	wtexture * swap
	21 0>> $3ff and
	or
	1+ 2 << tritexture + @ a!+ ;

:drawfull | z u v -- z u v

	$200000 pick3 1? ( 21 <</ )( drop ) |z?
	a> trizbuffer + @ <? ( drop 4 a+ ; )
	dup	a> trizbuffer + !
	pick2 over 21 *>>
	pick2 rot 21 *>>

|	over pick3 21 <</
|	over pick4 21 <</

	texture!+
	rot dLeftz +
	rot dLeftu +
	rot dLeftv + ;

:linesegment
	LeftZ LeftU LeftV
	RightX ceil
	LeftX 16 >>  		| x1
	dup 2 << b> + >a
	- 0? ( 4drop ; )
	( 1? )( 1- >r
		drawfull
		r> ) 4drop ;

:drawsegment | ycnt --
	( 1? )( >r
        linesegment
		Left_dUdY 'LeftU +!
		Left_dVdY 'LeftV +!
		Left_dZdY 'LeftZ +!
		Left_dXdY 'LeftX +!
		Right_dXdY 'RightX +!
		sw 2 << b+
		r> 1- ) drop ;

:>x @ ;
:>y 4+ @ ;
:>z 8 + @ ;
:>uv 12 + @ ;

:sortv
	v1 >y v2 >y >? ( v1 v2 'v1 ! 'v2 ! ) drop
	v1 >y v3 >y >? ( v1 v3 'v1 ! 'v3 ! ) drop
	v2 >y v3 >y >? ( v2 v3 'v2 ! 'v3 ! ) drop
	;

|--- LEFT
:1-2
	v2 @ v1 @ - over 16 <</ 'Left_dXdY !
	u_b u_a - over / 'Left_dUdY !
	v_b v_a - over / 'Left_dVdY !
	z_b z_a - over / 'Left_dZdY !

	u_a 'LeftU !
	v_a 'LeftV !
	z_a 'LeftZ !
	v1 >x 16 << 'LeftX !

	drawsegment ;

:2-3
	v3 @ v2 @ - over 16 <</ 'Left_dXdY !
	u_c u_b - over / 'Left_dUdY !
	v_c v_b - over / 'Left_dVdY !
	z_c z_b - over / 'Left_dZdY !

	u_b 'LeftU !
	v_b 'LeftV !
	z_b 'LeftZ !
	v2 >x 16 << 'LeftX !

	drawsegment ;

:leftside
	v3 @ v1 @ - y31 16 <</ 'Right_dXdY !
	v1 >x 16 << 'RightX !

    y21 1? ( 1-2 )( drop )
    y32 1? ( 2-3 )( drop )
	;

|--- RIGHT
:1-2
	v2 @ v1 @ - over 16 <</ 'Right_dXdY !
	v1 >x 16 << 'RightX !
	drawsegment ;

:2-3
	v3 @ v2 @ - over 16 <</ 'Right_dXdY !
	v2 >x 16 << 'RightX !
	drawsegment ;

:rightside
	y31
	v3 @ v1 @ - over 16 <</ 'Left_dXdY !
	u_c u_a - over / 'Left_dUdY !
	v_c v_a - over / 'Left_dVdY !
	z_c z_a - swap / 'Left_dZdY !

	u_a 'LeftU !
	v_a 'LeftV !
	z_a 'LeftZ !
    v1 >x 16 << 'LeftX !

	y21 1? ( 1-2 )( drop )
	y32 1? ( 2-3 )( drop )
	;

::tritex2 | 'vert --
	dup 'v1 ! 16 + dup 'v2 ! 16 + 'v3 !
	sortv
	v2 >y
	v1 >y dup >b
	v3 >y
	dup pick2 - 0? ( 4drop ; ) 'y31 !
	pick2 - 'y32 !
	- 'y21 !

	$200000 v1 >z / 'z_a !
	$200000 v2 >z / 'z_b !
	$200000 v3 >z / 'z_c !

	v1 >uv dup 16 0>> z_a * 'u_a ! $ffff and z_a * 'v_a !
	v2 >uv dup 16 0>> z_b * 'u_b ! $ffff and z_b * 'v_b !
	v3 >uv dup 16 0>> z_c * 'u_c ! $ffff and z_c * 'v_c !

	v3 >x v1 >x - y21 *
	v2 >x v1 >x - y31 * -
	0? ( drop ; )
	u_c u_a - y21 * u_b u_a - y31 * - over / 'dLeftU !
	v_c v_a - y21 * v_b v_a - y31 * - over / 'dLeftV !
	z_c z_a - y21 * z_b z_a - y31 * - over / 'dLeftZ !

	tritexture @ $ffff and 'wtexture !
 	b> sw * 2 << framev + >b
	+? ( drop leftside ; )
	drop rightside ;

|-------------------------
| x y z uv
#vertex2
189 183 40 $ff0000
200 100 120 $ff
60 160 100 $ff00ff


#vertex
549 424 3223281 $ff0000
156 232 3176603 $ff
365 226 3169984 $00

:dumpl
	@+ "x:%d " print
	@+ "y:%d " print
	@+ "z:%d " print
	@+ "c:%h " print
	cr
	;
:dump
	dumpl dumpl dumpl drop ;

:movz | cnt vert --
	8 + >a dup a> +!
	16 a+ dup a> +!
	a> 16 + +! ;

:main
	33
	mark
|	"media/obj/dragon.tga" loadtga 'texture !
	"media/obj/cube.png" loadpng 'tritexture !

	show clrscr
		dup "%d" print cr

|		xymouse swap 'vertex !+ !

		'vertex2 dump

		xymouse swap 'vertex2 !+ !
		'vertex2 tritex2

		'exit >esc<
		[ 10 'vertex2 movz ; ] <up>
		[ -10 'vertex2 movz ; ] <dn>

		cminiflecha
		;

: main ;

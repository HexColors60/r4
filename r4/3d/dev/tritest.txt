| PHREDA 2017 idea
| dibujo de triangulo por punto medio
| para voxelizar triangulos texturados
|--------------------------
^r4/lib/gui.txt
^r4/lib/trace.txt

#verlist )( 8192
#verlist> 'verlist

#cache

:draw1p
	@+ swap @+ swap 4+ @ >r
	setxy
	r> a>
	cache =? ( 2drop ; )
	dup 'cache ! !
	;

:endtri
	dup draw1p 48 - ;

| x y z uv
:samep | adr --
	dup @
	over 16 + @ <>? ( drop 0 ; )
	over 32 + @ <>? ( drop 0 ; )
	drop

	dup 4+ @
	over 20 + @ <>? ( drop 0 ; )
	over 36 + @ <>? ( drop 0 ; )
	drop
	1 ;

| siempre converge a o2
:2/1 | o2 o1 -- om ;
	over - dup 31 >> - 2/ + ;

:maketri | adr --		b:src a:des
	dup	@+ b@+ 2/1 a!+ @+ b@+ 2/1 a!+ @+ b@+ 2/1 a!+ @ b@+ colavg a!+
	dup @+ b@+ 2/1 a!+ @+ b@+ 2/1 a!+ @+ b@+ 2/1 a!+ @ b@+ colavg a!+
	@+ b@+ 2/1 a!+ @+ b@+ 2/1 a!+ @+ b@+ 2/1 a!+ @ b@+ colavg a!+ ;

:rectri | adr -- adr
	samep 1? ( drop endtri ; ) drop

	dup 48 + >a dup >b dup maketri
    48 + rectri

	dup 48 + >a dup >b dup 16 + maketri
    48 + rectri

	dup 48 + >a dup >b dup 32 + maketri
    48 + rectri

	dup 48 + >a dup >b
	b> 32 + @ b@+ + 2/ a!+
	b> 32 + @ b@+ + 2/ a!+
	b> 32 + @ b@+ + 2/ a!+
	b> 32 + @ b@+ colavg a!+
	b> 16 - @ b@+ + 2/ a!+
	b> 16 - @ b@+ + 2/ a!+
	b> 16 - @ b@+ + 2/ a!+
	b> 16 - @ b@+ colavg a!+
	b> 16 - @ b@+ + 2/ a!+
	b> 16 - @ b@+ + 2/ a!+
	b> 16 - @ b@+ + 2/ a!+
	b> 16 - @ b@+ colavg a!+
    48 + rectri

    48 -
	;

:tritest | 'vertez --
	-1 'cache !
	>a 'verlist >b
	a@+ b!+ a@+ b!+ a@+ b!+ a@+ b!+
	a@+ b!+ a@+ b!+ a@+ b!+ a@+ b!+
	a@+ b!+ a@+ b!+ a@+ b!+ a@+ b!+
	'verlist rectri drop
	;

|-------------------------
| x y z uv
#vertex
189 183 0 $ff0000
200 100 0 $ff
60 160 0 $ff00

:main
|	33
	mark
	show clrscr
|		dup "%d" print cr

		xymouse swap 'vertex !+ !
		'vertex tritest

		'exit >esc<
		;

: main ;

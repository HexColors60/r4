| Obj Model Loader
| - faltan indices negativos
| - faltan lineas en desorden
| PHREDA 2017
|-----------------------------------
^r4/lib/gui.txt
^r4/lib/parse.txt
^r4/lib/btn.txt
^r4/lib/trace.txt

#iniobj | textparse
#verl	| vertices
#nver
#facel	| faces
#nface
#norml	| normal
#nnorm
#texl	| text
#ntex

:vert	| vertices (x,y,z[,w=1])
	nver 0? ( here 'verl ! ) 1+ 'nver !
	2 + trim
	getfenro ,
	getfenro ,
	getfenro ,
	dup c@ 32 <? ( drop 1.0 , ; ) drop
	getfenro ,
	;
:texc	| textcoor (u, v [,w])
	ntex 0? ( here 'texl ! ) 1+ 'ntex !
	3 + trim
	getfenro ,
	getfenro ,
	getfenro ,
	;
:norm	| normales (x,y,z)
	nnorm 0? ( here 'norml ! ) 1+ 'nnorm !
	3 + trim
	getfenro ,
	getfenro ,
	getfenro ,
	;
:pspa	| param space ( u [,v] [,w] )
	3 + trim
	getfenro ,
	getfenro ,
	getfenro ,
	;

#nv #nt #nn
:uface
	?sint 'nv ! dup 1- c@ 
	32 <? ( drop 1- ; ) 33 <? ( drop ; ) drop
	?sint 'nt ! dup 1- c@
	32 <? ( drop 1- ; ) 33 <? ( drop ; ) drop
	?sint 'nn ! dup 1- c@
	32 <? ( drop 1- ; ) drop
	;

:face	| face nvert( v/t/n  v//n v/t  v)??
	nface 0? ( here 'facel ! ) 1+ 'nface !
	2 + trim
	here >r
	0 ,
	( dup c@ $ff and 13 >? )( drop
		uface
		nv ,
		nt 16 << nn or ,
		1 r@ +!
		) drop
	r> drop
	;
:smoo	| 1/off
	2 + trim
	;
:onam	| o [object name]
	2 + trim
	;
:gman	| g [group name]
	2 + trim
	;
:mtli	| mtllib [external .mtl file name]
	6 + trim
	;
:usmt	| usemtl [material name]
	6 + trim
	;

:parseline
	"v " =pre 1? ( drop vert ; ) drop	| vertices (x,y,z[,w=1])
	"vt	" =pre 1? ( drop texc ; ) drop	| textcoor (u, v [,w])
	"vn " =pre 1? ( drop norm ; ) drop	| normales (x,y,z)
	"vp " =pre 1? ( drop pspa ; ) drop	| param space ( u [,v] [,w] )
	"f " =pre 1? ( drop face ; ) drop	| face nvert( v/t/n  v//n v/t  v)??
	"s " =pre 1? ( drop smoo ; ) drop	| 1/off
	"o " =pre 1? ( drop onam ; ) drop	| o [object name]
	"g " =pre 1? ( drop gman ; ) drop	| g [group name]
	"mtllib" =pre 1? ( drop mtli ; ) drop	| mtllib [external .mtl file name]
	"usemtl" =pre 1? ( drop usmt ; ) drop	| usemtl [material name]
	;

::>>cr | adr -- adr'/0	; proxima linea/0
	( c@+ $ff and 13 >? )( drop ) 0? ( nip ; ) drop ;

:parseobj | adr --
	0 'nver !
	0 'nface !
	0 'nnorm !
	0 'ntex !
	( trim parseline >>cr 0? ) 2drop ;


:loadobj | "" -- mem
	here dup 'iniobj !
	swap load over =? ( 2drop 0 ; ) 0 swap !+
	dup 'here ! 'verl !
	iniobj parseobj

	here
	;

|-------------
#v2d

:objtest
	mark
	here 'v2d !
	verl >b
	nver ( 1? )( 1-
		b@+ b@+ b@+ 4 b+ project3d
		xy>d , ) drop
	facel >b
	nface 200 - ( 1? )( 1-
		b@+ 1-
		b@+ 1- 2 << v2d + @ dup d>xy op
		4 b+
		swap ( 1? )( 1-
			b@+ 1- 2 << v2d + @ d>xy line
			4 b+
			) drop
		d>xy line
|		b> 4 - @ ink poli
		) drop
	empty
	;

|-------------
:objwireframe | mem --
	drop
	;

|-----------------------------------
#xcam 0 #ycam 0 #zcam -5.0
#xr #yr

:freelook
	xymouse
	sh 2/ - 7 << swap
	sw 2/ - neg 7 << swap
	neg
|	swap 0 mrotxyz
	mrotx mroty
;

#model

:main
|	"media/obj/PoliceCar/PoliceCar.obj"
	"media/obj/dragon.obj"
|	"media/obj/monkey.obj"
|	"media/obj/diablo3_pose/diablo3_pose.obj"
	loadobj 'model !
	4 fonti
	show clrscr
		cr sp
		'exit dup >esc< rojo " X " btnt sp

		dup ":r%d " blanco print
		"Load Obj" verde print cr cr
		nver " %d vertices" print cr
    	nface " %d caras" print cr

		omode
		freelook
		xcam ycam zcam mtrans

|        model objwireframe
		objtest

		[ 0.1 'zcam +! ; ] <up>
		[ -0.1 'zcam +! ; ] <dn>
		'exit >esc<
		cminiflecha ;

		;

: mark 0 'paper ! 33 main ;

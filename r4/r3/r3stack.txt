| r3STACK 2018
| PHREDA
|--------------------

|--- Pilas
#REGA
#REGB
#TOS 0
#PSP )( 1024
#NOS 'PSP
#RSP )( 1024
#RTOS 'RSP

| format cell
| type-- $0000000f
| $0 nro	33
| $1 cte    FREEMEM
| $2 str    s01
| $3 cod    w32
| $4 dat	v33
| $5 reg	eax ebx
| $6 stk	[ebp] [ebp+4] ...

#nrovalue )( 128

|-------- constantes del sistema
#syscons "XRES" "YRES" "FREE_MEM" "SYSFRAME" "SYSXYM" "SYSBM" "SYSKEY"

#sysregs "eax" "ebx" "ecx" "edx" "edi" "esi" "r8d" "r9d" "r10d" "r11d" "r12d" "r13d" "r14d" "r15d"
#sysregw  "ax"  "bx"  "cx"  "dx"  "di"  "si" "r8w" "r9w" "r10w" "r11w" "r12w" "r13w" "r14w" "r15w"
#sysregb  "al"  "bl"  "cl"  "dl" "dil" "sil" "r8b" "r9b" "r10b" "r11b" "r12b" "r13b" "r14b" "r15b"

:list2str | nro 'list -- str
	swap ( 1? )( 1- swap >>0 swap ) drop ;

|---- imprime celda

:mt0 value "$%h" ,print ;			|--	0 nro 	33
:mt1 value 'syscons list2str ,s ;	|--	1 cte	XRES
:mt2 value "str%h" ,print ;			|--	2 str   "hola"
:mt3 value "w%h" ,print ;			|--	3 cod  'func		4 dat  'var

:mt5 value 'sysregs list2str ,s ;	|-- 5 reg 	eax
:mt5b value 'sysregb list2str ,s ;	|-- 5 regb 	al
:mt5w value 'sysregw list2str ,s ;	|-- 5 regw 	ax

:mt6 value 2 << "dword[ebp" ,s 1? ( +? ( "+" ,s ) ,d )( drop ) "]" ,s ; |-- 6 stack

#tiposrm mt0 mt1 mt2 mt3 mt3 mt5 mt6 mt0 mt0
#tiposrmb mt0 mt1 mt2 mt3 mt3 mt5b mt6 mt0 mt0
#tiposrmw mt0 mt1 mt2 mt3 mt3 mt5w mt6 mt0 mt0

::,cell | val -- str
	dup $f and 2 << 'tiposrm + @ exec ;

::,cellb | nro -- nro
	dup $f and 2 << 'tiposrmb + @ exec ;

::,cellw | nro -- nro
	dup $f and 2 << 'tiposrmw + @ exec ;


|--Pila
::.DUP		4 'NOS +! TOS NOS ! ;
::.DROP		NOS @ 'TOS !	|...
::.NIP      -4 'NOS +! ;
::.2NIP      -8 'NOS +! ;
::.2DROP    NOS 4 - @ 'TOS ! -8 'NOS +! ;
::.3DROP    NOS 8 - @ 'TOS ! -12 'NOS +! ;
::.4DROP    NOS 12 - @ 'TOS ! -16 'NOS +! ;
::.SWAP     NOS @ TOS NOS ! 'TOS ! ;
::.ROT      TOS NOS 4 - @ 'TOS ! NOS @ NOS 4 - !+ ! ;
::.OVER     .DUP NOS 4 - @ 'TOS ! ;
::.PICK2    .DUP NOS 8 - @ 'TOS ! ;
::.PICK3    .DUP NOS 12 - @ 'TOS ! ;
::.PICK4    .DUP NOS 16 - @ 'TOS ! ;
::.2DUP     .OVER .OVER ;
::.2OVER    .PICK3 .PICK3 ;
::.2SWAP    TOS NOS @ NOS 4 - dup 4 - @ NOS ! @ 'TOS !  NOS 8 - !+ ! ;

|-- Internas
::.LIT		.DUP dup @ 'TOS ! 4+ ;
::.ADR		.DUP dup @ @ 'TOS ! 4+ ;

::.;		RTOS @ nip -4 'RTOS +! ;
::.CALL		4 'RTOS +! dup 4+ RTOS ! @ ;
::.JMP		@ ;

|-- condicionales
::.0?		TOS 1? ( drop @ )( drop 4+ ) ;
::.1?		TOS 0?  ( drop @ )( drop 4+ ) ;
::.+?		TOS -?  ( drop @ )( drop 4+ ) ;
::.-?		TOS 1+ +? ( drop @ )( drop 4+ ) ;

::.=?		NOS @ TOS <>? ( drop @ )( drop 4+ ) .DROP ;
::.<?		NOS @ TOS >=? ( drop @ )( drop 4+ ) .DROP ;
::.>?		NOS @ TOS <=? ( drop @ )( drop 4+ ) .DROP ;
::.<=?		NOS @ TOS >? ( drop @ )( drop 4+ ) .DROP ;
::.>=?		NOS @ TOS <? ( drop @ )( drop 4+ ) .DROP ;
::.<>?		NOS @ TOS =? ( drop @ )( drop 4+ ) .DROP ;
::.A?		NOS @ TOS nand? ( drop @ )( drop 4+ ) .DROP ;
::.N?		NOS @ TOS and? ( drop @ )( drop 4+ ) .DROP ;

::.B?		NOS @ TOS and? ( drop @ )( drop 4+ ) .DROP ;

|-- exec
::.EX		TOS .DROP 4 'RTOS +! swap RTOS ! ;

::.>R		4 'RTOS +! TOS RTOS ! .DROP ;
::.R>		.DUP RTOS dup @ 'TOS ! 4 - 'RTOS ! ;
::.R@		.DUP RTOS @ 'TOS ! ;

::.AND		NOS @ TOS and 'TOS ! .NIP ;
::.OR		NOS @ TOS or 'TOS ! .NIP ;
::.XOR		NOS @ TOS xor 'TOS ! .NIP ;
::.NOT		TOS not 'TOS ! ;
::.+		NOS @ TOS + 'TOS ! .NIP ;
::.-		NOS @ TOS - 'TOS ! .NIP ;
::.*		NOS @ TOS * 'TOS ! .NIP ;
::./		NOS @ TOS / 'TOS ! .NIP ;
::.*/		NOS 4 - @ NOS @ TOS */ 'TOS ! .2NIP ;
::.*>>		NOS 4 - @ NOS @ TOS *>> 'TOS ! .2NIP ;
::.<</		NOS 4 - @ NOS @ TOS <</ 'TOS ! .2NIP ;
::./MOD		NOS @ TOS /mod 'TOS ! NOS ! ;
::.MOD		NOS @ TOS mod 'TOS ! .NIP ;
::.ABS		TOS abs 'TOS ! ;
::.NEG		TOS neg 'TOS ! ;
::.CLZ		TOS clz 'TOS ! ;
::.SQRT		TOS sqrt 'TOS ! ;
::.<<		NOS @ TOS << 'TOS ! .NIP ;
::.>>		NOS @ TOS >> 'TOS ! .NIP ;
::.>>>		NOS @ TOS 0>> 'TOS ! .NIP ;

::.>A		TOS 'REGA ! .DROP ;
::.A>		.DUP REGA 'TOS ! ;
::.A@		.DUP REGA @ 'TOS ! ;
::.A!		TOS REGA ! .DROP ;
::.A+       TOS 'REGA +! .DROP ;
::.A@+		.DUP REGA dup 4+ 'REGA ! @ 'TOS ! ;
::.A!+		TOS REGA dup 4+ 'REGA ! ! .DROP ;

::.>B		TOS 'REGB ! .DROP ;
::.B>		.DUP REGB 'TOS ! ;
::.B@		.DUP REGB @ 'TOS ! ;
::.B!		TOS REGB ! .DROP ;
::.B+       TOS 'REGB +! .DROP ;
::.B@+		.DUP REGB dup 4+ 'REGB ! @ 'TOS ! ;
::.B!+		TOS REGB dup 4+ 'REGB ! ! .DROP ;


:i@			TOS @ 'TOS ! ;
:iC@		TOS c@ 'TOS ! ;
:iD@		TOS w@ 'TOS ! ;
:i!			NOS @ TOS ! .NIP .DROP ;
:iC!		NOS @ TOS c! .NIP .DROP ;
:iD!		NOS @ TOS w! .NIP .DROP ;
:i+!		NOS @ TOS +! .NIP .DROP ;
:iC+!		NOS @ TOS c+! .NIP .DROP ;
:iD+!		NOS @ TOS w+! .NIP .DROP ;
:i@+		.DUP 4 NOS +! TOS @ 'TOS ! ;
:i!+		NOS @ TOS ! .NIP 4 'TOS +! ;
:iC@+		.DUP 1 NOS +! TOS c@ 'TOS ! ;
:iC!+		NOS @ TOS c! .NIP 1 'TOS +! ;
:iD@+		.DUP 2 NOS +! TOS w@ 'TOS ! ;
:iD!+		NOS @ TOS w! .NIP 2 'TOS +! ;

|-------------------------------
::IniSTACK
	'PSP 4 - 'NOS !
	'RSP 'RTOS !
	;

::GETDSTACK | nro -- value
	0? ( drop TOS ; )
	1- 2 << NOS - @ ;

::PUSH.NRO	| nro --
	.DUP 'TOS ! ;
::PUSH.CTE	| ncte --
	.DUP 'TOS ! ;
::PUSH.STR	| vstr --
	.DUP 'TOS ! ;
::PUSH.WRD	| vwor --
	.DUP 'TOS ! ;
::PUSH.REG	| reg --
	.DUP 'TOS ! ;
::PUSH.STK	| stk --
	.DUP 'TOS ! ;


::.popIP	| -- val
	RTOS dup @ swap 4 - RSP max 'RTOS ! ;


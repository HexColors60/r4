| r3 compiler
| pass 2 - tokenizer
| PHREDA 2018
|----------------
^./r3base.txt

#flag

::,, | n --
	code> !+ 'code> ! ;

:.com
	"|WIN|" =pre 1? ( drop 5 + ; ) drop | Compila para WINDOWS
	>>cr
	;

#codeini

|-----
#sst )( 1024 	| stack of blocks
#sst> 'sst
:sst!	sst> !+ 'sst> ! ;
:sst@   -4 'sst> +! sst> @ ;
:nivel 	sst> 'sst xor ;

:callen
	code> codeini - 2 >> | code_length
	$fffff and 12 <<
	dicc> 4 - ! | info in wordnow
	;

:inidef
	nivel 1? ( 2drop 9 'error ! 0 ; ) drop
	codeini 1? ( callen ) drop
	code> 'codeini !
	;

:.def
	inidef
	1+ dup c@
	33 <? ( code> '<<boot ! )
	$3A =? ( drop 1+ 2 'flag ! )( drop 0 'flag ! )  |::
	0 flag code> pick3 word!+
    >>sp ;

:.var
	inidef
	1+ dup c@
	$23 =? ( drop 1+ 3 'flag ! )( drop 1 'flag ! ) | ##
	0 flag code> pick3 word!+
    >>sp ;

:.str
	1+ dup src - 8 << 11 or ,,
	>>" ;

:.nro
	dup src - 8 << 7 or ,,
	>>sp ;

|---------------------------------
#iswhile

:blockIn
	1 'nbloques +!
	code>
	nbloques dup sst!
	3 << blok + !
	;

:cond | bl from to adr+ -- bl from to adr
	dup $ff and
	22 <? ( 2drop ; )
	36 >? ( 2drop ; )
	swap 8 >> 1? ( 2drop ; ) drop
	pick4 8 << or over 4 - !
	1 'iswhile !
	;

:blockOut | tok -- tok
	0 'iswhile !
	sst@ dup
	3 << blok + @
	code> | bl from to
	dup pick3 3 << blok + 4 + !
	over ( over <? )( @+ cond ) 2drop | bl from
	swap 8 <<        | tok from bl
	iswhile 0? ( drop swap 4 - +! ; ) 3drop
	1+ | 2-->3
	;

:anonIn
:anonOut
	;

:blocks
	1 =? ( blockIn ; )	| (
	2 =? ( blockOut ; )	| )
	3 =? ( anonIn ; )	| [
	4 =? ( anonOut ; )	| ]
	;

:.base | nro --
	blocks
	16 + ,,
	>>sp ;

:.word | adrwor --
	dup 8 + @ 1 and 12 + | 12 call 13 var
	swap adr>dic 8 << or ,,
	>>sp ;

:.adr | adrwor --
	dup 8 + @ 1 and 14 + | 14 dcode 15 ddata
	swap adr>dic 8 << or ,,
	>>sp ;

:wrd2token | str -- str'
	( dup c@ $ff and 33 <? )(
		0? ( nip ; ) drop 1+ )	| trim0
|	over "%w" slog | debug
	$5e =? ( drop >>cr ; )	| $5e ^  Include
	$7c =? ( drop .com ; )	| $7c |	 Comentario
	$3A =? ( drop .def ; )	| $3a :  Definicion
	$23 =? ( drop .var ; )	| $23 #  Variable
	$22 =? ( drop .str ; )	| $22 "	 Cadena
	$27 =? ( drop 1+ 		| $27 ' Direccion
|		dup ?base 0 >=? ( drop 7 'error ! drop 0 ; ) drop
		?word 1? ( .adr ; ) drop
		1 'error !
		dup "error1d %w" slog ;
		drop 0 ; )
	drop
	dup isNro 1? ( drop .nro ; ) drop		| numero
	dup ?base 0 >=? ( .base ; ) drop		| macro
	?word 1? ( .word ; ) drop		| palabra
 	1 'error !
	dup "error1 %w" slog
	drop 0 ;

:str2token | str --
	'sst sst> !
	( wrd2token 0? ) drop
	;

::r3-stage-2 | -- err/0
	cntdef allocdic
	here dup 'code ! 'code> !
	cnttokens 2 << 'here +!
	here 'blok !
	0 'nbloques !
	0 'codeini !
	'inc ( inc> <? )(
		dup @ "%w" slog
		4+ @+
		str2token
		error 1? ( nip ; ) drop
		dicc> 'dicc< !
		) drop
	callen
	nbloques 3 << 'here +!
	0 ;
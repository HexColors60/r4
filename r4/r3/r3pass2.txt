| r3 compiler
| pass 2 - tokenizer
| PHREDA 2018
|----------------
^./r3base.txt

#flag

::,, | n --
	code> !+ 'code> ! ;

:.com
	"|WIN|" =pre 1? ( drop 5 + ; ) drop | Compila para WINDOWS
	>>cr
	;

#codeini
#nivel

:callen
	code> codeini - 2 >> | code_length
	$fff and 20 <<
	dicc> 4 - ! | info in wordnow
	;

:inidef
	nivel 1? ( 2drop 9 'error ! 0 ; ) drop
	codeini 1? ( callen ) drop
	code> 'codeini !
	;

:.def
	inidef
	0 'flag !
	1+ dup c@
	33 <? ( code> '<<boot ! )
	$3A =? ( drop 1+ 2 'flag ! )( drop )  |::
	0 flag code> pick3 word!+
    >>sp ;

:.var
	inidef
	1 'flag !
	1+ dup c@
	$23 =? ( drop 1+ 3 'flag ! )( drop ) | ##
	0 flag code> pick3 word!+
    >>sp ;

:.str
	1+ src over - 8 << 11 or ,,
	>>" ;

:.nro
	src over - 8 << 7 or ,,
	>>sp ;

:blocks
	1 =? ( 1 'nivel +! ; )	| (
	2 =? ( -1 'nivel +! ; )	| )
	3 =? ( 1 'nivel +! ; )	| [
	4 =? ( -1 'nivel +! ; )	| ]
	;

:.base | nro --
	blocks
	16 + ,,
	>>sp ;

:.word | adrwor --
	dup 8 + @ 1 and 12 + | 12 call 13 var
	swap adr>dic 8 << or ,,
	>>sp ;

:.adr | adrwor --
	dup 8 + @ 1 and 14 + | 14 dcode 15 ddata
	swap adr>dic 8 << or ,,
	>>sp ;

:wrd2token | str -- str'
	( dup c@ $ff and 33 <? )(
		0? ( nip ; ) drop 1+ )	| trim0
|	over "%w" slog | debug
	$5e =? ( drop >>cr ; )	| $5e ^  Include
	$7c =? ( drop .com ; )	| $7c |	 Comentario
	$3A =? ( drop .def ; )	| $3a :  Definicion
	$23 =? ( drop .var ; )	| $23 #  Variable
	$22 =? ( drop .str ; )	| $22 "	 Cadena
	$27 =? ( drop 1+ 		| $27 ' Direccion
|		dup ?base 0 >=? ( drop 7 'error ! drop 0 ; ) drop
		?word 1? ( .adr ; ) drop
		1 'error !
		dup "error1d %w" slog ;
		drop 0 ; )
	drop
	dup isNro 1? ( drop .nro ; ) drop		| numero
	dup ?base 0 >=? ( .base ; ) drop		| macro
	?word 1? ( .word ; ) drop		| palabra
 	1 'error !
	dup "error1 %w" slog
	drop 0 ;

:str2token | str --
	0 'nivel !
	0 'codeini !
	( wrd2token 0? ) drop
	callen
	;

::r3-stage-2 | -- err/0
	cntdef allocdic
	here dup 'code ! 'code> !
	cnttokens 2 << 'here +!
	'inc ( inc> <? )(
		dup @ "%w" slog
		4+ @+
		str2token
		error 1? ( nip ; ) drop
		dicc> 'dicc< !
		) drop
	0 ;
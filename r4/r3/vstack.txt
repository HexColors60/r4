| VSTACK 2018
| PHREDA
|--------------------
^r4/lib/print.txt

|--- Pilas
#REGA
#REGB
#TOS 0
#PSP )( 1024
#NOS 'PSP
#RSP )( 1024
#RTOS 'RSP

|--Pila
::.DUP		4 'NOS +! TOS NOS ! ;
::.DROP		NOS @ 'TOS !	|...
::.NIP      -4 'NOS +! ;
::.2NIP      -8 'NOS +! ;
::.2DROP    NOS 4 - @ 'TOS ! -8 'NOS +! ;
::.3DROP    NOS 8 - @ 'TOS ! -12 'NOS +! ;
::.4DROP    NOS 12 - @ 'TOS ! -16 'NOS +! ;
::.SWAP     NOS @ TOS NOS ! 'TOS ! ;
::.ROT      TOS NOS 4 - @ 'TOS ! NOS @ NOS 4 - !+ ! ;
::.OVER     .DUP NOS 4 - @ 'TOS ! ;
::.PICK2    .DUP NOS 8 - @ 'TOS ! ;
::.PICK3    .DUP NOS 12 - @ 'TOS ! ;
::.PICK4    .DUP NOS 16 - @ 'TOS ! ;
::.2DUP     .OVER .OVER ;
::.2OVER    .PICK3 .PICK3 ;
::.2SWAP    TOS NOS @ NOS 4 - dup 4 - @ NOS ! @ 'TOS !  NOS 8 - !+ ! ;

|-- Internas
::.LIT		.DUP dup @ 'TOS ! 4+ ;
::.ADR		.DUP dup @ @ 'TOS ! 4+ ;

::.;		RTOS @ nip -4 'RTOS +! ;
::.CALL		4 'RTOS +! dup 4+ RTOS ! @ ;
::.JMP		@ ;

|-- condicionales
::.0?		TOS 1? ( drop @ )( drop 4+ ) ;
::.1?		TOS 0?  ( drop @ )( drop 4+ ) ;
::.+?		TOS -?  ( drop @ )( drop 4+ ) ;
::.-?		TOS 1+ +? ( drop @ )( drop 4+ ) ;

::.=?		NOS @ TOS <>? ( drop @ )( drop 4+ ) .DROP ;
::.<?		NOS @ TOS >=? ( drop @ )( drop 4+ ) .DROP ;
::.>?		NOS @ TOS <=? ( drop @ )( drop 4+ ) .DROP ;
::.<=?		NOS @ TOS >? ( drop @ )( drop 4+ ) .DROP ;
::.>=?		NOS @ TOS <? ( drop @ )( drop 4+ ) .DROP ;
::.<>?		NOS @ TOS =? ( drop @ )( drop 4+ ) .DROP ;
::.A?		NOS @ TOS nand? ( drop @ )( drop 4+ ) .DROP ;
::.N?		NOS @ TOS and? ( drop @ )( drop 4+ ) .DROP ;

|-- exec
::.EX		TOS .DROP 4 'RTOS +! swap RTOS ! ;

::.>R		4 'RTOS +! TOS RTOS ! .DROP ;
::.R>		.DUP RTOS dup @ 'TOS ! 4 - 'RTOS ! ;
::.R@		.DUP RTOS @ 'TOS ! ;

::.AND		NOS @ TOS and 'TOS ! .NIP ;
::.OR		NOS @ TOS or 'TOS ! .NIP ;
::.XOR		NOS @ TOS xor 'TOS ! .NIP ;
::.NOT		TOS not 'TOS ! ;
::.+		NOS @ TOS + 'TOS ! .NIP ;
::.-		NOS @ TOS - 'TOS ! .NIP ;
::.*		NOS @ TOS * 'TOS ! .NIP ;
::./		NOS @ TOS / 'TOS ! .NIP ;
::.*/		NOS 4 - @ NOS @ TOS */ 'TOS ! .2NIP ;
::.*>>		NOS 4 - @ NOS @ TOS *>> 'TOS ! .2NIP ;
::.<</		NOS 4 - @ NOS @ TOS <</ 'TOS ! .2NIP ;
::./MOD		NOS @ TOS /mod 'TOS ! NOS ! ;
::.MOD		NOS @ TOS mod 'TOS ! .NIP ;
::.ABS		TOS abs 'TOS ! ;
::.NEG		TOS neg 'TOS ! ;
::.CLZ		TOS clz 'TOS ! ;
::.SQRT		TOS sqrt 'TOS ! ;
::.<<		NOS @ TOS << 'TOS ! .NIP ;
::.>>		NOS @ TOS >> 'TOS ! .NIP ;
::.>>>		NOS @ TOS 0>> 'TOS ! .NIP ;

::.>A		TOS 'REGA ! .DROP ;
::.A>		.DUP REGA 'TOS ! ;
::.A@		.DUP REGA @ 'TOS ! ;
::.A!		TOS REGA ! .DROP ;
::.A+       TOS 'REGA +! .DROP ;
::.A@+		.DUP REGA dup 4+ 'REGA ! @ 'TOS ! ;
::.A!+		TOS REGA dup 4+ 'REGA ! ! .DROP ;

::.>B		TOS 'REGB ! .DROP ;
::.B>		.DUP REGB 'TOS ! ;
::.B@		.DUP REGB @ 'TOS ! ;
::.B!		TOS REGB ! .DROP ;
::.B+       TOS 'REGB +! .DROP ;
::.B@+		.DUP REGB dup 4+ 'REGB ! @ 'TOS ! ;
::.B!+		TOS REGB dup 4+ 'REGB ! ! .DROP ;


:i@			TOS @ 'TOS ! ;
:iC@		TOS c@ 'TOS ! ;
:iD@		TOS w@ 'TOS ! ;
:i!			NOS @ TOS ! .NIP .DROP ;
:iC!		NOS @ TOS c! .NIP .DROP ;
:iD!		NOS @ TOS w! .NIP .DROP ;
:i+!		NOS @ TOS +! .NIP .DROP ;
:iC+!		NOS @ TOS c+! .NIP .DROP ;
:iD+!		NOS @ TOS w+! .NIP .DROP ;
:i@+		.DUP 4 NOS +! TOS @ 'TOS ! ;
:i!+		NOS @ TOS ! .NIP 4 'TOS +! ;
:iC@+		.DUP 1 NOS +! TOS c@ 'TOS ! ;
:iC!+		NOS @ TOS c! .NIP 1 'TOS +! ;
:iD@+		.DUP 2 NOS +! TOS w@ 'TOS ! ;
:iD!+		NOS @ TOS w! .NIP 2 'TOS +! ;

|-------------------------------
::IniSTACK
	'PSP 4 - 'NOS !
	'RSP 'RTOS !
	;

::GETDSTACK | nro -- value
	0? ( drop TOS ; )
	1- 2 << NOS - @ ;

::.PUSHD	| val --
	.DUP 'TOS ! ;

::.popIP	| -- val
	RTOS dup @ swap 4 - RSP max 'RTOS ! ;

::dumpDstack
	TOS " %h" print
	'PSP 4+ ( NOS <=? )( @+ " %h" print ) drop
	;
| r3-gencode
| PHREDA 2018
|
^./r3base.txt
^./r3asm.txt

:adr>toklen | adr+4 -- adr len
	@+ swap 4+ @ 12 0>> ;

|----- string
#nstr

:datasteps
	dup $ff and 11 <>? ( 2drop ; ) drop
	"%d " ,print
	1 'nstr +!
	;

:gendatastr | adr --
	dup 8 + @ 1 and? ( 2drop ; )	 | code
	12 >> $fff and 0? ( 2drop ; )	 | no calls
	drop

	adr>toklen
	( 1? )( 1- >r
		@+ datasteps
		r> ) 2drop ;

|----- data
:datastep
	"%h " ,print
	;

:gendata | adr --
	dup 8 + @ 1 nand? ( 2drop ; )	| data
	12 >> $fff and 0? ( 2drop ; )	| no calls
	drop

	@+ "; #%w" ,print ,cr
	adr>toklen
|	2dup "%d %h" slog
	( 1? )( 1- >r
		@+ datastep
		r> ) 2drop
	,cr ;

::r3-gendata
	mark
	";---r3 compiler data.asm" ,ln
	0 'nstr !
	"; *** STRINGS ***" ,s ,cr

	dicc ( dicc> <? )(
		dup gendatastr
		16 + ) drop

	"; *** VARS ***" ,s ,cr
	"align 16 " ,s ,cr
	dicc ( dicc> <? )(
		dup gendata
		16 + ) drop

	0 ,c
	"r4asm/data.asm"
	savemem
	empty ;

|----- code
:tok>dicn | nro -- adr
	8 >> 4 << dicc + @ "%w" mprint ;

:tok>cte |
	8 >>  ;

:tok>str
	8 >> "%d" mprint ;

:coldefw ":" ,s tok>dicn dup c@ $3a =? ( 2drop ; ) drop ,s ;
:coldefv "#" ,s tok>dicn ,s ;
:colit	tok>cte "$%h" ,print ;
:colits	34 ,c tok>str ,s 34 ,c ;

:colwor
:colvar
	tok>dicn ,s ;
:coldwo
:coldva
	"'" ,s tok>dicn ,s ;

#codei 0 coldefw coldefw coldefv coldefv 0 0 colit colit colit colit colits colwor colvar coldwo coldva

:codestep
|	cgstep
	dup $7f and
	16 <? ( 2 << 'codei + @ exec ; ) nip
	16 - r3basename ,s
	;

:gencode | adr --
	dup 8 + @ 1 and? ( 2drop ; )	| code
	12 >> $fff and 0? ( 2drop ; )	| no calls
	drop
	@+ "; :%w" ,print ,cr
	adr>toklen
|	2dup "%d %d" slog
	( 1? )( 1- >r
		@+ codestep ,sp
		r> ) 2drop
	,cr ;

::r3-gencode
	mark
	";---r3 compiler code.asm" ,ln
	dicc ( dicc> <? )(
		dup gencode
		16 + ) drop
	0 ,c
	"r4asm/code.asm"
	savemem
	empty ;


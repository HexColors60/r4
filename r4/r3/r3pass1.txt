| r3 compiler
| PHREDA 2018
|----------------

#path )( 256

#:cnttokens
#:cntdef

|---- includes
| 'string|'mem
#:inc )( $fff
#:inc> 'inc

|------------ parse
:>>cr | adr -- adr'
	( c@+ 1? )( 10 =? ( drop 1- ; ) 13 =? ( drop 1- ; ) drop ) drop 1- ;

:>>sp | adr -- adr'
	( c@+ 1? )( $ff and 33 <? ( drop 1- ; ) drop ) drop 1- ;

:>>" | adr -- adr'
	( c@+ 1? )( 34 =? ( drop c@+ 34 <>? ( drop 1- ; ) ) drop ) drop 1- ;

:trim | adr -- adr' c
	( c@+ $ff and 33 <? )( 0? ( swap 1- swap ; ) drop ) ;


|----------- comments / configuration
:setgraf | adr -- adr
	3 + trim
|	"F" =pre 1? ( fullscreen ) drop
|	"W" =pre 1? ( size ) drop
	>>cr ;

:escom
	"|WIN|" =pre 1? ( drop 5 + ; ) drop | Compila para WINDOWS
	"||G" =pre 1? ( setgraf ; ) drop | GRAPHIC (F/Ww,h)(x2/x3/x4)
	| MOUSE/PEN
	| SOCKET
	| JOYSTICK
	| CAMERA
	| STREAM
	| CONSOLE
    >>cr ;


:includepal | str -- str'
	$7c =? ( drop escom ; )		| $7c |	 Comentario
	1 'cnttokens +!
	$22 =? ( drop >>" ; )		| $22 "	 Cadena
	$3A =? ( 1 'cntdef +! )		| $3a :  Definicion
	$23 =? ( 1 'cntdef +! )		| $23 #  Variable
	drop >>sp ;

|----------- includes
:ininc? | str -- str adr/0
	'inc ( inc> <? )(
		@+ pick2 =s 1? ( drop ; ) drop
		4+ ) drop 0 ;

:load.inc | str -- str new ; incluye codigo
    here over		| str here str
	"." =pre 1? ( drop 2 + 'path "%s%w" mprint )( drop "%w" mprint )
	load
	here =? ( drop 0 ; ) | no existe
	here 0 rot c!+ 'here ! ;

:add.inc | src here -- src
	over inc> !+ !+ 'inc> ! ;

:includes | src --
	dup ( trim 1? )(
		( $5e =? )( drop | $5e ^  Include
			ininc? 1? ( drop
				load.inc | 0? ( ) no existe
				includes
				dup ) drop
			>>cr trim )
		includepal
		) 2drop 
	add.inc ;

::r3-stage-1 | str -- err/0
	0 'cnttokens !
	0 'cntdef !
	includes ;

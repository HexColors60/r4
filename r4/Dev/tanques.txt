| Tanques War
| 2016 - Itinerario
|-----------------
^r4/lib/gui.txt
^r4/lib/part16.txt
^r4/dev/tanque.spr

|tanque1
#xt -0.5 #yt #dt 0 #vt #rt #ct #adt
#xb #yb #db #vxb #vyb #tb
#danio

|tanque2
#xt2 0.5 #yt2 #dt2 0.5 #vt2 #rt2 #ct2 #adt2
#xb2 #yb2 #db2 #vxb2 #vyb2 #tb2
#danio2

| fx
#fx 0 0

|-----------------
:r0001 rand 0.001 mod ;
:r001 rand 0.01 mod ;
:r01 rand 0.1 mod ;

:fpos
	8 >> sh 2/ + swap 8 >> sw 2/ + swap pos ;

:distancia
	 rot - dup *. >r - dup *. r> + sqrt. ;

|-----------------
:phumo
	dup @ 1- 0? ( nip ; )
	dup 2* alpha
	swap !+ >r
	r@+	r@+ fpos
	r@+ r 12 - +!	| VX
	r@+ r 12 - +!	| VY
	r@+ qdim
	r@+ r@+ rnsprite
	r@+ r 8 - +!
	rdrop
	;

:+humo | x y --
	'phumo 'fx p!+ >r
	127 r!+ | vida
	r!+ r!+ | x y
	r0001 r!+ r0001 r!+ | vx vy
	r0001 4 + r!+		| tamaño
	'humo2 r!+			| dibujo
	0 r!+
	r001 r!+
	rdrop ;

:ptext
	dup @ 1- 0? ( nip ; ) swap !+ >r
	r@+ r@+ atxy
	r@+ ink
	r@+ printx
	rdrop ;

:+text |
	'ptext 'fx p!+ >r
	1000 r!+
	r!+ r!+ r!+ r!+
	rdrop ;


:reset
	'fx p.clear
	;

|--------------
:ganotanque2
	show clrscr
		"gano tanque2!" print
		'exit >esc< ;

:ganotanque1
	show clrscr
		"gano tanque1!" print
		'exit >esc< ;

:golpet1
	0 'tb2 ! 0 'vt !
	10 'danio +!
	danio 100 >=? ( ganotanque2 exit ) drop
	10 ( 1? )( yt xt +humo 1- ) drop
	;

:golpet2
	0 'tb ! 0 'vt2 !
	10 'danio2 +!
	danio2 100 >=? ( ganotanque1 exit ) drop
	10 ( 1? )( yt2 xt2 +humo 1- ) drop
	;


:bala1
	tb 0? ( drop ; ) drop
	48 qdim xb yb fpos
	'misil db rnsprite

	vxb 'xb +! vyb 'yb +! -1 'tb +!

	xb yb xt2 yt2 distancia
	0.05 <? ( golpet2 ) drop
	;

:bala2
	tb2 0? ( drop ; ) drop
	48 qdim xb2 yb2 fpos
	'misil db2 rnsprite

	vxb2 'xb2 +! vyb2 'yb2 +! -1 'tb2 +!

	xb2 yb2 xt yt distancia
	0.05 <? ( golpet1 ) drop
	;

:disparo
	tb 1? ( drop ; ) drop
	xt 'xb ! yt 'yb ! dt 'db !
	dt sincos 0.01 *. 'vxb ! 0.01 *. 'vyb !
	75 'tb ! ;

:disparo2
	tb2 1? ( drop ; ) drop
	xt2 'xb2 ! yt2 'yb2 ! dt2 'db2 !
	dt2 sincos 0.01 *. 'vxb2 ! 0.01 *. 'vyb2 !
	75 'tb2 ! ;

:tanque1
	80 qdim xt yt fpos
	'stanque dt rnsprite

	dt sincos vt *. 'xt +! vt *. 'yt +!

	[ -0.001 'adt ! ; ] <le>
	[ 0.001 'adt ! ; ] <ri>
	adt 'dt +!
	[ 0 'adt ! ; ] dup >le< >ri<
	[ 0.001 'vt ! ; ] <up>
	[ 0 'vt ! ; ] >up<
	 'disparo <dn>
	;

:tanque2
	80 qdim xt2 yt2 fpos
	'stanque dt2 rnsprite

	dt2 sincos vt2 *. 'xt2 +! vt2 *. 'yt2 +!

	[ -0.001 'adt2 ! ; ] 30 ?key
	[ 0.001 'adt2 ! ; ] 32 ?key
	adt2 'dt2 +!
	[ 0 'adt2 ! ; ] dup 30 ?ukey 32 ?ukey
	[ 0.001 'vt2 ! ; ] 17 ?key
	[ 0 'vt2 ! ; ] 17 ?ukey
	 'disparo2 31 ?key
	;


:main
	show clrscr
		"tanques " print
		danio "t1: %d " print
		danio2 "t2: %d" print

		bala1
		bala2

		tanque1
		tanque2
		'fx p.draw 255 alpha

		'exit >esc<
		cminiflecha ;

:memoria
	mark
	10000 'fx p.ini
	;

: memoria main ;

| Rutinas Graficas
| PHREDA 2013
|--------------------------
^r4/lib/gui.txt
^r4/lib/btn.txt
^r4/lib/trace.txt


| Cover LIst
| PHREDA 2013

|--------------- COVERBUFFER
| dato - puntero
#coverbuff )( 4096
#coverbuff> 'coverbuff
#ccnt 0
#rl

| pos(12) | len(11) | cover(9) |
:getpos 20 >> $fff and ;
:getlen 9 >> $7ff and ;
:getval $1ff and ;
:setpos 20 << ;
:setlen 9 << ;

#suma
:dump
	0 'suma ! cr cr cr
	'coverbuff
	(   rl =? ( ">" print )
		@+
		dup getpos "%d " print
		dup getlen dup 'suma +!
		"%d " print
		getval "%d" print cr
		@ 0? ) drop
	suma "-----%d-----" print
	;


:inicover | largo --
	'coverbuff >a
	0 setpos sw 1+ setlen or a!+ 0 a!+
	a> 'coverbuff> ! ;


|------------------------------------------------------
:cover.busca | pos adr -- pos  adr'
	( dup 4+ @ 1? )(	| pos adr1 adr2
		dup @ getpos pick3
		>? ( 2drop ; ) drop
		nip ) drop ;

:insert1 | adr -- adr
	dup 4+ @ coverbuff> 4+ !
	coverbuff> over 4+ !
	8 'coverbuff> +! ;

:insert2 | adr -- adr
	dup 4+ @ coverbuff> 12 + !
	coverbuff> over 4+ !
	coverbuff> 4+ dup 4+ swap !
	16 'coverbuff> +! ;

:cover.insp | valor pos adr -- adr'
	>a
	a@ getlen
	1 =? ( 2drop a> +! a> 4+ @ ; )
	a@ getpos | v p largo pos
	pick2 =? ( 2drop |--- inicio de segmento
		a> insert1
		@ dup $fff001ff and $200 or rot drop rot + a!+
		a@ >a
		$100000 + $200 - a!
		a> ; )
	+ 1- over =? ( 2drop |--- fin de segmento
		a> insert1
		@ dup $200 - a!+ a@ >a
		getval rot + swap setpos or 1 setlen or a!+
		a@ ; )
	1+
	a> insert2 @ | val pos posf v
	dup $fff001ff and pick3 pick2 getpos - setlen or a!+ a@ >a
   	getval	| valor pos posf valf
	pick3 over + pick3 setpos or 1 setlen or a!+ a@ >a
	rot 1+	| valor posf valf pos
	rot over - setlen swap setpos or | valor
	or nip a! a> ;

:cover.insl | valor largo pos adr -- adr'
	rot 1 =? ( drop cover.insp ; )
	swap >a swap | valor largo pos
	a@ getpos over
	=? (  |--- empieza igual
		a@ getlen | valor largo pos posv largov
		pick3 >? ( |-- ocupa menos
			a> insert1 >a nip | valor largo pos largov
			pick2 - setlen
			pick2 pick2 + setpos or
			a@ getval or
			coverbuff> 8 - !	| valor largo pos
			rot a@ getval + 	| largo pos v+v
			rot setlen or swap setpos or a!
			coverbuff> 8 - ; )
		pick3 <? ( |--- ocupa mas
			nip a@ pick4 + a!+	| valor largo pos largov
			rot over -			| valor pos lv len-lv
			rot rot +			| valor largo pos
			a@ cover.insl ; )
    	4drop a@ + a!+ a@ ; ) |-- ocupa igual
	|--- empieza adentro
	a@ getlen + | valor largo pos posf
	pick2 pick2 +
	>? ( |-- ocupa menos
		a> insert2 @ getval
		a@ $fff001ff and pick3 a@ getpos - setlen or a!+ a@ >a
		| valor largo pos posf vv
		pick4 over + pick4 setlen or pick3 setpos or a!+ a@ >a
		| valor largo pos posf vv
		>r >r + r>  | valor len+pos posf
		over - setlen swap setpos or r> or a!
		drop a> ; )
	pick2 pick2 +
	<? ( |-- ocupa mas
		a> insert1 @ getval | valor largo pos posf vv
		a@ $fff001ff and pick3 a@ getpos - setlen or a!+ a@ >a
			| valor largo pos posf vv
		pick4 + over pick3 - setlen or pick2 setpos or a!+
		| valor largo pos posf
		rot rot + over -
		swap a@
		cover.insl ; )
	|--- ocupa hasta
	a> insert1 @ getval | valor largo pos posf vv
	a@ $fff001ff and pick3 a@ getpos - setlen or a!+ a@ >a
	nip rot setlen rot setpos or >r + r> or a!+ a@
	;

#lugar 30
:testinsert
	1 lugar 'coverbuff cover.busca
	cover.insp
	'rl !
	1 'lugar +!
	;

:testinsert2
	3 70 42 'coverbuff cover.busca
	cover.insl
	'rl !
	;

:testinsert3
	'coverbuff insert2
	3 9 << over 4+ @ !
	'rl !
	;


:main
	500 inicover
	show clrscr
		verde
		dump cr
		[  testinsert ; ] <f1>
		[  testinsert2 ; ] <f2>
		[  testinsert3 ; ] <f3>

		cflecha
		'exit >esc<
		;

: main ;


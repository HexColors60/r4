| 2017 PHEDA
|-----------------
^r4/lib/gui.txt
^r4/lib/xfb.txt
^r4/lib/part16.txt
^r4/lib/plqlib.txt

^r4/lib/vesprite.txt
^r4/rtv/fx.vsp

#ti 0 0
#fx 0 0
#objs 0 0
#movs 0 0


|-----------------------------
:tletra
	>b
	b@+ b@+ pos 0.8 qdim home
	0 0.05 boxpad
	1 0.5 setfont3d
	blanco
	b@ 64 + "%k" mprint 6 boxprintn
	;

:+letra | nro x y --
	'tletra 'fx p!+ >a
	swap a!+ a!+
	a!
	;

|-----------------------------
:stsprite
	dup 7 2 << + @ 0? ( 2drop ; ) drop
	>b
	mpush
	b@+ b@+ 0 mtransi
	b@+ 1? ( mrotxi )( drop )
	b@+ 1? ( mrotyi )( drop )
	b@+ 1? ( mrotzi )( drop )
	b@+ dup dup mscalei
	b@ 3dvesprite
	mpop
	;

:+sprite | 'spr size x y --
	'stsprite 'objs p!+ >a
	swap a!+ a!+		| x y
	0 0 0 a!+ a!+ a!+	| rx ry rz
	a!+					| s
	a!+					| sprite
	1 a!				| estado
	;

:sprite.n | nro -- spr
	6 << 'objs 4+ @ + ;

:nxy.sprite | x y nro --
	sprite.n 4+ >a swap a!+ a! ;

:ns.sprite | s nro --
	sprite.n 24 + ! ;

:e.sprite | e nro --
	sprite.n 8 2 << + ! ;

:xy.sprite | x y obj --
	4+ >a swap a!+ a! ;


|---------- animadores
#deltat
#prevt
#t0

:itime
	msec 'prevt ! 0 'deltat ! ;

:dtime
	msec dup prevt - 'deltat ! 'prevt ! ;

:llerp | a b -- r | a + t * (b - a) | t 0.0 .. 1.0
	over - t0 16 *>> + ;
:clerp | a b -- r | a + t * (b - a) | t 0.0 .. 1.0
	over - t0 cos abs 16 *>> + ;
:slerp | a b -- r | a + t * (b - a) | t 0.0 .. 1.0
	over - t0 sin abs 16 *>> + ;

|-------------------
:movlinea
	>a
	a@+ deltat + dup a> 4 - !
	16 << a@+ / 1.0 min 't0 !
	a@+
	4+ a@+ a@+ llerp over !	| x
	4+ a@+ a@+ llerp swap !	| y
	t0 1.0 - 1? ( drop ; )
	;

:+linea | xf yf obj time --
	'movlinea 'movs p!+ >a
	0 a!+ 1000 *. a!+	| 0 tmax
	dup a!+		| obj
	| xf yf obj
	4+ dup @ a!+ rot a!+
	4+ @ a!+ a!
	;

:movlin
	>a
	a@+ deltat + dup a> 4 - !
	16 << a@+ / 0.25 min 't0 !
	a@+
	4+ a@+ a@+ slerp over !	| x
	4+ a@+ a@+ slerp swap !	| y
	t0 0.25 - 1? ( drop ; )
	;

:+lin | xf yf obj time --
	'movlin 'movs p!+ >a
	0 a!+ 1000 *. a!+	| 0 tmax
	dup a!+		| obj
	| xf yf obj
	4+ dup @ a!+ rot a!+
	4+ @ a!+ a!
	;

:movcircle
	>a
	a@+ deltat + dup a> 4 - !
	16 << a@+ 2* / 0.5 min 't0 !

	t0 a@+ -? ( - neg )( + ) sincos
	a@+ rot over *. >r *. r> swap
	a@+ rot + a@+ rot + | x y
	a@+ xy.sprite
	t0 0.5 - 1? ( drop ; )
	;

:+swap	| v1 v2 suma tiempo --
	'movcircle 'movs p!+ >a
	0 a!+ a!+	| 0 tmax
	a!+			| suma

	over 4+ @ over 4+ @
	2dup - abs 2/ a!+	| ancho
	+ 2/ a!+ 		| centro x
	over 8 + @ over 8 + @
	+ 2/ a!+ 		| centro y
	a!+			| v1
	a!+			| v2
	;

:timex
	>a
	a@+ deltat + dup a> 4 - !
	a@+ >=? ( drop a@ exec 0 ; )
	drop
	;

:+time | 'exec tiempo --
	'timex 'ti p!+ >a
	0 a!+ a!+ a! ;

|--------------
#xcam #ycam #zcam 2.0

:freelook
	xymouse
	sh 2/ - 7 << swap
	sw 2/ - neg 7 << swap
	neg mrotx
	mroty ;

:gfondo
	sw sh max 128 + qdim
	sw 2/ sh 2/ 64 - pos
	cls
	'fondo vesprite
	>xfb ;

:avanza
	dup 4+ >r
	r@ @ 0.003 +
	2.0 >? ( -2.0 nip )
|	2.0 >? ( 0 swap )
	r> !
	;

:moviautos
	[ dup " %h"  allowcr print ; ] 'objs p.mapv
	'avanza 'objs p.mapd
	;

|-----------------------------------------
#lautos 'auto1 'auto2 'auto3 'auto12 'auto5 'auto6 'auto7 'auto13

:+auto
	rand 13 >> $1c and 'lautos + @
	0.2
	-2.0 					| x
	rand 0.09 mod 0.55 +	| y
	+sprite
	2 'objs p.lastsort	| ordena por y
	;

:agregaauto
	+auto
	'agregaauto
	rand $7ff and $10 +
	+time
	;

|-----------------------------------------
:main
	33
	gfondo
	itime
	'agregaauto 10 +time

	show0 xfb>scr
		fonti
		dup "%d" print
		moviautos

		omode
|		freelook
|		msec 4 << mroty
		xcam ycam zcam mtrans

		7 0.5 setfont3d
		0 0 pos 3.0 2.0 dim home
		violeta

		dtime
		'ti p.draw

		'movs p.draw
		'objs p.draw
		'fx p.draw

		[ +auto ; ] <f1>

		'exit >esc<
		cminiflecha ;



:memoria
	mark
	inixfb
	256 'ti p.ini
	1024 'objs p.ini
	256 'movs p.ini
	1024 'fx p.ini
	;

: memoria main ;

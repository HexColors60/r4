| cuenta las verduras
| 2017 PHEDA
|-----------------
^r4/lib/gui.txt
^r4/lib/part16.txt
^r4/lib/plqlib.txt

^r4/lib/vesprite.txt
^r4/rtv/fxv.vsp

#ti 0 0
#fx 0 0
#objs 0 0
#movs 0 0


|-----------------------------
:tletra
	>b
	b@+ b@+ atxy
	b@+ ink b@+ emitf
	;

:+letra | char x y --
	'tletra 'fx p!+ >a
	swap a!+ a!+
	a!+ a!+
	;

|-----------------------------
:stsprite
	dup 7 2 << + @ 0? ( 2drop ; ) drop
	>b
	mpush
	b@+ b@+ 0 mtransi
	b@+ mrotxi
	b@+ mrotyi
	b@+ mrotzi
	b@+ dup dup mscalei
	b@+ 3dvesprite
	mpop
	;

:+sprite | 'spr size x y --
	'stsprite 'objs p!+ >a
	swap a!+ a!+		| x y
	0 0 0 a!+ a!+ a!+	| rx ry rz
	a!+					| s
	a!+					| sprite
	1 a!+				| estado
	;

:sprite.n | nro -- spr
	6 << 'objs 4+ @ + ;

:nxy.sprite | x y nro --
	sprite.n 4+ >a swap a!+ a! ;

:ns.sprite | s nro --
	sprite.n 24 + ! ;

:e.sprite | e nro --
	sprite.n 8 2 << + ! ;

:xy.sprite | x y obj --
	4+ >a swap a!+ a! ;

|-----------------------------
:drdibujo
	dup 5 2 << + @ 0? ( 2drop ; ) drop
	>b
	b@+ b@+ fpos
	b@+ %s qdim
	b@+ b@+ swap rvesprite
	;

:+dibujo | 'spr r size x y --
	'drdibujo 'objs p!+ >a
	swap a!+ a!+		| x y
	a!+					| s
	a!+ 				| r
	a!+					| sprite
	1 a!				| estado
	;

|---------- animadores
#deltat
#prevt
#t0

:itime
	msec 'prevt ! 0 'deltat ! ;

:dtime
	msec dup prevt - 'deltat ! 'prevt ! ;

:llerp | a b -- r | a + t * (b - a) | t 0.0 .. 1.0
	over - t0 16 *>> + ;
:clerp | a b -- r | a + t * (b - a) | t 0.0 .. 1.0
	over - t0 cos abs 16 *>> + ;
:slerp | a b -- r | a + t * (b - a) | t 0.0 .. 1.0
	over - t0 sin abs 16 *>> + ;

|-------------------
:movlinea
	>a
	a@+ deltat + dup a> 4 - !
	16 << a@+ / 1.0 min 't0 !
	a@+
	4+ a@+ a@+ llerp over !	| x
	4+ a@+ a@+ llerp swap !	| y
	t0 1.0 - 1? ( drop ; )
	;

:+linea | xf yf obj time --
	'movlinea 'movs p!+ >a
	0 a!+ 1000 *. a!+	| 0 tmax
	dup a!+		| obj
	| xf yf obj
	4+ dup @ a!+ rot a!+
	4+ @ a!+ a!+
	;

:movlin
	>a
	a@+ deltat + dup a> 4 - !
	16 << a@+ / 0.25 min 't0 !
	a@+
	4+ a@+ a@+ slerp over !	| x
	4+ a@+ a@+ slerp swap !	| y
	t0 0.25 - 1? ( drop ; )
	;

:+lin | xf yf obj time --
	'movlin 'movs p!+ >a
	0 a!+ 1000 *. a!+	| 0 tmax
	dup a!+		| obj
	| xf yf obj
	4+ dup @ a!+ rot a!+
	4+ @ a!+ a!+
	;

:movcircle
	>a
	a@+ deltat + dup a> 4 - !
	16 << a@+ 2* / 0.5 min 't0 !

	t0 a@+ -? ( - neg )( + ) sincos
	a@+ rot over *. >r *. r> swap
	a@+ rot + a@+ rot + | x y
	a@+ xy.sprite

	t0 0.5 - 1? ( drop ; )
	;

:+swap	| v1 v2 suma tiempo --
	'movcircle 'movs p!+ >a
	0 a!+ a!+	| 0 tmax
	a!+			| suma

	over 4+ @ over 4+ @
	2dup - abs 2/ a!+	| ancho
	+ 2/ a!+ 		| centro x
	over 8 + @ over 8 + @
	+ 2/ a!+ 		| centro y
	a!+			| v1
	a!+			| v2
	;

:timex
	>a
	a@+ deltat + dup a> 4 - !
	a@+ >=? ( drop a@ exec 0 ; )
	drop ;

:+time | 'exec tiempo --
	'timex 'ti p!+ >a
	0 a!+ a!+ a!  ;


#lver svg0 svg1 svg2 svg3 svg4 svg5 svg6 svg7 svg8 svg9 svg10 svg11 svg12 svg13 svg14 svg15

:r1.0 rand 1.0 mod ;
:r0.2 rand 0.2 mod ;

:+verdura
	rand 8 >> $3c and 'lver + @
	r1.0
	r0.2 abs 0.2 +
	r1.0 r1.0 +dibujo ;

#cntverduras 30

:+verduras
	cntverduras ( 1? )( 1- +verdura ) drop ;

:resetg
	'objs p.clear
	+verduras
	;

|--------------
:main
	33
	itime
	+verduras
	show0 clrscr
		fonti
		dup "%d" print

		7 80 setfont
		violeta
		"Cuantas Verduras" printc

		dtime
		'ti p.draw

		'movs p.draw
		'objs p.draw

		1 0.5 setfont3d
		azul
		'fx p.draw

		[ resetg ; ] <f1>
		'exit >esc<
		cminiflecha ;



:memoria
	mark
	256 'ti p.ini
	256 'objs p.ini
	256 'movs p.ini
	1024 'fx p.ini
	;

: memoria main ;

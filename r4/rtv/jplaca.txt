| 2017 PHEDA
|-----------------
^r4/lib/gui.txt
^r4/lib/part16.txt
^r4/lib/plqlib.txt
^r4/lib/loadjpg.txt
^r4/lib/loadpng.txt
^r4/lib/loadbmp.txt


#fx 0 0
#objs 0 0
#movs 0 0

#archivo
#placas )( 64
#placas> 'placas

#pantalla )( $fff
#pantalla> 'pantalla

#fondo 0
#cletra 0
#cfondo $ffffff

:,placa | adr --
	pantalla> placas> !+ 'placas> ! ;

:,p | val --
	pantalla> !+ 'pantalla> ! ;

:reset
	'objs p.clear
	'fx p.clear

	'movs p.clear
	;

|-------------------------------------------
:2color dup $f0f0f0 and dup 4 >> or swap $f0f0f and dup 4 << or ;
:to3d 0 project3d ;
:bfill
	tx1 ty1 to3d 2dup op
	tx2 ty1 to3d pline
	tx2 ty2 to3d pline
	tx1 ty2 to3d pline pline
	poli ;

:f1	ink bfill ;			| solido
:f2	ink bfill ;			| imagen?
:f3 2color fcol tx1 ty2 to3d fcen 1.0 tx2 tx1 to3d - 1 max / 0 fmat lfill bfill sfill ;
:f4 2color fcol tx1 ty2 to3d fcen 0 1.0 ty2 ty1 to3d - 1 max / fmat lfill bfill sfill ;
:f5 2color fcol tx1 ty1 to3d fcen 0.5 tx2 tx1 to3d - 1 max / 0.5 ty2 ty1 to3d - 1 max / neg fmat lfill bfill sfill ;
:f6 2color fcol tx1 ty2 to3d fcen 0.5 tx2 tx1 to3d - 1 max / 0.5 ty2 ty1 to3d - 1 max / fmat lfill bfill sfill ;
:f0 drop ;	| auxiliar

#lbfill f0 f1 f2 f3 f4 f5 f6 f0

:3dfillbox | nro --
	dup 28 >> 7 and 2 << 'lbfill + @ exec ;

:fillbox3d | x y x2 y2 --
	2over to3d op
	pick3 over to3d pline
	2dup to3d pline
	drop over to3d pline
	to3d pline
	poli ;

|-------------------------------------------
:tcar | adr --
	dup @ 1- 0? ( nip ; )
	|dup 2* alpha
	swap !+ >r
	r@+ r@+ pos
	r@+ r@+ dim home
	r@+ 3dfillbox
	r@+ dup 16 << 16 >> swap 16 >> boxpad
	r@+ r@+ setfont3d
	r@+ r@+ dup ink 24 >> boxprintn
	rdrop ;


:+cartel | "" w h x y  --
	'tcar 'fx p!+ >r
	128 r!+ | vida
	swap r!+ r!+ swap r!+ r!+

	0 r!+ | fondo
	0 r!+ | pad
	9 r!+ | font
	0.4 r!+ | size
	r!+
	$600ff00 r!+
	rdrop	;

|-----------------------------
:tletra
	>r
	r@+ r@+ pos 0.8 qdim home
	0 0.05 boxpad
	1 0.5 setfont3d
	blanco
	r@+ 64 + "%k" mprint 6 boxprintn
	rdrop
	;

:+letra | nro x y --
	'tletra 'fx p!+ >r
	swap r!+ r!+
	r!+
	rdrop
	;

|-----------------------------
:stsprite
	>r
	mpush
	r@+ r@+ 0 mtransi
	r@+ 1? ( mrotxi )( drop )
	r@+ 1? ( mrotyi )( drop )
	r@+ 1? ( mrotzi )( drop )
	r@+ dup dup mscalei
	r@+ 3dnsprite
	rdrop
	mpop
	;

:+sprite | 'spr size x y --
	'stsprite 'objs p!+ >r
	swap r!+ r!+		| x y
	0 0 0 r!+ r!+ r!+	| rx ry rz
	r!+					| s
	r!+					| sprite
	rdrop ;

:sprite.n | nro -- spr
	6 << 'objs 4+ @ + ;

:nxy.sprite | x y nro --
	sprite.n 4+ >r swap r!+ r!+ rdrop ;

:xy.sprite | x y obj --
	4+ >r swap r!+ r!+ rdrop ;


|---------- animadores
#deltat
#prevt
#t0

:itime
	msec 'prevt ! 0 'deltat ! ;

:dtime
	msec dup prevt - 'deltat ! 'prevt ! ;

:llerp | a b -- r | a + t * (b - a) | t 0.0 .. 1.0
	over - t0 16 *>> + ;
:clerp | a b -- r | a + t * (b - a) | t 0.0 .. 1.0
	over - t0 cos abs 16 *>> + ;
:slerp | a b -- r | a + t * (b - a) | t 0.0 .. 1.0
	over - t0 sin abs 16 *>> + ;


:movlinea
	>r
	r@+ deltat + dup r 4 - !
	16 << r@+ / 1.0 min 't0 !
	r@+
	4+ r@+ r@+ llerp over !	| x
	4+ r@+ r@+ llerp swap !	| y
	rdrop
	t0 1.0 - 1? ( drop ; )
	;

:+linea | xf yf obj time --
	'movlinea 'movs p!+ >r
	0 r!+ r!+	| 0 tmax
	dup r!+		| obj
	| xf yf obj
	4+ dup @ r!+ rot r!+
	4+ @ r!+ r!+
	rdrop
	;

:movcircle
	>r
	r@+ deltat + dup r 4 - !
	16 << r@+ 2* / 0.5 min 't0 !

	t0 r@+ -? ( - neg )( + ) sincos
	r@+ rot over *. >r *. r> swap
	r@+ rot + r@+ rot + | x y
	r@+ xy.sprite

	rdrop
	t0 0.5 - 1? ( drop ; )
	;

:+swap	| v1 v2 suma tiempo --
	'movcircle 'movs p!+ >r
	0 r!+ r!+	| 0 tmax
	r!+			| suma

	over 4+ @ over 4+ @
	2dup - abs 2/ r!+	| ancho
	+ 2/ r!+ 		| centro x
	over 8 + @ over 8 + @
	+ 2/ r!+ 		| centro y
	r!+			| v1
	r!+			| v2
	rdrop ;

:timex
	>r
	r@+ deltat + dup r 4 - !
	r@+ >=? ( drop r> @ exec 0 ; )
	drop
	rdrop ;

:+time | 'exec tiempo --
	'timex 'fx p!+ >r
	0 r!+ r!+ r!+
	rdrop ;

#sizefnt
#typefnt
#xxc #yyc
#xxm #yym

|-------------------------------------------
| pantalla
| imagen
| sprite
| video
| fx
|

| texto -- color x y fnt size "texto"
:t0	8 >> ink
	>r
	r@+ r@+ atxy
	r@+ r@+ setfont3d
	r> @ print ;

| caja -- color x y x y
:t1 8 >> ink
	>r
	r@+ r@+ r@+ r@+ fillbox3d
	rdrop ;

:t2
:t3
:t4
:t5
:t6
:t7
:t8 2drop ;

#ldraw t0 t1 t2 t3 t4 t5 t6 t7 t8 t8 t8 t8 t8 t8 t8 t8

:drawbox
	@+ dup $f and 2 << 'ldraw + @ exec ;

:drawpant | adr --
	0 0 pos 4.0 2.0 dim home
|	$40058000 3dfillbox
	@+ swap @ swap
	( over <? )(
		dup drawbox
		32 + ) 2drop ;

|---------------------------------------


:startpant
	-1.6 dup 'xxc ! 'xxm !
	-1.0 dup 'yyc ! 'yym !
	;

:endpant
	startpant
	;
:endbox

	;

:>>l | adr -- adr'/0
	( c@+ $ff and 31 >? )( drop )
	0? ( drop 1- 0 over c! ; ) drop
	0 over 1- c!
	;

:espag	|@@
	>>l
	,placa
	endpant ;

:eslin 	|--
	$01 cletra 8 << or ,p
	xxc ,p yyc 0.1 + ,p
	0.1 'yyc +!
	xxc 3.2 + ,p yyc 0.01 + ,p
	0 ,p 0 ,p 0 ,p
	>>l ;

:eshead	|#..
	0 swap ( dup c@ $23 =? )( drop 1+ swap 1+ swap ) drop
	swap 0.4 swap 0.05 * -
	cletra 8 << ,p
	xxc ,p yyc ,p
	dup 2/ 'yyc +!
	13 ,p ,p
	dup ,p
	0 ,p
	0 ,p
	>>l ;

:escuadro |<
:eslista |>
	cletra 8 << ,p
	xxc ,p yyc ,p
	11 ,p 0.15 ,p
	dup ,p
	0 ,p
	0 ,p
	0.1 'yyc +!
	>>l
	;

:esbold |*
	;
:escolumna |_
	;

:simg |[
:eimg |]
:stemp |{
:establa ||
:etemp |}
	;
:endmark | cr
	0 over 1- c!
	0.1 'yyc +!
	;

:setimg | adr -- adr'
	dup 'fondo ! >>l ;

:setfon | adr -- adr'
	str$>nro 'cfondo ! trim ;

:setlet | adr -- adr'
	str$>nro 'cletra ! trim ;


| iatv/coso.p.jpg	| imagen de fondo
| fff0000			| color de fondo
| l00ff00			| color de letra
:parseprev | adr -- adr'
	0 'fondo !
	( c@+ 1? )(
    	$40 =? ( drop 1- ; )
    	$69 =? ( drop setimg 0 ) | i
    	$66 =? ( drop setfon 0 ) | f
    	$6c =? ( drop setlet 0 ) | l
    	drop
		)
	drop 1- ;
	;
|----------------------------
| @@ cambio de pantalla
| -- linea separadora
| #.. header hasta cr
| | 	tabla
| < 	cuadro
| >  	lista

| *		bold
| _ 	columna

| []	imagen
| {}	template
|----------------------------
:parsec | adr c -- adr
	$40 =? ( drop dup c@ $40 =? ( drop espag ; ) ) |@@
	$2d =? ( drop dup c@ $2d =? ( drop eslin ; ) ) |--
	$23 =? ( drop eshead ; )	|#..

	$2a =? ( drop esbold ; )	|*
	$3c =? ( drop escuadro ; )	|<
	$3e =? ( drop eslista ; )	|>
	$5b =? ( drop simg ; )		|[
	$5d =? ( drop eimg ; )		|]
	$5f =? ( drop escolumna ; ) |_
	$7b =? ( drop stemp ; )		|{
	$7c =? ( drop establa ; )	||
	$7d =? ( drop etemp ; ) 	|}
	13 =? ( drop endmark ; )
	drop
	;

:quita10 | mem -- endmem
	dup
	( c@+ 1? )(
		13 =? ( over c@ 10 =? ( drop swap 1+ swap )( drop ) )
		10 =? ( drop c@+ 13 <>? ( drop 1- 13 ) )
		rot c!+ swap )
	drop swap c!+ ;

:loadimg | filename -- img
	".jpg" =pos 1? ( drop loadjpg ; ) drop
	".png" =pos 1? ( drop loadpng ; ) drop
	".bmp" =pos 1? ( drop loadbmp ; )
	2drop 0 ;

:loadparse | "" --
	here dup 'archivo !
	swap load 0 swap !
	archivo quita10
	'here !
	archivo
	parseprev
	( c@+ 1? )( parsec )
	2drop
	,placa
	fondo 0? ( drop ; ) loadimg 'fondo !
	;

|--------------
#xcam #ycam #zcam 2.0
#nplaca 0

:freelook
	xymouse
	sh 2/ - 7 << swap
	sw 2/ - neg 7 << swap
	neg mrotx
	mroty ;


:main
	33
	itime

	cfondo paper
	fondo 1? ( 0 0 sw sh fondo img.drawsize )( cls )
	drop
	>XFB

	show xfb>scr
|		fonti
|		dup "%d" print

		omode
|		freelook
|		msec 4 << mrotx
		xcam ycam zcam mtrans

		7 0.2 setfont3d
		0 0 pos 3.0 2.0 dim home
		blanco

|        0 0 boxpad 'placas nplaca 2 << + @ 5 boxprintn
		1 0.4 setfont3d
		'placas nplaca 2 << + drawpant

		dtime
		'movs p.draw

		[ "Cartel f2" 1.0 0.5 0 0 +cartel ; ] <f2>
  		[ 1 'nplaca +! ; ] <f1>
		[ 1 'typefnt +! ; ] <f2>

		'exit >esc<
		cminiflecha ;

:memoria
	mark
	256 'objs p.ini
	256 'movs p.ini
	1024 'fx p.ini

|	"atv/urgente.txt" loadparse
	"atv/profesionalesmedicos.txt" loadparse
	;

: memoria main ;
